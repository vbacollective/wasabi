Attribute VB_Name = "Wasabi"
' ============================================================================
' Wasabi v2.3.5-beta
' Copyright (c) 2026 UesleiDev
'
' Permission is hereby granted, free of charge, to any person obtaining a
' copy of this software and associated documentation files (the "Software"),
' to deal in the Software without restriction, including without limitation
' the rights to use, copy, modify, merge, publish, distribute, sublicense,
' and/or sell copies of the Software, and to permit persons to whom the
' Software is furnished to do so, subject to the following conditions:
'
' The above copyright notice and this permission notice shall be included in
' all copies or substantial portions of the Software.
'
' THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
' IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
' FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
' AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
' LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
' FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
' DEALINGS IN THE SOFTWARE.
' ============================================================================

Option Explicit
Option Private Module

#If VBA7 Then
    Private Declare PtrSafe Function WinHttpGetIEProxyConfigForCurrentUser Lib "winhttp.dll" (ByRef pProxyConfig As WINHTTP_CURRENT_USER_IE_PROXY_CONFIG) As Long
    Private Declare PtrSafe Function GlobalFree Lib "kernel32" (ByVal hMem As LongPtr) As LongPtr
    Private Declare PtrSafe Function lstrlenW Lib "kernel32" (ByVal lpString As LongPtr) As Long
    Private Declare PtrSafe Function CryptAcquireContextW Lib "advapi32.dll" (ByRef phProv As LongPtr, ByVal pszContainer As LongPtr, ByVal pszProvider As LongPtr, ByVal dwProvType As Long, ByVal dwFlags As Long) As Long
    Private Declare PtrSafe Function CryptCreateHash Lib "advapi32.dll" (ByVal hProv As LongPtr, ByVal Algid As Long, ByVal hKey As LongPtr, ByVal dwFlags As Long, ByRef phHash As LongPtr) As Long
    Private Declare PtrSafe Function CryptHashData Lib "advapi32.dll" (ByVal hHash As LongPtr, ByRef pbData As Byte, ByVal dwDataLen As Long, ByVal dwFlags As Long) As Long
    Private Declare PtrSafe Function CryptGetHashParam Lib "advapi32.dll" (ByVal hHash As LongPtr, ByVal dwParam As Long, ByRef pbData As Byte, ByRef pdwDataLen As Long, ByVal dwFlags As Long) As Long
    Private Declare PtrSafe Function CryptDestroyHash Lib "advapi32.dll" (ByVal hHash As LongPtr) As Long
    Private Declare PtrSafe Function CryptReleaseContext Lib "advapi32.dll" (ByVal hProv As LongPtr, ByVal dwFlags As Long) As Long
    Private Declare PtrSafe Function CryptBinaryToStringW Lib "crypt32.dll" (ByVal pbBinary As LongPtr, ByVal cbBinary As Long, ByVal dwFlags As Long, ByVal pszString As LongPtr, ByRef pcchString As Long) As Long
    Private Declare PtrSafe Function LoadLibrary Lib "kernel32" Alias "LoadLibraryA" (ByVal lpLibFileName As String) As LongPtr
    Private Declare PtrSafe Function CertGetCertificateChain Lib "crypt32.dll" (ByVal hChainEngine As LongPtr, ByVal pCertContext As LongPtr, ByVal pTime As LongPtr, ByVal hAdditionalStore As LongPtr, ByRef pChainPara As CERT_CHAIN_PARA, ByVal dwFlags As Long, ByVal pvReserved As LongPtr, ByRef ppChainContext As LongPtr) As Long
    Private Declare PtrSafe Function CertVerifyCertificateChainPolicy Lib "crypt32.dll" (ByVal pszPolicyOID As LongPtr, ByVal pChainContext As LongPtr, ByRef pPolicyPara As CERT_CHAIN_POLICY_PARA, ByRef pPolicyStatus As CERT_CHAIN_POLICY_STATUS) As Long
    Private Declare PtrSafe Sub CertFreeCertificateChain Lib "crypt32.dll" (ByVal pChainContext As LongPtr)
    Private Declare PtrSafe Function CertOpenStore Lib "crypt32.dll" (ByVal lpszStoreProvider As LongPtr, ByVal dwEncodingType As Long, ByVal hCryptProv As LongPtr, ByVal dwFlags As Long, ByVal pvPara As LongPtr) As LongPtr
    Private Declare PtrSafe Function CertFindCertificateInStore Lib "crypt32.dll" (ByVal hCertStore As LongPtr, ByVal dwCertEncodingType As Long, ByVal dwFindFlags As Long, ByVal dwFindType As Long, ByRef pvFindPara As Any, ByVal pPrevCertContext As LongPtr) As LongPtr
    Private Declare PtrSafe Function CertCloseStore Lib "crypt32.dll" (ByVal hCertStore As LongPtr, ByVal dwFlags As Long) As Long
    Private Declare PtrSafe Function PFXImportCertStore Lib "crypt32.dll" (ByRef pPFX As CRYPT_DATA_BLOB, ByVal szPassword As LongPtr, ByVal dwFlags As Long) As LongPtr
    Private Declare PtrSafe Function CertFreeCertificateContext Lib "crypt32.dll" (ByVal pCertContext As LongPtr) As Long
    Private Declare PtrSafe Function AcquireCredentialsHandle Lib "secur32.dll" Alias "AcquireCredentialsHandleA" (ByVal pszPrincipal As LongPtr, ByVal pszPackage As String, ByVal fCredentialUse As Long, ByVal pvLogonID As LongPtr, ByRef pAuthData As Any, ByVal pGetKeyFn As LongPtr, ByVal pvGetKeyArgument As LongPtr, ByRef phCredential As SecHandle, ByRef ptsExpiry As SECURITY_INTEGER) As Long
    Private Declare PtrSafe Function FreeCredentialsHandle Lib "secur32.dll" (ByRef phCredential As SecHandle) As Long
    Private Declare PtrSafe Function InitializeSecurityContext Lib "secur32.dll" Alias "InitializeSecurityContextA" (ByRef phCredential As SecHandle, ByVal phContext As LongPtr, ByVal pszTargetName As String, ByVal fContextReq As Long, ByVal Reserved1 As Long, ByVal TargetDataRep As Long, ByVal pInput As LongPtr, ByVal Reserved2 As Long, ByRef phNewContext As SecHandle, ByRef pOutput As SecBufferDesc, ByRef pfContextAttr As Long, ByRef ptsExpiry As SECURITY_INTEGER) As Long
    Private Declare PtrSafe Function InitializeSecurityContextContinue Lib "secur32.dll" Alias "InitializeSecurityContextA" (ByRef phCredential As SecHandle, ByRef phContext As SecHandle, ByVal pszTargetName As String, ByVal fContextReq As Long, ByVal Reserved1 As Long, ByVal TargetDataRep As Long, ByRef pInput As SecBufferDesc, ByVal Reserved2 As Long, ByRef phNewContext As SecHandle, ByRef pOutput As SecBufferDesc, ByRef pfContextAttr As Long, ByRef ptsExpiry As SECURITY_INTEGER) As Long
    Private Declare PtrSafe Function DeleteSecurityContext Lib "secur32.dll" (ByRef phContext As SecHandle) As Long
    Private Declare PtrSafe Function FreeContextBuffer Lib "secur32.dll" (ByVal pvContextBuffer As LongPtr) As Long
    Private Declare PtrSafe Function QueryContextAttributes Lib "secur32.dll" Alias "QueryContextAttributesA" (ByRef phContext As SecHandle, ByVal ulAttribute As Long, ByRef pBuffer As Any) As Long
    Private Declare PtrSafe Function EncryptMessage Lib "secur32.dll" (ByRef phContext As SecHandle, ByVal fQOP As Long, ByRef pMessage As SecBufferDesc, ByVal MessageSeqNo As Long) As Long
    Private Declare PtrSafe Function DecryptMessage Lib "secur32.dll" (ByRef phContext As SecHandle, ByRef pMessage As SecBufferDesc, ByVal MessageSeqNo As Long, ByRef pfQOP As Long) As Long
    Private Declare PtrSafe Function WSAStartup Lib "ws2_32.dll" (ByVal wVersionRequested As Integer, ByRef lpWSAData As WSADATA) As Long
    Private Declare PtrSafe Function WSACleanup Lib "ws2_32.dll" () As Long
    Private Declare PtrSafe Function WSAGetLastError Lib "ws2_32.dll" () As Long
    Private Declare PtrSafe Function sock_getsockopt Lib "ws2_32.dll" Alias "getsockopt" (ByVal s As LongPtr, ByVal level As Long, ByVal optname As Long, ByRef optVal As Long, ByRef optlen As Long) As Long
    Private Declare PtrSafe Function sock_getaddrinfo Lib "ws2_32.dll" Alias "getaddrinfo" (ByVal pNodeName As String, ByVal pServiceName As String, ByVal pHints As LongPtr, ByRef ppResult As LongPtr) As Long
    Private Declare PtrSafe Sub sock_freeaddrinfo Lib "ws2_32.dll" Alias "freeaddrinfo" (ByVal pAddrInfo As LongPtr)
    Private Declare PtrSafe Function sock_socket Lib "ws2_32.dll" Alias "socket" (ByVal af As Long, ByVal socktype As Long, ByVal protocol As Long) As LongPtr
    Private Declare PtrSafe Function sock_closesocket Lib "ws2_32.dll" Alias "closesocket" (ByVal s As LongPtr) As Long
    Private Declare PtrSafe Function sock_connect Lib "ws2_32.dll" Alias "connect" (ByVal s As LongPtr, ByVal name As LongPtr, ByVal namelen As Long) As Long
    Private Declare PtrSafe Function sock_send Lib "ws2_32.dll" Alias "send" (ByVal s As LongPtr, ByRef buf As Byte, ByVal bufLen As Long, ByVal flags As Long) As Long
    Private Declare PtrSafe Function sock_recv Lib "ws2_32.dll" Alias "recv" (ByVal s As LongPtr, ByRef buf As Byte, ByVal bufLen As Long, ByVal flags As Long) As Long
    Private Declare PtrSafe Function sock_htons Lib "ws2_32.dll" Alias "htons" (ByVal hostshort As Long) As Integer
    Private Declare PtrSafe Function sock_gethostbyname Lib "ws2_32.dll" Alias "gethostbyname" (ByVal hostname As String) As LongPtr
    Private Declare PtrSafe Function sock_inet_addr Lib "ws2_32.dll" Alias "inet_addr" (ByVal cp As String) As Long
    Private Declare PtrSafe Function sock_ioctlsocket Lib "ws2_32.dll" Alias "ioctlsocket" (ByVal s As LongPtr, ByVal cmd As Long, ByRef argp As Long) As Long
    Private Declare PtrSafe Function sock_select Lib "ws2_32.dll" Alias "select" (ByVal nfds As Long, ByRef readfds As Any, ByRef writefds As Any, ByRef exceptfds As Any, ByRef TIMEOUT As TIMEVAL) As Long
    Private Declare PtrSafe Function sock_setsockopt Lib "ws2_32.dll" Alias "setsockopt" (ByVal s As LongPtr, ByVal level As Long, ByVal optname As Long, ByRef optVal As Long, ByVal optlen As Long) As Long
    Private Declare PtrSafe Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" (ByRef dest As Any, ByRef src As Any, ByVal size As Long)
    Private Declare PtrSafe Sub CopyMemoryFromPtr Lib "kernel32" Alias "RtlMoveMemory" (ByRef dest As Any, ByVal src As LongPtr, ByVal size As Long)
    Private Declare PtrSafe Function MultiByteToWideChar Lib "kernel32" (ByVal CodePage As Long, ByVal dwFlags As Long, ByRef lpMultiByteStr As Byte, ByVal cchMultiByte As Long, ByVal lpWideCharStr As LongPtr, ByVal cchWideChar As Long) As Long
    Private Declare PtrSafe Function WideCharToMultiByte Lib "kernel32" (ByVal CodePage As Long, ByVal dwFlags As Long, ByVal lpWideCharStr As LongPtr, ByVal cchWideChar As Long, ByRef lpMultiByteStr As Byte, ByVal cchMultiByte As Long, ByVal lpDefaultChar As LongPtr, ByVal lpUsedDefaultChar As LongPtr) As Long
    Private Declare PtrSafe Function GetTickCount Lib "kernel32" () As Long
    Private Declare PtrSafe Function RtlGenRandom Lib "advapi32.dll" Alias "SystemFunction036" (ByVal RandomBuffer As LongPtr, ByVal RandomBufferLength As Long) As Long
    Private Declare PtrSafe Function VarPtrArray Lib "VBE7" Alias "VarPtr" (ByRef arr() As Byte) As LongPtr
    Private Declare PtrSafe Function VirtualAlloc Lib "kernel32" (ByVal lpAddress As LongPtr, ByVal dwSize As LongPtr, ByVal flAllocationType As Long, ByVal flProtect As Long) As LongPtr
    Private Declare PtrSafe Function VirtualFree Lib "kernel32" (ByVal lpAddress As LongPtr, ByVal dwSize As LongPtr, ByVal dwFreeType As Long) As Long
    Private Declare PtrSafe Function CallWindowProcW Lib "user32" (ByVal lpPrevWndFunc As LongPtr, ByVal P1 As LongPtr, ByVal P2 As LongPtr, ByVal P3 As LongPtr, ByVal P4 As LongPtr) As LongPtr
    
    Private m_ptrWsMask As LongPtr
    Private m_ptrMemZero As LongPtr
    Private m_ptrMemFind As LongPtr
    Private m_ptrTickDiff As LongPtr
    
    Private Const NULL_PTR As LongPtr = 0
#Else
    Private Declare Function WinHttpGetIEProxyConfigForCurrentUser Lib "winhttp.dll" (ByRef pProxyConfig As WINHTTP_CURRENT_USER_IE_PROXY_CONFIG) As Long
    Private Declare Function GlobalFree Lib "kernel32" (ByVal hMem As Long) As Long
    Private Declare Function lstrlenW Lib "kernel32" (ByVal lpString As Long) As Long
    Private Declare Function CryptAcquireContextW Lib "advapi32.dll" (ByRef phProv As Long, ByVal pszContainer As Long, ByVal pszProvider As Long, ByVal dwProvType As Long, ByVal dwFlags As Long) As Long
    Private Declare Function CryptCreateHash Lib "advapi32.dll" (ByVal hProv As Long, ByVal Algid As Long, ByVal hKey As Long, ByVal dwFlags As Long, ByRef phHash As Long) As Long
    Private Declare Function CryptHashData Lib "advapi32.dll" (ByVal hHash As Long, ByRef pbData As Byte, ByVal dwDataLen As Long, ByVal dwFlags As Long) As Long
    Private Declare Function CryptGetHashParam Lib "advapi32.dll" (ByVal hHash As Long, ByVal dwParam As Long, ByRef pbData As Byte, ByRef pdwDataLen As Long, ByVal dwFlags As Long) As Long
    Private Declare Function CryptDestroyHash Lib "advapi32.dll" (ByVal hHash As Long) As Long
    Private Declare Function CryptReleaseContext Lib "advapi32.dll" (ByVal hProv As Long, ByVal dwFlags As Long) As Long
    Private Declare Function CryptBinaryToStringW Lib "crypt32.dll" (ByVal pbBinary As Long, ByVal cbBinary As Long, ByVal dwFlags As Long, ByVal pszString As Long, ByRef pcchString As Long) As Long
    Private Declare Function LoadLibrary Lib "kernel32" Alias "LoadLibraryA" (ByVal lpLibFileName As String) As Long
    Private Declare Function CertGetCertificateChain Lib "crypt32.dll" (ByVal hChainEngine As Long, ByVal pCertContext As Long, ByVal pTime As Long, ByVal hAdditionalStore As Long, ByRef pChainPara As CERT_CHAIN_PARA, ByVal dwFlags As Long, ByVal pvReserved As Long, ByRef ppChainContext As Long) As Long
    Private Declare Function CertVerifyCertificateChainPolicy Lib "crypt32.dll" (ByVal pszPolicyOID As Long, ByVal pChainContext As Long, ByRef pPolicyPara As CERT_CHAIN_POLICY_PARA, ByRef pPolicyStatus As CERT_CHAIN_POLICY_STATUS) As Long
    Private Declare Sub CertFreeCertificateChain Lib "crypt32.dll" (ByVal pChainContext As Long)
    Private Declare Function CertOpenStore Lib "crypt32.dll" (ByVal lpszStoreProvider As Long, ByVal dwEncodingType As Long, ByVal hCryptProv As Long, ByVal dwFlags As Long, ByVal pvPara As Long) As Long
    Private Declare Function CertFindCertificateInStore Lib "crypt32.dll" (ByVal hCertStore As Long, ByVal dwCertEncodingType As Long, ByVal dwFindFlags As Long, ByVal dwFindType As Long, ByRef pvFindPara As Any, ByVal pPrevCertContext As Long) As Long
    Private Declare Function CertCloseStore Lib "crypt32.dll" (ByVal hCertStore As Long, ByVal dwFlags As Long) As Long
    Private Declare Function PFXImportCertStore Lib "crypt32.dll" (ByRef pPFX As CRYPT_DATA_BLOB, ByVal szPassword As Long, ByVal dwFlags As Long) As Long
    Private Declare Function CertFreeCertificateContext Lib "crypt32.dll" (ByVal pCertContext As Long) As Long
    Private Declare Function AcquireCredentialsHandle Lib "secur32.dll" Alias "AcquireCredentialsHandleA" (ByVal pszPrincipal As Long, ByVal pszPackage As String, ByVal fCredentialUse As Long, ByVal pvLogonID As Long, ByRef pAuthData As Any, ByVal pGetKeyFn As Long, ByVal pvGetKeyArgument As Long, ByRef phCredential As SecHandle, ByRef ptsExpiry As SECURITY_INTEGER) As Long
    Private Declare Function FreeCredentialsHandle Lib "secur32.dll" (ByRef phCredential As SecHandle) As Long
    Private Declare Function InitializeSecurityContext Lib "secur32.dll" Alias "InitializeSecurityContextA" (ByRef phCredential As SecHandle, ByVal phContext As Long, ByVal pszTargetName As String, ByVal fContextReq As Long, ByVal Reserved1 As Long, ByVal TargetDataRep As Long, ByVal pInput As Long, ByVal Reserved2 As Long, ByRef phNewContext As SecHandle, ByRef pOutput As SecBufferDesc, ByRef pfContextAttr As Long, ByRef ptsExpiry As SECURITY_INTEGER) As Long
    Private Declare Function InitializeSecurityContextContinue Lib "secur32.dll" Alias "InitializeSecurityContextA" (ByRef phCredential As SecHandle, ByRef phContext As SecHandle, ByVal pszTargetName As String, ByVal fContextReq As Long, ByVal Reserved1 As Long, ByVal TargetDataRep As Long, ByRef pInput As SecBufferDesc, ByVal Reserved2 As Long, ByRef phNewContext As SecHandle, ByRef pOutput As SecBufferDesc, ByRef pfContextAttr As Long, ByRef ptsExpiry As SECURITY_INTEGER) As Long
    Private Declare Function DeleteSecurityContext Lib "secur32.dll" (ByRef phContext As SecHandle) As Long
    Private Declare Function FreeContextBuffer Lib "secur32.dll" (ByVal pvContextBuffer As Long) As Long
    Private Declare Function QueryContextAttributes Lib "secur32.dll" Alias "QueryContextAttributesA" (ByRef phContext As SecHandle, ByVal ulAttribute As Long, ByRef pBuffer As Any) As Long
    Private Declare Function EncryptMessage Lib "secur32.dll" (ByRef phContext As SecHandle, ByVal fQOP As Long, ByRef pMessage As SecBufferDesc, ByVal MessageSeqNo As Long) As Long
    Private Declare Function DecryptMessage Lib "secur32.dll" (ByRef phContext As SecHandle, ByRef pMessage As SecBufferDesc, ByVal MessageSeqNo As Long, ByRef pfQOP As Long) As Long
    Private Declare Function WSAStartup Lib "ws2_32.dll" (ByVal wVersionRequested As Integer, ByRef lpWSAData As WSADATA) As Long
    Private Declare Function WSACleanup Lib "ws2_32.dll" () As Long
    Private Declare Function WSAGetLastError Lib "ws2_32.dll" () As Long
    Private Declare Function sock_getsockopt Lib "ws2_32.dll" Alias "getsockopt" (ByVal s As Long, ByVal level As Long, ByVal optname As Long, ByRef optVal As Long, ByRef optlen As Long) As Long
    Private Declare Function sock_getaddrinfo Lib "ws2_32.dll" Alias "getaddrinfo" (ByVal pNodeName As String, ByVal pServiceName As String, ByVal pHints As Long, ByRef ppResult As Long) As Long
    Private Declare Sub sock_freeaddrinfo Lib "ws2_32.dll" Alias "freeaddrinfo" (ByVal pAddrInfo As Long)
    Private Declare Function sock_socket Lib "ws2_32.dll" Alias "socket" (ByVal af As Long, ByVal socktype As Long, ByVal protocol As Long) As Long
    Private Declare Function sock_closesocket Lib "ws2_32.dll" Alias "closesocket" (ByVal s As Long) As Long
    Private Declare Function sock_connect Lib "ws2_32.dll" Alias "connect" (ByVal s As Long, ByVal name As Long, ByVal namelen As Long) As Long
    Private Declare Function sock_send Lib "ws2_32.dll" Alias "send" (ByVal s As Long, ByRef buf As Byte, ByVal buflen As Long, ByVal flags As Long) As Long
    Private Declare Function sock_recv Lib "ws2_32.dll" Alias "recv" (ByVal s As Long, ByRef buf As Byte, ByVal buflen As Long, ByVal flags As Long) As Long
    Private Declare Function sock_htons Lib "ws2_32.dll" Alias "htons" (ByVal hostshort As Long) As Integer
    Private Declare Function sock_gethostbyname Lib "ws2_32.dll" Alias "gethostbyname" (ByVal hostname As String) As Long
    Private Declare Function sock_inet_addr Lib "ws2_32.dll" Alias "inet_addr" (ByVal cp As String) As Long
    Private Declare Function sock_ioctlsocket Lib "ws2_32.dll" Alias "ioctlsocket" (ByVal s As Long, ByVal cmd As Long, ByRef argp As Long) As Long
    Private Declare Function sock_select Lib "ws2_32.dll" Alias "select" (ByVal nfds As Long, ByRef readfds As Any, ByRef writefds As Any, ByRef exceptfds As Any, ByRef timeout As TIMEVAL) As Long
    Private Declare Function sock_setsockopt Lib "ws2_32.dll" Alias "setsockopt" (ByVal s As Long, ByVal level As Long, ByVal optname As Long, ByRef optval As Long, ByVal optlen As Long) As Long
    Private Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" (ByRef dest As Any, ByRef src As Any, ByVal size As Long)
    Private Declare Sub CopyMemoryFromPtr Lib "kernel32" Alias "RtlMoveMemory" (ByRef dest As Any, ByVal src As Long, ByVal size As Long)
    Private Declare Function MultiByteToWideChar Lib "kernel32" (ByVal CodePage As Long, ByVal dwFlags As Long, ByRef lpMultiByteStr As Byte, ByVal cchMultiByte As Long, ByVal lpWideCharStr As Long, ByVal cchWideChar As Long) As Long
    Private Declare Function WideCharToMultiByte Lib "kernel32" (ByVal CodePage As Long, ByVal dwFlags As Long, ByVal lpWideCharStr As Long, ByVal cchWideChar As Long, ByRef lpMultiByteStr As Byte, ByVal cchMultiByte As Long, ByVal lpDefaultChar As Long, ByVal lpUsedDefaultChar As Long) As Long
    Private Declare Function GetTickCount Lib "kernel32" () As Long
    Private Declare Function RtlGenRandom Lib "advapi32.dll" Alias "SystemFunction036" (ByVal RandomBuffer As Long, ByVal RandomBufferLength As Long) As Long
    Private Declare Function VarPtrArray Lib "VBE6" Alias "VarPtr" (ByRef arr() As Byte) As Long
    Private Declare Function VirtualAlloc Lib "kernel32" (ByVal lpAddress As Long, ByVal dwSize As Long, ByVal flAllocationType As Long, ByVal flProtect As Long) As Long
    Private Declare Function VirtualFree Lib "kernel32" (ByVal lpAddress As Long, ByVal dwSize As Long, ByVal dwFreeType As Long) As Long
    Private Declare Function CallWindowProcW Lib "user32" (ByVal lpPrevWndFunc As Long, ByVal P1 As Long, ByVal P2 As Long, ByVal P3 As Long, ByVal P4 As Long) As Long
    
    Private m_ptrWsMask As Long
    Private m_ptrMemZero As Long
    Private m_ptrMemFind As Long
    Private m_ptrTickDiff As Long
    
    Private Const NULL_PTR As Long = 0
#End If

Private Const TCP_MAXSEG As Long = 4

#If VBA7 Then
    Private Const INVALID_SOCKET As LongPtr = -1
#Else
    Private Const INVALID_SOCKET As Long = -1
#End If

Private Type CRYPT_DATA_BLOB
#If VBA7 Then
    cbData As Long
    pbData As LongPtr
#Else
    cbData As Long
    pbData As Long
#End If
End Type

Private Type CERT_ENHKEY_USAGE
    cUsageIdentifier As Long
#If VBA7 Then
    rgpszUsageIdentifier As LongPtr
#Else
    rgpszUsageIdentifier As Long
#End If
End Type

Private Type CERT_USAGE_MATCH
    dwType As Long
    Usage As Long
End Type

Private Type CERT_CHAIN_PARA
    cbSize As Long
    RequestedUsage_dwType As Long
    RequestedUsage_cUsage As Long
#If VBA7 Then
    RequestedUsage_rgpOID As LongPtr
#Else
    RequestedUsage_rgpOID As Long
#End If
End Type

Private Type WINHTTP_CURRENT_USER_IE_PROXY_CONFIG
    fAutoDetect As Long
#If VBA7 Then
    lpszAutoConfigUrl As LongPtr
    lpszProxy As LongPtr
    lpszProxyBypass As LongPtr
#Else
    lpszAutoConfigUrl As Long
    lpszProxy As Long
    lpszProxyBypass As Long
#End If
End Type

Private Type SSL_EXTRA_CERT_CHAIN_POLICY_PARA
    cbSize As Long
    dwAuthType As Long
    fdwChecks As Long
#If VBA7 Then
    pwszServerName As LongPtr
#Else
    pwszServerName As Long
#End If
End Type

Private Type CERT_CHAIN_POLICY_PARA
    cbSize As Long
    dwFlags As Long
#If VBA7 Then
    pvExtraPolicyPara As LongPtr
#Else
    pvExtraPolicyPara As Long
#End If
End Type

Private Type CERT_CHAIN_POLICY_STATUS
    cbSize As Long
    dwError As Long
    lChainIndex As Long
    lElementIndex As Long
End Type

Private Type BatchBuffer
    Frames() As Byte
    FrameCount As Long
    totalLen As Long
    MaxFrames As Long
End Type

Private Type SOCKADDR_IN6
    sin6_family   As Integer
    sin6_port     As Integer
    sin6_flowinfo As Long
    sin6_addr(0 To 15) As Byte
    sin6_scope_id As Long
End Type

Private Type BinaryMessage
    data() As Byte
End Type

Private Type SecHandle
#If VBA7 Then
    dwLower As LongPtr
    dwUpper As LongPtr
#Else
    dwLower As Long
    dwUpper As Long
#End If
End Type

Private Type SECURITY_INTEGER
    LowPart As Long
    HighPart As Long
End Type

Private Type SecBuffer
    cbBuffer As Long
    BufferType As Long
#If VBA7 Then
    pvBuffer As LongPtr
#Else
    pvBuffer As Long
#End If
End Type

Private Type SecBufferDesc
    ulVersion As Long
    cBuffers As Long
#If VBA7 Then
    pBuffers As LongPtr
#Else
    pBuffers As Long
#End If
End Type

Private Type SecPkgContext_StreamSizes
    cbHeader As Long
    cbTrailer As Long
    cbMaximumMessage As Long
    cBuffers As Long
    cbBlockSize As Long
End Type

Private Type SCHANNEL_CRED
    dwVersion As Long
    cCreds As Long
#If VBA7 Then
    paCred As LongPtr
    hRootStore As LongPtr
#Else
    paCred As Long
    hRootStore As Long
#End If
    cMappers As Long
#If VBA7 Then
    aphMappers As LongPtr
#Else
    aphMappers As Long
#End If
    cSupportedAlgs As Long
#If VBA7 Then
    palgSupportedAlgs As LongPtr
#Else
    palgSupportedAlgs As Long
#End If
    grbitEnabledProtocols As Long
    dwMinimumCipherStrength As Long
    dwMaximumCipherStrength As Long
    dwSessionLifespan As Long
    dwFlags As Long
    dwCredFormat As Long
End Type

Private Type WSADATA
    wVersion As Integer
    wHighVersion As Integer
    szDescription(0 To 256) As Byte
    szSystemStatus(0 To 128) As Byte
    iMaxSockets As Integer
    iMaxUdpDg As Integer
#If VBA7 Then
    lpVendorInfo As LongPtr
#Else
    lpVendorInfo As Long
#End If
End Type

Private Type SOCKADDR_IN
    sin_family As Integer
    sin_port As Integer
    sin_addr As Long
    sin_zero(0 To 7) As Byte
End Type

Private Type TIMEVAL
    tv_sec As Long
    tv_usec As Long
End Type

Private Type FD_SET
    fd_count As Long
#If VBA7 Then
    fd_array(0 To 0) As LongPtr
#Else
    fd_array(0 To 0) As Long
#End If
End Type

Private Type HOSTENT32
    h_name As Long
    h_aliases As Long
    h_addrtype As Integer
    h_length As Integer
    h_addr_list As Long
End Type

Private Type MTUInfo
    CurrentMTU As Long
    MaxSegmentSize As Long
    OptimalFrameSize As Long
    LastProbeTime As Long
    ProbeEnabled As Boolean
    UseTLSFragmentation As Boolean
End Type

Private Type MqttInFlightMsg
    packetId As Integer
    topic As String
    payload() As Byte
    qos As Byte
    SentTick As Long
End Type

Private Type HOSTENT64
    h_name As LongPtr
    h_aliases As LongPtr
    h_addrtype As Integer
    h_length As Integer
    h_addr_list As LongPtr
End Type

Private Type WasabiStats
    BytesSent As Currency
    BytesReceived As Currency
    MessagesSent As Long
    MessagesReceived As Long
    ConnectedAt As Long
End Type

Public Enum WasabiState
    STATE_CLOSED = 0
    STATE_CONNECTING = 1
    STATE_OPEN = 2
    STATE_CLOSING = 3
End Enum

Public Enum WasabiConnectionMode
    MODE_WEBSOCKET = 0
    MODE_TCP = 1
    MODE_TCP_TLS = 2
End Enum

Private Type WasabiConnection
#If VBA7 Then
    Socket As LongPtr
    hClientCertStore As LongPtr
    pClientCertCtx As LongPtr
    hNtlmCred As SecHandle
#Else
    Socket As Long
    hClientCertStore As Long
    pClientCertCtx As Long
    hNtlmCred As SecHandle
#End If
    state As WasabiState
    TLS As Boolean
    HOST As String
    port As Long
    path As String
    OriginalUrl As String
    hCred As SecHandle
    hContext As SecHandle
    sizes As SecPkgContext_StreamSizes
    recvBuffer() As Byte
    recvLen As Long
    DecryptBuffer() As Byte
    DecryptLen As Long
    MsgQueue() As String
    MsgHead As Long
    MsgTail As Long
    MsgCount As Long
    BinaryQueue() As BinaryMessage
    BinaryHead As Long
    BinaryTail As Long
    BinaryCount As Long
    FragmentBuffer() As Byte
    FragmentLen As Long
    FragmentOpcode As Byte
    Fragmenting As Boolean
    LastError As Long
    LastErrorCode As Long
    TechnicalDetails As String
    CustomHeaders() As String
    CustomHeaderCount As Long
    AutoReconnect As Boolean
    ReconnectMaxAttempts As Long
    ReconnectAttempts As Long
    ReconnectBaseDelayMs As Long
    PingIntervalMs As Long
    LastPingSentAt As Long
    ReceiveTimeoutMs As Long
    EnableErrorDialog As Boolean
    LogCallback As String
    stats As WasabiStats
    NoDelay As Boolean
    proxyHost As String
    proxyPort As Long
    proxyUser As String
    proxyPass As String
    proxyType As Long
    ProxyEnabled As Boolean
    InactivityTimeoutMs As Long
    LastActivityAt As Long
    SubProtocol As String
    CustomBufferSize As Long
    CustomFragmentSize As Long
    mtu As MTUInfo
    AutoMTU As Boolean
    ZeroCopyEnabled As Boolean
    closeCode As Integer
    closeReason As String
    CloseInitiatedByUs As Boolean
    PreferIPv6 As Boolean
    ValidateServerCert As Boolean
    EnableRevocationCheck As Boolean
    ClientCertThumb As String
    ClientCertPfxPath As String
    ClientCertPfxPass As String
    UseHttp2 As Boolean
    ProxyUseNtlm As Boolean
    LastRttMs As Long
    LastPingTimestamp As Long
    MqttParserStage As Long
    MqttBuffer() As Byte
    MqttBufLen As Long
    MqttExpectedRemaining As Long
    MqttCurrentPacketType As Byte
    MqttCurrentFlags As Byte
    DeflateEnabled          As Boolean
    DeflateContextTakeover  As Boolean
    InflateContextTakeover  As Boolean
    DeflateWindowBits       As Long
    InflateWindowBits       As Long
    DeflateReady            As Boolean
    InflateReady            As Boolean
    DeflateActive           As Boolean
    FragmentIsCompressed As Boolean
    ClientMaxWindowBits As Long
    ServerMaxWindowBits As Long
    MqttNextPacketId As Integer
    MqttInFlight() As MqttInFlightMsg
    MqttInFlightCount As Long
    OfflineQueueEnabled As Boolean
    OfflineTextQueue() As String
    OfflineTextCount As Long
    OfflineBinaryQueue() As BinaryMessage
    OfflineBinaryCount As Long
    
    Mode As WasabiConnectionMode
    TcpRecvBuffer() As Byte
    TcpRecvLen As Long
    
    PingJitterMaxMs As Long
    CurrentPingIntervalMs As Long
    
    ProtocolHandler As Object
    CompressionHandler As Object
    Middlewares As Collection
End Type

Public Enum WasabiError
    ERR_NONE = 0
    ERR_WSA_STARTUP_FAILED = 1
    ERR_SOCKET_CREATE_FAILED = 2
    ERR_DNS_RESOLVE_FAILED = 3
    ERR_CONNECT_FAILED = 4
    ERR_TLS_ACQUIRE_CREDS_FAILED = 5
    ERR_TLS_HANDSHAKE_FAILED = 6
    ERR_TLS_HANDSHAKE_TIMEOUT = 7
    ERR_WEBSOCKET_HANDSHAKE_FAILED = 8
    ERR_WEBSOCKET_HANDSHAKE_TIMEOUT = 9
    ERR_SEND_FAILED = 10
    ERR_RECV_FAILED = 11
    ERR_NOT_CONNECTED = 12
    ERR_ALREADY_CONNECTED = 13
    ERR_TLS_ENCRYPT_FAILED = 14
    ERR_TLS_DECRYPT_FAILED = 15
    ERR_INVALID_URL = 16
    ERR_HANDSHAKE_REJECTED = 17
    ERR_CONNECTION_LOST = 18
    ERR_INVALID_HANDLE = 19
    ERR_MAX_CONNECTIONS = 20
    ERR_PROXY_CONNECT_FAILED = 21
    ERR_PROXY_AUTH_FAILED = 22
    ERR_PROXY_TUNNEL_FAILED = 23
    ERR_INACTIVITY_TIMEOUT = 24
    ERR_CERT_LOAD_FAILED = 25
    ERR_CERT_VALIDATE_FAILED = 26
    ERR_FRAGMENT_OVERFLOW = 27
    ERR_TLS_RENEGOTIATE = 28
End Enum

Private Const BUFFER_SIZE As Long = 262144
Private Const FRAGMENT_BUFFER_SIZE As Long = 262144
Private Const MSG_QUEUE_SIZE As Long = 512
Private Const MAX_CONNECTIONS As Long = 64
Private Const INVALID_CONN_HANDLE As Long = -1
Private Const DEFAULT_RECEIVE_TIMEOUT_MS As Long = 5000
Private Const DEFAULT_PING_INTERVAL_MS As Long = 0
Private Const DEFAULT_RECONNECT_BASE_DELAY_MS As Long = 1000
Private Const DEFAULT_RECONNECT_MAX_ATTEMPTS As Long = 5
Private Const MAX_RECONNECT_DELAY_MS As Long = 30000
Private Const DEFAULT_MTU As Long = 1500
Private Const HAPPY_EYEBALLS_DELAY_MS As Long = 250
Private Const PMTU_DISCOVERY_INTERVAL_MS As Long = 60000
Private Const SOL_SOCKET As Long = 65535
Private Const SO_KEEPALIVE As Long = 8
Private Const SO_RCVBUF As Long = &H1002
Private Const SO_SNDBUF As Long = &H1001
Private Const IPPROTO_TCP_SOL As Long = 6
Private Const TCP_NODELAY As Long = 1
Private Const CP_UTF8 As Long = 65001
Private Const AF_INET As Long = 2
Private Const AF_INET6 As Long = 23
Private Const SOCK_STREAM As Long = 1
Private Const IPPROTO_TCP As Long = 6
Private Const FIONBIO As Long = &H8004667E
Private Const FIONREAD As Long = &H4004667F
Private Const INADDR_NONE As Long = &HFFFFFFFF
Private Const PROXY_TYPE_HTTP As Long = 0
Private Const PROXY_TYPE_SOCKS5 As Long = 1
Private Const SECPKG_CRED_OUTBOUND As Long = &H2
Private Const SCHANNEL_CRED_VERSION As Long = &H4
Private Const SCH_CRED_NO_DEFAULT_CREDS As Long = &H10
Private Const SCH_CRED_MANUAL_CRED_VALIDATION As Long = &H8
Private Const SCH_CRED_IGNORE_NO_REVOCATION_CHECK As Long = &H800
Private Const SCH_CRED_IGNORE_REVOCATION_OFFLINE As Long = &H1000
Private Const SP_PROT_TLS1_2_CLIENT As Long = &H800
Private Const SP_PROT_TLS1_3_CLIENT As Long = &H2000
Private Const ISC_REQ_SEQUENCE_DETECT As Long = &H8
Private Const ISC_REQ_REPLAY_DETECT As Long = &H4
Private Const ISC_REQ_CONFIDENTIALITY As Long = &H10
Private Const ISC_REQ_ALLOCATE_MEMORY As Long = &H100
Private Const ISC_REQ_STREAM As Long = &H8000
Private Const CERT_CHAIN_POLICY_SSL As Long = 4
Private Const CERT_FIND_ANY As Long = 0
Private Const CERT_FIND_SUBJECT_STR_A As Long = &H80007
Private Const SECPKG_ATTR_REMOTE_CERT_CONTEXT As Long = &H53
Private Const SECPKG_ATTR_STREAM_SIZES As Long = 4
Private Const AUTHTYPE_SERVER As Long = 1
Private Const CERT_STORE_PROV_SYSTEM As Long = 10
Private Const CERT_SYSTEM_STORE_CURRENT_USER As Long = &H10000
Private Const CERT_CHAIN_REVOCATION_CHECK_CHAIN As Long = &H20000000
Private Const X509_ASN_ENCODING As Long = 1
Private Const PKCS_7_ASN_ENCODING As Long = &H10000
Private Const CRYPT_EXPORTABLE As Long = 1
Private Const PKCS12_ALLOW_OVERWRITE_KEY As Long = &H4000
Private Const SECBUFFER_VERSION As Long = 0
Private Const SECBUFFER_EMPTY As Long = 0
Private Const SECBUFFER_DATA As Long = 1
Private Const SECBUFFER_TOKEN As Long = 2
Private Const SECBUFFER_EXTRA As Long = 5
Private Const SECBUFFER_STREAM_HEADER As Long = 7
Private Const SECBUFFER_STREAM_TRAILER As Long = 6
Private Const SEC_E_OK As Long = 0
Private Const SEC_I_CONTINUE_NEEDED As Long = &H90312
Private Const SEC_E_INCOMPLETE_MESSAGE As Long = &H80090318
Private Const SEC_I_RENEGOTIATE As Long = &H90321
Private Const SEC_I_CONTEXT_EXPIRED As Long = &H90317
Private Const ETHERNET_HEADER As Long = 14
Private Const IP_HEADER_MIN As Long = 20
Private Const TCP_HEADER_MIN As Long = 20
Private Const TLS_RECORD_HEADER As Long = 5
Private Const WEBSOCKET_HEADER_MAX As Long = 14
Private Const WS_OPCODE_CONTINUATION As Byte = 0
Private Const WS_OPCODE_TEXT As Byte = 1
Private Const WS_OPCODE_BINARY As Byte = 2
Private Const WS_OPCODE_CLOSE As Byte = 8
Private Const WS_OPCODE_PING As Byte = 9
Private Const WS_OPCODE_PONG As Byte = 10

Private Const CALG_SHA1         As Long = &H8004&
Private Const HP_HASHVAL        As Long = &H2
Private Const CRYPT_STRING_BASE64 As Long = &H1
Private Const CRYPT_NOCRLF      As Long = &H40000000
Private Const PROV_RSA_FULL     As Long = 1
Private Const CRYPT_VERIFYCONTEXT As Long = &HF0000000

Private Const SECPKG_CRED_OUTBOUND_NTLM As Long = &H2
Private Const SEC_I_COMPLETE_NEEDED As Long = &H90313

Private Const MEM_COMMIT As Long = &H1000
Private Const MEM_RESERVE As Long = &H2000
Private Const PAGE_EXECUTE_READWRITE As Long = &H40
Private Const MEM_RELEASE As Long = &H8000&

Private Enum MqttPacketType
    MQTT_CONNECT = 1
    MQTT_CONNACK = 2
    MQTT_PUBLISH = 3
    MQTT_PUBACK = 4
    MQTT_PUBREC = 5
    MQTT_PUBREL = 6
    MQTT_PUBCOMP = 7
    MQTT_SUBSCRIBE = 8
    MQTT_SUBACK = 9
    MQTT_UNSUBSCRIBE = 10
    MQTT_UNSUBACK = 11
    MQTT_PINGREQ = 12
    MQTT_PINGRESP = 13
    MQTT_DISCONNECT = 14
End Enum

Private m_WSAInitialized As Boolean
Private m_Connections() As WasabiConnection
Private m_ConnectionCount As Long
Private m_DefaultHandle As Long
Private m_LastError As WasabiError
Private m_LastErrorCode As Long
Private m_TechnicalDetails As String
Private m_Utf8Buf() As Byte
Private m_Utf8BufSize As Long
Private m_ZeroCopyText As String
Private m_ZeroCopyBinary() As Byte
#If VBA7 Then
    Private m_ClientCertContextPtrs(0 To MAX_CONNECTIONS - 1) As LongPtr
#Else
    Private m_ClientCertContextPtrs(0 To MAX_CONNECTIONS - 1) As Long
#End If
Public EnableErrorDialog As Boolean

Private Sub InitWasabiThunks()
    If m_ptrWsMask <> 0 Then Exit Sub
    #If Win64 Then
        m_ptrWsMask = LoadThunk(GetWsMaskOpcodes_x64())
        m_ptrMemZero = LoadThunk(GetMemZeroOpcodes_x64())
        m_ptrMemFind = LoadThunk(GetMemFindOpcodes_x64())
        m_ptrTickDiff = LoadThunk(GetTickDiffOpcodes_x64())
    #Else
        m_ptrWsMask = LoadThunk(GetWsMaskOpcodes_x86())
        m_ptrMemZero = LoadThunk(GetMemZeroOpcodes_x86())
        m_ptrMemFind = LoadThunk(GetMemFindOpcodes_x86())
        m_ptrTickDiff = LoadThunk(GetTickDiffOpcodes_x86())
    #End If
End Sub

Private Sub ShutdownWasabiThunks()
    If m_ptrWsMask <> 0 Then VirtualFree m_ptrWsMask, 0, MEM_RELEASE: m_ptrWsMask = 0
    If m_ptrMemZero <> 0 Then VirtualFree m_ptrMemZero, 0, MEM_RELEASE: m_ptrMemZero = 0
    If m_ptrMemFind <> 0 Then VirtualFree m_ptrMemFind, 0, MEM_RELEASE: m_ptrMemFind = 0
    If m_ptrTickDiff <> 0 Then VirtualFree m_ptrTickDiff, 0, MEM_RELEASE: m_ptrTickDiff = 0
End Sub

#If VBA7 Then
Private Sub WasabiMemZero(ByVal ptr As LongPtr, ByVal length As Long)
#Else
Private Sub WasabiMemZero(ByVal ptr As Long, ByVal length As Long)
#End If
    If ptr = 0 Or length <= 0 Then Exit Sub
    
    If m_ptrMemZero <> 0 Then
        CallWindowProcW m_ptrMemZero, ptr, length, 0, 0
    Else
        Dim i As Long
        Dim zero As Byte: zero = 0
        For i = 0 To length - 1
            CopyMemoryFromPtr ByVal (ptr + i), VarPtr(zero), 1
        Next i
    End If
End Sub

#If VBA7 Then
Private Function WasabiMemFind(ByVal haystackPtr As LongPtr, ByVal hayLen As Long, ByVal needlePtr As LongPtr, ByVal needleLen As Long) As Long
#Else
Private Function WasabiMemFind(ByVal haystackPtr As Long, ByVal hayLen As Long, ByVal needlePtr As Long, ByVal needleLen As Long) As Long
#End If
    If m_ptrMemFind <> 0 Then
        #If VBA7 Then
            WasabiMemFind = CLng(CallWindowProcW(m_ptrMemFind, haystackPtr, hayLen, needlePtr, needleLen))
        #Else
            WasabiMemFind = CallWindowProcW(m_ptrMemFind, haystackPtr, hayLen, needlePtr, needleLen)
        #End If
    Else
        If hayLen < needleLen Or needleLen <= 0 Then
            WasabiMemFind = -1
            Exit Function
        End If
        
        Dim i As Long, j As Long
        Dim found As Boolean
        Dim hByte As Byte, nByte As Byte
        
        For i = 0 To hayLen - needleLen
            found = True
            For j = 0 To needleLen - 1
                CopyMemoryFromPtr hByte, haystackPtr + i + j, 1
                CopyMemoryFromPtr nByte, needlePtr + j, 1
                If hByte <> nByte Then
                    found = False
                    Exit For
                End If
            Next j
            If found Then
                WasabiMemFind = i
                Exit Function
            End If
        Next i
        WasabiMemFind = -1
    End If
End Function

#If Win64 Then

Private Function GetWsMaskOpcodes_x64() As Byte()
    Dim opcodes(0 To 21) As Byte
    Dim HexStr As Variant: HexStr = Array(&H48, &H85, &HD2, &H74, &H10, &H41, &H8B, &H0, &H30, &H1, &H48, &HFF, &HC1, &HC1, &HC8, &H8, &H48, &HFF, &HCA, &H75, &HF3, &HC3)
    Dim i As Long: For i = 0 To 21: opcodes(i) = CByte(HexStr(i)): Next i
    GetWsMaskOpcodes_x64 = opcodes
End Function

Private Function GetMemZeroOpcodes_x64() As Byte()
    Dim opcodes(0 To 12) As Byte
    Dim HexStr As Variant: HexStr = Array(&H57, &H48, &H89, &HCF, &H48, &H89, &HD1, &H31, &HC0, &HF3, &HAA, &H5F, &HC3)
    Dim i As Long: For i = 0 To 12: opcodes(i) = CByte(HexStr(i)): Next i
    GetMemZeroOpcodes_x64 = opcodes
End Function

Private Function GetMemFindOpcodes_x64() As Byte()
    Dim opcodes(0 To 59) As Byte
    Dim HexStr As Variant: HexStr = Array(&H56, &H57, &H53, &H4C, &H39, &HCA, &H72, &H29, &H4D, &H85, &HC9, &H74, &H24, &H4C, &H29, &HCA, &H48, &HFF, &HC2, &H48, &H31, &HC0, &H48, &H89, &HCB, &H4C, &H89, &HC9, &H48, &H89, &HDF, &H4C, &H89, &HC6, &HF3, &HA6, &H74, &H12, &H48, &HFF, &HC3, &H48, &HFF, &HC0, &H48, &HFF, &HCA, &H75, &HE8, &H48, &HC7, &HC0, &HFF, &HFF, &HFF, &HFF, &H5B, &H5F, &H5E, &HC3)
    Dim i As Long: For i = 0 To 59: opcodes(i) = CByte(HexStr(i)): Next i
    GetMemFindOpcodes_x64 = opcodes
End Function

Private Function GetTickDiffOpcodes_x64() As Byte()
    Dim opcodes(0 To 4) As Byte
    Dim HexStr As Variant: HexStr = Array(&H89, &HD0, &H2B, &HC1, &HC3)
    Dim i As Long: For i = 0 To 4: opcodes(i) = CByte(HexStr(i)): Next i
    GetTickDiffOpcodes_x64 = opcodes
End Function

#Else

Private Function GetWsMaskOpcodes_x86() As Byte()
    Dim opcodes(0 To 34) As Byte
    Dim HexStr As Variant: HexStr = Array(&H55, &H89, &HE5, &H53, &H8B, &H4D, &HC, &H85, &HC9, &H74, &H13, &H8B, &H55, &H8, &H8B, &H45, &H10, &H8B, &H18, &H88, &HD8, &H30, &H2, &H42, &HC1, &HCB, &H8, &H49, &H75, &HF5, &H5B, &H5D, &HC2, &H10, &H0)
    Dim i As Long: For i = 0 To 34: opcodes(i) = CByte(HexStr(i)): Next i
    GetWsMaskOpcodes_x86 = opcodes
End Function

Private Function GetMemZeroOpcodes_x86() As Byte()
    Dim opcodes(0 To 18) As Byte
    Dim HexStr As Variant: HexStr = Array(&H55, &H89, &HE5, &H57, &H8B, &H7D, &H8, &H8B, &H4D, &HC, &H31, &HC0, &HF3, &HAA, &H5F, &H5D, &HC2, &H10, &H0)
    Dim i As Long: For i = 0 To 18: opcodes(i) = CByte(HexStr(i)): Next i
    GetMemZeroOpcodes_x86 = opcodes
End Function

Private Function GetMemFindOpcodes_x86() As Byte()
    Dim opcodes(0 To 60) As Byte
    Dim HexStr As Variant: HexStr = Array(&H55, &H89, &HE5, &H53, &H56, &H57, &H8B, &H55, &HC, &H8B, &H4D, &H14, &H39, &HCA, &H72, &H21, &H85, &HC9, &H74, &H1D, &H29, &HCA, &H42, &H31, &HC0, &H8B, &H5D, &H8, &H51, &H53, &H8B, &H4D, &H14, &H89, &HDF, &H8B, &H75, &H10, &HF3, &HA6, &H5B, &H59, &H74, &HA, &H43, &H40, &H4A, &H75, &HEB, &HB8, &HFF, &HFF, &HFF, &HFF, &H5F, &H5E, &H5B, &H5D, &HC2, &H10, &H0)
    Dim i As Long: For i = 0 To 60: opcodes(i) = CByte(HexStr(i)): Next i
    GetMemFindOpcodes_x86 = opcodes
End Function

Private Function GetTickDiffOpcodes_x86() As Byte()
    Dim opcodes(0 To 10) As Byte
    Dim HexStr As Variant: HexStr = Array(&H8B, &H44, &H24, &H8, &H2B, &H44, &H24, &H4, &HC2, &H10, &H0)
    Dim i As Long: For i = 0 To 10: opcodes(i) = CByte(HexStr(i)): Next i
    GetTickDiffOpcodes_x86 = opcodes
End Function

#End If

#If VBA7 Then
Private Function LoadThunk(ByRef opcodes() As Byte) As LongPtr
    Dim pMem As LongPtr
#Else
Private Function LoadThunk(ByRef opcodes() As Byte) As Long
    Dim pMem As Long
#End If
    Dim size As Long
    
    If (Not Not opcodes) = 0 Then Exit Function
    size = UBound(opcodes) - LBound(opcodes) + 1
    
    pMem = VirtualAlloc(0, size, MEM_COMMIT Or MEM_RESERVE, PAGE_EXECUTE_READWRITE)
    
    If pMem <> 0 Then
        CopyMemoryFromPtr ByVal pMem, VarPtr(opcodes(LBound(opcodes))), size
    End If
    
    LoadThunk = pMem
End Function

Private Sub FillRandomBytes(ByRef buf() As Byte, ByVal count As Long)
    If count <= 0 Then Exit Sub
    
    If RtlGenRandom(VarPtr(buf(LBound(buf))), count) = 0 Then
        Dim i As Long
        For i = 0 To count - 1
            buf(i) = CByte(Int(Rnd * 256))
        Next i
    End If
End Sub

Private Function TickDiff(ByVal startTick As Long, ByVal endTick As Long) As Double
    #If VBA7 Then
        Dim resultPtr As LongPtr
    #Else
        Dim resultPtr As Long
    #End If
    
    If m_ptrTickDiff = 0 Then InitWasabiThunks
    
    If m_ptrTickDiff <> 0 Then
        resultPtr = CallWindowProcW(m_ptrTickDiff, startTick, endTick, 0, 0)
        TickDiff = CDbl(resultPtr)
        If TickDiff < 0 Then TickDiff = TickDiff + 4294967296#
    Else
        Dim s As Currency, e As Currency
        If startTick >= 0 Then s = startTick Else s = startTick + 4294967296@
        If endTick >= 0 Then e = endTick Else e = endTick + 4294967296@
        If e >= s Then
            TickDiff = CDbl(e - s)
        Else
            TickDiff = CDbl(e - s + 4294967296@)
        End If
    End If
End Function

Private Sub EnsureBufferCapacity(ByRef Buffer() As Byte, ByVal RequiredLen As Long)
    Dim currentCap As Long
    Dim newCap As Long
    
    currentCap = UBound(Buffer) + 1
    
    If RequiredLen > currentCap Then
        newCap = currentCap * 2
        If newCap < RequiredLen Then newCap = RequiredLen
        
        ReDim Preserve Buffer(0 To newCap - 1)
    End If
End Sub

Private Function SafeArrayLen(ByRef arr() As Byte) As Long
    #If VBA7 Then
        Dim pSA As LongPtr
        Dim ptr As LongPtr
    #Else
        Dim pSA As Long
        Dim ptr As Long
    #End If
    Dim lo As Long
    Dim hi As Long
    ptr = VarPtrArray(arr)
    If ptr = 0 Then Exit Function
    CopyMemoryFromPtr pSA, ptr, LenB(pSA)
    If pSA = 0 Then Exit Function
    lo = LBound(arr)
    hi = UBound(arr)
    If hi >= lo Then
        SafeArrayLen = hi - lo + 1
    End If
End Function

Private Function GetProjectPath() As String
#If ((VBA7 = 1) Or (VBA6 = 1)) And (TWINBASIC = 0) Then
    Dim path As String
    path = Application.VBE.ActiveVBProject.FileName
    path = Left(path, InStrRev(path, "\"))
    GetProjectPath = path
#Else
    GetProjectPath = App.path
#End If
End Function

Private Function ResolveHandle(ByVal handle As Long) As Long
    If handle = INVALID_CONN_HANDLE Then
        ResolveHandle = m_DefaultHandle
    Else
        ResolveHandle = handle
    End If
End Function

Private Function ValidIndex(ByVal handle As Long) As Boolean
    If handle < 0 Or handle >= MAX_CONNECTIONS Then Exit Function
    InitConnectionPool
    ValidIndex = True
End Function

Private Sub WasabiLog(ByVal handle As Long, ByVal msg As String)
    Debug.Print "[WASABI] " & msg
    If ValidIndex(handle) Then
        If m_Connections(handle).LogCallback <> "" Then
            Application.Run m_Connections(handle).LogCallback, msg
        End If
    End If
End Sub

Private Sub SetError(ByVal errType As WasabiError, ByVal techMsg As String, ByVal userMsg As String, ByVal handle As Long, Optional ByVal errCode As Long = 0)
    Static lastErr As Long
    Static lastHandle As Long
    If errType = ERR_NONE Then Exit Sub
    m_LastError = errType
    m_LastErrorCode = errCode
    m_TechnicalDetails = techMsg
    WasabiLog handle, "ERR " & errType & " | " & techMsg
    If errCode <> 0 Then WasabiLog handle, "SysCode: " & errCode & " (0x" & Hex(errCode) & ")"
    If ValidIndex(handle) Then
        m_Connections(handle).LastError = errType
        m_Connections(handle).LastErrorCode = errCode
        m_Connections(handle).TechnicalDetails = techMsg
        If m_Connections(handle).EnableErrorDialog Then
            If errType <> lastErr Or handle <> lastHandle Then
                lastErr = errType
                lastHandle = handle
                MsgBox userMsg, vbCritical, "Wasabi WebSocket Error"
            End If
        End If
    ElseIf EnableErrorDialog Then
        MsgBox userMsg, vbCritical, "Wasabi WebSocket Error"
    End If
End Sub

Private Function WSAErrDesc(ByVal code As Long) As String
    Select Case code
        Case 10004: WSAErrDesc = "WSAEINTR - Interrupted"
        Case 10013: WSAErrDesc = "WSAEACCES - Permission denied"
        Case 10014: WSAErrDesc = "WSAEFAULT - Bad address"
        Case 10022: WSAErrDesc = "WSAEINVAL - Invalid argument"
        Case 10024: WSAErrDesc = "WSAEMFILE - Too many sockets"
        Case 10035: WSAErrDesc = "WSAEWOULDBLOCK - Operation would block"
        Case 10036: WSAErrDesc = "WSAEINPROGRESS - Operation in progress"
        Case 10037: WSAErrDesc = "WSAEALREADY - Already in progress"
        Case 10038: WSAErrDesc = "WSAENOTSOCK - Not a socket"
        Case 10039: WSAErrDesc = "WSAEDESTADDRREQ - Destination address required"
        Case 10040: WSAErrDesc = "WSAEMSGSIZE - Message too long"
        Case 10047: WSAErrDesc = "WSAEAFNOSUPPORT - Address family not supported"
        Case 10048: WSAErrDesc = "WSAEADDRINUSE - Address in use"
        Case 10049: WSAErrDesc = "WSAEADDRNOTAVAIL - Address not available"
        Case 10050: WSAErrDesc = "WSAENETDOWN - Network is down"
        Case 10051: WSAErrDesc = "WSAENETUNREACH - Network unreachable"
        Case 10052: WSAErrDesc = "WSAENETRESET - Network dropped connection"
        Case 10053: WSAErrDesc = "WSAECONNABORTED - Connection aborted"
        Case 10054: WSAErrDesc = "WSAECONNRESET - Connection reset by peer"
        Case 10055: WSAErrDesc = "WSAENOBUFS - No buffer space"
        Case 10056: WSAErrDesc = "WSAEISCONN - Socket already connected"
        Case 10057: WSAErrDesc = "WSAENOTCONN - Socket not connected"
        Case 10058: WSAErrDesc = "WSAESHUTDOWN - Shutdown"
        Case 10060: WSAErrDesc = "WSAETIMEDOUT - Connection timed out"
        Case 10061: WSAErrDesc = "WSAECONNREFUSED - Connection refused"
        Case 10064: WSAErrDesc = "WSAEHOSTDOWN - Host is down"
        Case 10065: WSAErrDesc = "WSAEHOSTUNREACH - Host unreachable"
        Case 11001: WSAErrDesc = "WSAHOST_NOT_FOUND - Host not found"
        Case 11002: WSAErrDesc = "WSATRY_AGAIN - Non-authoritative host not found"
        Case 11003: WSAErrDesc = "WSANO_RECOVERY - Non-recoverable DNS error"
        Case 11004: WSAErrDesc = "WSANO_DATA - No address for hostname"
        Case Else: WSAErrDesc = "WSA error " & code
    End Select
End Function

Private Function GetCloseCodeDesc(ByVal code As Integer) As String
    Select Case code
        Case 1000: GetCloseCodeDesc = "Normal Closure"
        Case 1001: GetCloseCodeDesc = "Going Away"
        Case 1002: GetCloseCodeDesc = "Protocol Error"
        Case 1003: GetCloseCodeDesc = "Unsupported Data"
        Case 1004: GetCloseCodeDesc = "Reserved"
        Case 1005: GetCloseCodeDesc = "No Status Received"
        Case 1006: GetCloseCodeDesc = "Abnormal Closure"
        Case 1007: GetCloseCodeDesc = "Invalid Frame Payload"
        Case 1008: GetCloseCodeDesc = "Policy Violation"
        Case 1009: GetCloseCodeDesc = "Message Too Big"
        Case 1010: GetCloseCodeDesc = "Mandatory Extension"
        Case 1011: GetCloseCodeDesc = "Internal Server Error"
        Case 1012: GetCloseCodeDesc = "Service Restart"
        Case 1013: GetCloseCodeDesc = "Try Again Later"
        Case 1014: GetCloseCodeDesc = "Bad Gateway"
        Case 1015: GetCloseCodeDesc = "TLS Handshake Failure"
        Case Else: GetCloseCodeDesc = "Unknown (" & code & ")"
    End Select
End Function

Private Sub InitConnectionPool()
    Dim i As Long
    If m_ConnectionCount > 0 Then Exit Sub
    Randomize
    InitWasabiThunks
    ReDim m_Connections(0 To MAX_CONNECTIONS - 1)
    For i = 0 To MAX_CONNECTIONS - 1
        m_Connections(i).Socket = INVALID_SOCKET
        m_Connections(i).state = STATE_CLOSED
        m_Connections(i).hNtlmCred.dwLower = 0
        m_Connections(i).hNtlmCred.dwUpper = 0
    Next i
    m_ConnectionCount = MAX_CONNECTIONS
End Sub

Private Function AllocConnection() As Long
    Dim i As Long
    Dim bufSize As Long
    Dim fragSize As Long
    InitConnectionPool
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state = STATE_CLOSED And m_Connections(i).Socket = INVALID_SOCKET Then
            bufSize = IIf(m_Connections(i).CustomBufferSize > 0, m_Connections(i).CustomBufferSize, BUFFER_SIZE)
            fragSize = IIf(m_Connections(i).CustomFragmentSize > 0, m_Connections(i).CustomFragmentSize, FRAGMENT_BUFFER_SIZE)
            ReDim m_Connections(i).recvBuffer(0 To bufSize - 1)
            ReDim m_Connections(i).DecryptBuffer(0 To bufSize - 1)
            ReDim m_Connections(i).MsgQueue(0 To MSG_QUEUE_SIZE - 1)
            ReDim m_Connections(i).BinaryQueue(0 To MSG_QUEUE_SIZE - 1)
            ReDim m_Connections(i).FragmentBuffer(0 To fragSize - 1)
            ReDim m_Connections(i).CustomHeaders(0 To 31)
            ReDim m_Connections(i).MqttBuffer(0 To 4095)
            ReDim m_Connections(i).OfflineTextQueue(0 To MSG_QUEUE_SIZE - 1)
            ReDim m_Connections(i).OfflineBinaryQueue(0 To MSG_QUEUE_SIZE - 1)
            ReDim m_Connections(i).TcpRecvBuffer(0 To bufSize - 1)
            Set m_Connections(i).Middlewares = New Collection
            ResetConnectionState i
            InitializeMTU i
            AllocConnection = i
            Exit Function
        End If
    Next i
    AllocConnection = INVALID_CONN_HANDLE
End Function

Private Sub ResetConnectionState(ByVal handle As Long)
    With m_Connections(handle)
        .Mode = MODE_WEBSOCKET
        .TcpRecvLen = 0
        .state = STATE_CLOSED
        .TLS = False
        .HOST = ""
        .port = 0
        .path = ""
        .OriginalUrl = ""
        .recvLen = 0
        .DecryptLen = 0
        .MsgHead = 0
        .MsgTail = 0
        .MsgCount = 0
        .BinaryHead = 0
        .BinaryTail = 0
        .BinaryCount = 0
        .FragmentLen = 0
        .Fragmenting = False
        .FragmentOpcode = 0
        .LastError = ERR_NONE
        .LastErrorCode = 0
        .TechnicalDetails = ""
        .AutoReconnect = False
        .ReconnectMaxAttempts = DEFAULT_RECONNECT_MAX_ATTEMPTS
        .ReconnectAttempts = 0
        .ReconnectBaseDelayMs = DEFAULT_RECONNECT_BASE_DELAY_MS
        .PingIntervalMs = DEFAULT_PING_INTERVAL_MS
        .LastPingSentAt = 0
        .ReceiveTimeoutMs = DEFAULT_RECEIVE_TIMEOUT_MS
        .EnableErrorDialog = False
        .LogCallback = ""
        .NoDelay = False
        .proxyHost = ""
        .proxyPort = 0
        .proxyUser = ""
        .proxyPass = ""
        .proxyType = PROXY_TYPE_HTTP
        .ProxyEnabled = False
        .InactivityTimeoutMs = 0
        .LastActivityAt = 0
        .SubProtocol = ""
        .CustomBufferSize = 0
        .CustomFragmentSize = 0
        .AutoMTU = True
        .ZeroCopyEnabled = False
        .closeCode = 0
        .closeReason = ""
        .CloseInitiatedByUs = False
        .PreferIPv6 = False
        .ValidateServerCert = False
        .EnableRevocationCheck = False
        .ClientCertThumb = ""
        .ClientCertPfxPath = ""
        .ClientCertPfxPass = ""
        .UseHttp2 = False
        .ProxyUseNtlm = False
        .LastRttMs = 0
        .LastPingTimestamp = 0
        .MqttParserStage = 0
        .MqttBufLen = 0
        .MqttExpectedRemaining = 0
        .MqttCurrentPacketType = 0
        .MqttCurrentFlags = 0
        .MqttNextPacketId = 0
        .MqttInFlightCount = 0
        ReDim .MqttInFlight(0 To 0)
        With .stats
            .BytesSent = 0
            .BytesReceived = 0
            .MessagesSent = 0
            .MessagesReceived = 0
            .ConnectedAt = 0
        End With
        .mtu.CurrentMTU = DEFAULT_MTU
        .mtu.MaxSegmentSize = 1460
        .mtu.OptimalFrameSize = 1024
        .mtu.LastProbeTime = 0
        .mtu.ProbeEnabled = True
        .mtu.UseTLSFragmentation = .TLS
        .DeflateEnabled = False
        .DeflateContextTakeover = True
        .InflateContextTakeover = True
        .DeflateWindowBits = -15
        .InflateWindowBits = -15
        .DeflateReady = False
        .InflateReady = False
        .DeflateActive = False
        .FragmentIsCompressed = False
        .ClientMaxWindowBits = 15
        .ServerMaxWindowBits = 15
        Set .ProtocolHandler = Nothing
        Set .CompressionHandler = Nothing
        Set .Middlewares = New Collection
    End With
End Sub

Private Sub FreeSecurityHandles(ByVal handle As Long)
    With m_Connections(handle)
        If .pClientCertCtx <> 0 Then
            CertFreeCertificateContext .pClientCertCtx
            .pClientCertCtx = 0
        End If
        If .hClientCertStore <> 0 Then
            CertCloseStore .hClientCertStore, 0
            .hClientCertStore = 0
        End If
        If .hContext.dwLower <> 0 Or .hContext.dwUpper <> 0 Then
            DeleteSecurityContext .hContext
            .hContext.dwLower = 0
            .hContext.dwUpper = 0
        End If
        If .hCred.dwLower <> 0 Or .hCred.dwUpper <> 0 Then
            FreeCredentialsHandle .hCred
            .hCred.dwLower = 0
            .hCred.dwUpper = 0
        End If
        If .hNtlmCred.dwLower <> 0 Or .hNtlmCred.dwUpper <> 0 Then
            FreeCredentialsHandle .hNtlmCred
            .hNtlmCred.dwLower = 0
            .hNtlmCred.dwUpper = 0
        End If
    End With
End Sub

Private Sub CleanupHandle(ByVal handle As Long)
    If Not ValidIndex(handle) Then Exit Sub
    
    If Not m_Connections(handle).CompressionHandler Is Nothing Then
        m_Connections(handle).CompressionHandler.OnDisconnect handle
    End If
    
    If Not m_Connections(handle).ProtocolHandler Is Nothing Then
        m_Connections(handle).ProtocolHandler.OnDisconnect handle
    End If
    
    Dim mwDisconnect As Object
    For Each mwDisconnect In m_Connections(handle).Middlewares
        mwDisconnect.OnDisconnect handle
    Next mwDisconnect
    
    With m_Connections(handle)
        If .Socket <> INVALID_SOCKET Then
            sock_closesocket .Socket
            .Socket = INVALID_SOCKET
        End If
        
        If .recvLen > 0 Or .DecryptLen > 0 Or .TcpRecvLen > 0 Then
            WasabiMemZero VarPtr(.recvBuffer(0)), UBound(.recvBuffer) + 1
            WasabiMemZero VarPtr(.DecryptBuffer(0)), UBound(.DecryptBuffer) + 1
            WasabiMemZero VarPtr(.TcpRecvBuffer(0)), UBound(.TcpRecvBuffer) + 1
            WasabiMemZero VarPtr(.FragmentBuffer(0)), UBound(.FragmentBuffer) + 1
        End If
    End With
    
    FreeSecurityHandles handle
    If handle >= 0 And handle < MAX_CONNECTIONS Then
        m_ClientCertContextPtrs(handle) = 0
    End If
    ResetConnectionState handle
End Sub

Private Sub InitializeMTU(ByVal handle As Long)
    With m_Connections(handle)
        .mtu.CurrentMTU = DEFAULT_MTU
        .mtu.LastProbeTime = 0
        .mtu.ProbeEnabled = True
        CalculateOptimalFrameSize handle
    End With
End Sub

Private Sub CalculateOptimalFrameSize(ByVal handle As Long)
    Dim ipOverhead As Long
    Dim tlsOverhead As Long
    Dim available As Long
    With m_Connections(handle)
        ipOverhead = IIf(.PreferIPv6, 40, IP_HEADER_MIN)
        If .TLS Then
            tlsOverhead = TLS_RECORD_HEADER + .sizes.cbHeader + .sizes.cbTrailer
        Else
            tlsOverhead = 0
        End If
        available = .mtu.CurrentMTU - ETHERNET_HEADER - ipOverhead - TCP_HEADER_MIN - tlsOverhead - WEBSOCKET_HEADER_MAX
        If available < 125 Then
            available = 125
        End If
        If available > 65535 Then
            available = 65535
        End If
        .mtu.MaxSegmentSize = .mtu.CurrentMTU - ETHERNET_HEADER - ipOverhead - TCP_HEADER_MIN
        .mtu.OptimalFrameSize = available
    End With
End Sub

Private Sub probeMTU(ByVal handle As Long)
    Dim mss As Long
    Dim optVal As Long
    Dim optlen As Long
    Dim probeMTU As Long
    With m_Connections(handle)
        If .Socket = INVALID_SOCKET Then Exit Sub
        optlen = 4
        If sock_getsockopt(.Socket, IPPROTO_TCP_SOL, TCP_MAXSEG, optVal, optlen) = 0 And optVal > 0 Then
            mss = optVal
        Else
            mss = 1460
        End If
        probeMTU = mss + TCP_HEADER_MIN + IIf(.PreferIPv6, 40, IP_HEADER_MIN) + ETHERNET_HEADER
        If probeMTU <> .mtu.CurrentMTU Then
            .mtu.CurrentMTU = probeMTU
            CalculateOptimalFrameSize handle
            WasabiLog handle, "MTU updated: " & .mtu.CurrentMTU & " MSS=" & mss & " OptFrame=" & .mtu.OptimalFrameSize & " (handle=" & handle & ")"
        End If
        .mtu.LastProbeTime = GetTickCount()
    End With
End Sub

Private Sub ApplySocketOptions(ByVal handle As Long)
    Dim optVal As Long
    Dim wsaErr As Long
    With m_Connections(handle)
        If .Socket = INVALID_SOCKET Then Exit Sub
        optVal = IIf(.NoDelay, 1, 0)
        If sock_setsockopt(.Socket, IPPROTO_TCP_SOL, TCP_NODELAY, optVal, 4) <> 0 Then
            wsaErr = WSAGetLastError()
            WasabiLog handle, "TCP_NODELAY failed: " & WSAErrDesc(wsaErr)
        End If
        optVal = 1
        If sock_setsockopt(.Socket, SOL_SOCKET, SO_KEEPALIVE, optVal, 4) <> 0 Then
            wsaErr = WSAGetLastError()
            WasabiLog handle, "SO_KEEPALIVE failed: " & WSAErrDesc(wsaErr)
        End If
        optVal = BUFFER_SIZE
        sock_setsockopt .Socket, SOL_SOCKET, SO_RCVBUF, optVal, 4
        sock_setsockopt .Socket, SOL_SOCKET, SO_SNDBUF, optVal, 4
    End With
End Sub

Private Function WaitForDataOn(ByVal handle As Long, ByVal timeoutMs As Long) As Boolean
    Dim readSet As FD_SET
    Dim TIMEOUT As TIMEVAL
    Dim effective As Long
    effective = timeoutMs
    If effective = 0 And ValidIndex(handle) Then
        If m_Connections(handle).ReceiveTimeoutMs > 0 Then
            effective = m_Connections(handle).ReceiveTimeoutMs
        End If
    End If
    readSet.fd_count = 1
    readSet.fd_array(0) = m_Connections(handle).Socket
    TIMEOUT.tv_sec = effective \ 1000
    TIMEOUT.tv_usec = (effective Mod 1000) * 1000
    WaitForDataOn = (sock_select(0, readSet, ByVal 0&, ByVal 0&, TIMEOUT) > 0)
End Function

Private Function RawSendFor(ByVal handle As Long, ByRef frame() As Byte) As Boolean
    Dim totalSent As Long
    Dim toSend As Long
    Dim sent As Long
    Dim wsaErr As Long
    toSend = UBound(frame) + 1
    totalSent = 0
    With m_Connections(handle)
        Do While totalSent < toSend
            sent = sock_send(.Socket, frame(totalSent), toSend - totalSent, 0)
            If sent <= 0 Then
                wsaErr = WSAGetLastError()
                SetError ERR_SEND_FAILED, "send() failed: " & WSAErrDesc(wsaErr), "Failed to send data to server.", handle, wsaErr
                .state = STATE_CLOSED
                Exit Function
            End If
            totalSent = totalSent + sent
        Loop
    End With
    RawSendFor = True
End Function

Private Function BuildWSFrame(ByRef payload() As Byte, ByVal payloadLen As Long, ByVal opcode As Byte, ByVal isFinal As Boolean, Optional ByVal setRSV1 As Boolean = False) As Byte()
    Dim mask(0 To 3) As Byte
    Dim headerLen As Long
    Dim frame() As Byte
    Dim finBit As Byte
    Dim rsv1 As Byte
    Dim i As Long
    rsv1 = IIf(setRSV1, &H40, 0)
    FillRandomBytes mask, 4
    finBit = IIf(isFinal, &H80, 0)
    If payloadLen < 126 Then
        headerLen = 6
        ReDim frame(0 To headerLen + payloadLen - 1)
        frame(0) = finBit Or rsv1 Or opcode
        frame(1) = &H80 Or CByte(payloadLen)
        frame(2) = mask(0)
        frame(3) = mask(1)
        frame(4) = mask(2)
        frame(5) = mask(3)
    ElseIf payloadLen < 65536 Then
        headerLen = 8
        ReDim frame(0 To headerLen + payloadLen - 1)
        frame(0) = finBit Or rsv1 Or opcode
        frame(1) = &HFE
        frame(2) = CByte((payloadLen \ 256) And &HFF)
        frame(3) = CByte(payloadLen And &HFF)
        frame(4) = mask(0)
        frame(5) = mask(1)
        frame(6) = mask(2)
        frame(7) = mask(3)
    Else
        headerLen = 14
        ReDim frame(0 To headerLen + payloadLen - 1)
        frame(0) = finBit Or rsv1 Or opcode
        frame(1) = &HFF
        frame(2) = 0
        frame(3) = 0
        frame(4) = 0
        frame(5) = 0
        frame(6) = CByte((payloadLen \ 16777216) And &HFF)
        frame(7) = CByte((payloadLen \ 65536) And &HFF)
        frame(8) = CByte((payloadLen \ 256) And &HFF)
        frame(9) = CByte(payloadLen And &HFF)
        frame(10) = mask(0)
        frame(11) = mask(1)
        frame(12) = mask(2)
        frame(13) = mask(3)
    End If
    If m_ptrWsMask <> 0 And payloadLen > 0 Then
        CopyMemory frame(headerLen), payload(LBound(payload)), payloadLen
        CallWindowProcW m_ptrWsMask, VarPtr(frame(headerLen)), payloadLen, VarPtr(mask(0)), 0
    Else
        For i = 0 To payloadLen - 1
            frame(headerLen + i) = payload(LBound(payload) + i) Xor mask(i Mod 4)
        Next i
    End If
    
    BuildWSFrame = frame
End Function

#If VBA7 Then
Private Function PtrToStrW(ByVal ptr As LongPtr) As String
#Else
Private Function PtrToStrW(ByVal ptr As Long) As String
#End If
    Dim length As Long
    Dim buf() As Byte
    If ptr = 0 Then Exit Function
    
    length = lstrlenW(ptr) * 2
    If length > 0 Then
        ReDim buf(0 To length - 1)
        CopyMemoryFromPtr buf(0), ptr, length
        PtrToStrW = buf
    End If
End Function



Private Function StringToUtf8(ByVal str As String) As Byte()
    Dim need As Long
    Dim written As Long
    Dim result() As Byte
    If Len(str) = 0 Then
        StringToUtf8 = result
        Exit Function
    End If
    need = Len(str) * 4
    If need > m_Utf8BufSize Then
        ReDim m_Utf8Buf(0 To need - 1)
        m_Utf8BufSize = need
    End If
    written = WideCharToMultiByte(CP_UTF8, 0, StrPtr(str), Len(str), m_Utf8Buf(0), need, NULL_PTR, NULL_PTR)
    If written > 0 Then
        ReDim result(0 To written - 1)
        CopyMemory result(0), m_Utf8Buf(0), written
    End If
    StringToUtf8 = result
End Function

Private Function Utf8ToString(ByRef utf8() As Byte, ByVal length As Long) As String
    Dim charCount As Long
    Dim result As String
    If length <= 0 Then
        Utf8ToString = ""
        Exit Function
    End If
    charCount = MultiByteToWideChar(CP_UTF8, 0, utf8(LBound(utf8)), length, NULL_PTR, 0)
    If charCount > 0 Then
        result = String$(charCount, vbNullChar)
        MultiByteToWideChar CP_UTF8, 0, utf8(LBound(utf8)), length, StrPtr(result), charCount
    End If
    Utf8ToString = result
End Function

Private Function Base64Encode(ByRef Bytes() As Byte) As String
    Dim dataLen As Long
    
    If (Not Bytes) = -1 Then
        Base64Encode = ""
        Exit Function
    End If
    
    dataLen = UBound(Bytes) - LBound(Bytes) + 1
    If dataLen = 0 Then
        Base64Encode = ""
        Exit Function
    End If

    Dim strLen As Long
    strLen = 0
    #If VBA7 Then
        CryptBinaryToStringW VarPtr(Bytes(LBound(Bytes))), dataLen, CRYPT_STRING_BASE64 Or CRYPT_NOCRLF, NULL_PTR, strLen
    #Else
        CryptBinaryToStringW VarPtr(Bytes(LBound(Bytes))), dataLen, CRYPT_STRING_BASE64 Or CRYPT_NOCRLF, 0, strLen
    #End If

    If strLen <= 0 Then
        Base64Encode = ""
        Exit Function
    End If

    Dim buf As String
    buf = String$(strLen, vbNullChar)
    #If VBA7 Then
        CryptBinaryToStringW VarPtr(Bytes(LBound(Bytes))), dataLen, CRYPT_STRING_BASE64 Or CRYPT_NOCRLF, StrPtr(buf), strLen
    #Else
        CryptBinaryToStringW VarPtr(Bytes(LBound(Bytes))), dataLen, CRYPT_STRING_BASE64 Or CRYPT_NOCRLF, StrPtr(buf), strLen
    #End If

    Base64Encode = Left$(buf, strLen)
End Function

Private Function ParseURL(ByVal url As String, ByRef outHost As String, ByRef outPort As Long, ByRef outPath As String, ByRef outTLS As Boolean) As Boolean
    Dim work As String
    Dim slashPos As Long
    Dim colonPos As Long
    Dim portStr As String
    Dim portVal As Long
    Dim i As Long
    Dim c As String
    If Len(Trim(url)) = 0 Then Exit Function
    work = url
    outTLS = False
    outPort = 80
    If LCase(Left(work, 6)) = "wss://" Then
        work = Mid(work, 7)
        outTLS = True
        outPort = 443
    ElseIf LCase(Left(work, 5)) = "ws://" Then
        work = Mid(work, 6)
    Else
        Exit Function
    End If
    If Len(work) = 0 Then Exit Function
    slashPos = InStr(work, "/")
    If slashPos > 0 Then
        outPath = Mid(work, slashPos)
        work = Left(work, slashPos - 1)
    Else
        outPath = "/"
    End If
    colonPos = InStr(work, ":")
    If colonPos > 0 Then
        outHost = Left(work, colonPos - 1)
        portStr = Mid(work, colonPos + 1)
        If Len(portStr) = 0 Then Exit Function
        For i = 1 To Len(portStr)
            c = Mid(portStr, i, 1)
            If c < "0" Or c > "9" Then Exit Function
        Next i
        portVal = val(portStr)
        If portVal <= 0 Or portVal > 65535 Then Exit Function
        outPort = portVal
    Else
        outHost = work
    End If
    If Len(outHost) = 0 Then Exit Function
    ParseURL = True
End Function

Private Function ResolveHostname(ByVal hostname As String, ByVal handle As Long) As Long
    Dim addr As Long
    Dim wsaErr As Long
#If VBA7 Then
    Dim hostent As LongPtr
    Dim he As HOSTENT64
    Dim addrList As LongPtr
    Dim pAddr As LongPtr
#Else
    Dim hostent As Long
    Dim he As HOSTENT32
    Dim addrList As Long
    Dim pAddr As Long
#End If
    addr = sock_inet_addr(hostname)
    If addr <> INADDR_NONE Then
        ResolveHostname = addr
        Exit Function
    End If
    hostent = sock_gethostbyname(hostname)
    If hostent = 0 Then
        wsaErr = WSAGetLastError()
        SetError ERR_DNS_RESOLVE_FAILED, "gethostbyname failed for '" & hostname & "': " & WSAErrDesc(wsaErr), "Cannot resolve address: " & hostname & vbCrLf & WSAErrDesc(wsaErr), handle, wsaErr
        Exit Function
    End If
#If Win64 Then
    CopyMemoryFromPtr he, hostent, LenB(he)
    addrList = he.h_addr_list
    If addrList = 0 Then Exit Function
    CopyMemoryFromPtr pAddr, addrList, 8
    If pAddr = 0 Then Exit Function
    CopyMemoryFromPtr addr, pAddr, 4
#Else
    CopyMemoryFromPtr he, hostent, LenB(he)
    addrList = he.h_addr_list
    If addrList = 0 Then Exit Function
    CopyMemoryFromPtr pAddr, addrList, 4
    If pAddr = 0 Then Exit Function
    CopyMemoryFromPtr addr, pAddr, 4
#End If
    ResolveHostname = addr
End Function

Private Function ResolveAndConnect(ByVal handle As Long, ByVal hostname As String, ByVal port As Long) As Boolean
#If VBA7 Then
    Dim ppResult As LongPtr
    Dim pCur As LongPtr
    Dim pNext As LongPtr
    Dim pSockaddr As LongPtr
    Dim sock6 As LongPtr
    Dim sock4 As LongPtr
    Dim aiAddrLenFull As LongPtr
#Else
    Dim ppResult As Long
    Dim pCur As Long
    Dim pNext As Long
    Dim pSockaddr As Long
    Dim sock6 As Long
    Dim sock4 As Long
#End If
    Dim hints() As Byte
    Dim aiFamily As Long
    Dim aiSocktype As Long
    Dim aiProtocol As Long
    Dim aiAddrLen As Long
    Dim nbMode As Long
    Dim writeSet As FD_SET
    Dim exceptSet As FD_SET
    Dim tv As TIMEVAL
    Dim selResult As Long
    Dim wsaErr As Long
    Dim sa6() As Byte
    Dim sa4() As Byte
    Dim sa6Len As Long
    Dim sa4Len As Long
    Dim found6 As Boolean
    Dim found4 As Boolean
    Dim startTick As Long
    Dim elapsed As Long
    Dim sin4 As SOCKADDR_IN

    sock6 = INVALID_SOCKET
    sock4 = INVALID_SOCKET

#If VBA7 Then
    ReDim hints(0 To 47)
#Else
    ReDim hints(0 To 31)
#End If
    aiSocktype = SOCK_STREAM
    CopyMemory hints(8), aiSocktype, 4
    aiProtocol = IPPROTO_TCP
    CopyMemory hints(12), aiProtocol, 4

    ppResult = 0
    If sock_getaddrinfo(hostname, CStr(port), VarPtr(hints(0)), ppResult) = 0 And ppResult <> 0 Then
        pCur = ppResult
        Do While pCur <> 0
                        #If Win64 Then
                            CopyMemoryFromPtr aiFamily, pCur + 4, 4
                            CopyMemoryFromPtr aiSocktype, pCur + 8, 4
                            CopyMemoryFromPtr aiAddrLenFull, pCur + 16, 8
                            aiAddrLen = CLng(aiAddrLenFull And &H7FFFFFFF)
                            CopyMemoryFromPtr pSockaddr, pCur + 32, 8
                            CopyMemoryFromPtr pNext, pCur + 40, 8
                        #Else
                            CopyMemoryFromPtr aiFamily, pCur + 4, 4
                            CopyMemoryFromPtr aiSocktype, pCur + 8, 4
                            CopyMemoryFromPtr aiAddrLen, pCur + 16, 4
                            CopyMemoryFromPtr pSockaddr, pCur + 24, 4
                            CopyMemoryFromPtr pNext, pCur + 28, 4
                        #End If
            If aiSocktype = SOCK_STREAM And aiAddrLen > 0 And pSockaddr <> 0 Then
                If aiFamily = AF_INET6 And Not found6 Then
                    ReDim sa6(0 To aiAddrLen - 1)
                    CopyMemoryFromPtr sa6(0), pSockaddr, aiAddrLen
                    sa6Len = aiAddrLen
                    found6 = True
                ElseIf aiFamily = AF_INET And Not found4 Then
                    ReDim sa4(0 To aiAddrLen - 1)
                    CopyMemoryFromPtr sa4(0), pSockaddr, aiAddrLen
                    sa4Len = aiAddrLen
                    found4 = True
                End If
            End If
            If found6 And found4 Then Exit Do
            pCur = pNext
        Loop
        sock_freeaddrinfo ppResult
    End If

    If Not found6 And Not found4 Then
        sin4.sin_family = AF_INET
        sin4.sin_port = sock_htons(port)
        sin4.sin_addr = ResolveHostname(hostname, handle)
        If sin4.sin_addr = 0 Then Exit Function
        sa4Len = LenB(sin4)
        ReDim sa4(0 To sa4Len - 1)
        CopyMemory sa4(0), sin4, sa4Len
        found4 = True
    End If

    If found6 Then
        sock6 = sock_socket(AF_INET6, SOCK_STREAM, IPPROTO_TCP)
        If sock6 <> INVALID_SOCKET Then
            nbMode = 1
            sock_ioctlsocket sock6, FIONBIO, nbMode
            sock_connect sock6, VarPtr(sa6(0)), sa6Len
        Else
            found6 = False
        End If
    End If

    If found6 And found4 Then
        startTick = GetTickCount()
        Do
            writeSet.fd_count = 1
            writeSet.fd_array(0) = sock6
            exceptSet.fd_count = 1
            exceptSet.fd_array(0) = sock6
            tv.tv_sec = 0
            tv.tv_usec = 50000
            selResult = sock_select(0, ByVal 0&, writeSet, exceptSet, tv)
            If selResult > 0 And exceptSet.fd_count = 0 Then
                nbMode = 0
                sock_ioctlsocket sock6, FIONBIO, nbMode
                m_Connections(handle).Socket = sock6
                ResolveAndConnect = True
                Exit Function
            End If
            If selResult > 0 And exceptSet.fd_count > 0 Then
                sock_closesocket sock6
                sock6 = INVALID_SOCKET
                found6 = False
                Exit Do
            End If
            elapsed = TickDiff(startTick, GetTickCount())
            If elapsed >= HAPPY_EYEBALLS_DELAY_MS Then Exit Do
            DoEvents
        Loop
    End If

    If Not ResolveAndConnect And found4 Then
        sock4 = sock_socket(AF_INET, SOCK_STREAM, IPPROTO_TCP)
        If sock4 <> INVALID_SOCKET Then
            nbMode = 1
            sock_ioctlsocket sock4, FIONBIO, nbMode
            sock_connect sock4, VarPtr(sa4(0)), sa4Len
        End If
    End If

    If Not ResolveAndConnect Then
        Dim raceTimeout As Long
        raceTimeout = 10000
        startTick = GetTickCount()
        Do
            If sock6 <> INVALID_SOCKET Then
                writeSet.fd_count = 1
                writeSet.fd_array(0) = sock6
                exceptSet.fd_count = 1
                exceptSet.fd_array(0) = sock6
                tv.tv_sec = 0
                tv.tv_usec = 50000
                selResult = sock_select(0, ByVal 0&, writeSet, exceptSet, tv)
                If selResult > 0 And exceptSet.fd_count = 0 Then
                    nbMode = 0
                    sock_ioctlsocket sock6, FIONBIO, nbMode
                    m_Connections(handle).Socket = sock6
                    If sock4 <> INVALID_SOCKET Then sock_closesocket sock4
                    ResolveAndConnect = True
                    Exit Function
                End If
                If selResult > 0 And exceptSet.fd_count > 0 Then
                    sock_closesocket sock6
                    sock6 = INVALID_SOCKET
                End If
            End If
            If sock4 <> INVALID_SOCKET Then
                writeSet.fd_count = 1
                writeSet.fd_array(0) = sock4
                exceptSet.fd_count = 1
                exceptSet.fd_array(0) = sock4
                tv.tv_sec = 0
                tv.tv_usec = 50000
                selResult = sock_select(0, ByVal 0&, writeSet, exceptSet, tv)
                If selResult > 0 And exceptSet.fd_count = 0 Then
                    nbMode = 0
                    sock_ioctlsocket sock4, FIONBIO, nbMode
                    m_Connections(handle).Socket = sock4
                    If sock6 <> INVALID_SOCKET Then sock_closesocket sock6
                    ResolveAndConnect = True
                    Exit Function
                End If
                If selResult > 0 And exceptSet.fd_count > 0 Then
                    sock_closesocket sock4
                    sock4 = INVALID_SOCKET
                End If
            End If
            If sock6 = INVALID_SOCKET And sock4 = INVALID_SOCKET Then Exit Do
            elapsed = TickDiff(startTick, GetTickCount())
            If elapsed >= raceTimeout Then Exit Do
            DoEvents
        Loop
    End If

    If Not ResolveAndConnect Then
        If sock6 <> INVALID_SOCKET Then sock_closesocket sock6
        If sock4 <> INVALID_SOCKET Then sock_closesocket sock4
        wsaErr = WSAGetLastError()
        SetError ERR_CONNECT_FAILED, "Connect failed: " & WSAErrDesc(wsaErr), "Could not connect to server." & vbCrLf & WSAErrDesc(wsaErr), handle, wsaErr
    End If
End Function

Private Function DoProxyHTTP(ByVal handle As Long) As Boolean
    Dim req As String
    Dim sendBuf() As Byte
    Dim recvBuf() As Byte
    Dim received As Long
    Dim response As String
    Dim sendResult As Long
    Dim wsaErr As Long
    With m_Connections(handle)
        req = "CONNECT " & .HOST & ":" & .port & " HTTP/1.1" & vbCrLf
        req = req & "Host: " & .HOST & ":" & .port & vbCrLf
        If .proxyUser <> "" And Not .ProxyUseNtlm Then
            req = req & "Proxy-Authorization: Basic " & Base64Encode(StrConv(.proxyUser & ":" & .proxyPass, vbFromUnicode)) & vbCrLf
        End If
        If .ProxyUseNtlm Then
            req = req & "Proxy-Authorization: NTLM TlRMTVNTUAABAAAAB4IIogAAAAAAAAAAAAAAAAAAAAAFASgKAAAADw==" & vbCrLf
        End If
        req = req & vbCrLf
        sendBuf = StrConv(req, vbFromUnicode)
        sendResult = sock_send(.Socket, sendBuf(0), UBound(sendBuf) + 1, 0)
        If sendResult <= 0 Then
            wsaErr = WSAGetLastError()
            SetError ERR_PROXY_CONNECT_FAILED, "send() to proxy failed with WSA error " & wsaErr, "Failed to send CONNECT to proxy.", handle, wsaErr
            Exit Function
        End If
        If Not WaitForDataOn(handle, 5000) Then
            SetError ERR_PROXY_CONNECT_FAILED, "Proxy CONNECT timeout", "Proxy did not respond to CONNECT request.", handle
            Exit Function
        End If
        ReDim recvBuf(0 To 4095)
        received = sock_recv(.Socket, recvBuf(0), 4096, 0)
        If received <= 0 Then
            wsaErr = WSAGetLastError()
            SetError ERR_PROXY_CONNECT_FAILED, "recv() from proxy failed with WSA error " & wsaErr, "Failed to receive proxy response.", handle, wsaErr
            Exit Function
        End If
        response = Left(StrConv(recvBuf, vbUnicode), received)
        If InStr(response, "407") > 0 Then
            If .ProxyUseNtlm Then
                Dim ntlmHeader As String
                Dim hPos As Long
                Dim lf As Long
                Dim ntlmToken As String
                hPos = InStr(LCase(response), "proxy-authenticate: ntlm")
                If hPos > 0 Then
                    ntlmHeader = Mid(response, hPos)
                    lf = InStr(ntlmHeader, vbCrLf)
                    If lf > 0 Then ntlmHeader = Left(ntlmHeader, lf - 1)
                    ntlmToken = GenerateNtlmToken(handle, ntlmHeader, .proxyHost)
                    If ntlmToken <> "" Then
                        req = "CONNECT " & .HOST & ":" & .port & " HTTP/1.1" & vbCrLf
                        req = req & "Host: " & .HOST & ":" & .port & vbCrLf
                        req = req & "Proxy-Authorization: " & ntlmToken & vbCrLf
                        req = req & vbCrLf
                        sendBuf = StrConv(req, vbFromUnicode)
                        sendResult = sock_send(.Socket, sendBuf(0), UBound(sendBuf) + 1, 0)
                        If sendResult <= 0 Then Exit Function
                        If Not WaitForDataOn(handle, 5000) Then Exit Function
                        received = sock_recv(.Socket, recvBuf(0), 4096, 0)
                        If received <= 0 Then Exit Function
                        response = Left(StrConv(recvBuf, vbUnicode), received)
                    End If
                End If
            Else
                SetError ERR_PROXY_AUTH_FAILED, "Proxy returned 407 Proxy Authentication Required", "Proxy authentication failed." & vbCrLf & "Please check your proxy credentials.", handle
                Exit Function
            End If
        End If
        If InStr(response, "200") = 0 Then
            Dim statusLine As String
            Dim lineEnd As Long
            lineEnd = InStr(response, vbCrLf)
            If lineEnd > 0 Then
                statusLine = Left(response, lineEnd - 1)
            Else
                statusLine = Left(response, 50)
            End If
            SetError ERR_PROXY_TUNNEL_FAILED, "Proxy CONNECT rejected: " & statusLine, "Proxy refused the tunnel connection." & vbCrLf & "Server: " & .HOST & ":" & .port, handle
            Exit Function
        End If
    End With
    DoProxyHTTP = True
End Function

Private Function DoProxySOCKS5(ByVal handle As Long) As Boolean
    Dim sendBuf() As Byte
    Dim recvBuf(0 To 255) As Byte
    Dim received As Long
    Dim wsaErr As Long
    Dim hostBytes() As Byte
    Dim hostLen As Byte
    Dim i As Long
    With m_Connections(handle)
        If .proxyUser <> "" Then
            ReDim sendBuf(0 To 3)
            sendBuf(0) = 5
            sendBuf(1) = 2
            sendBuf(2) = 0
            sendBuf(3) = 2
        Else
            ReDim sendBuf(0 To 2)
            sendBuf(0) = 5
            sendBuf(1) = 1
            sendBuf(2) = 0
        End If
        If sock_send(.Socket, sendBuf(0), UBound(sendBuf) + 1, 0) <= 0 Then
            wsaErr = WSAGetLastError()
            SetError ERR_PROXY_CONNECT_FAILED, "SOCKS5 greeting failed: " & wsaErr, "SOCKS5 handshake failed.", handle, wsaErr
            Exit Function
        End If
        If Not WaitForDataOn(handle, 5000) Then
            SetError ERR_PROXY_CONNECT_FAILED, "SOCKS5 greeting timeout", "SOCKS5 server did not respond.", handle
            Exit Function
        End If
        received = sock_recv(.Socket, recvBuf(0), 256, 0)
        If received < 2 Or recvBuf(0) <> 5 Then
            SetError ERR_PROXY_CONNECT_FAILED, "SOCKS5 invalid greeting response", "SOCKS5 handshake failed.", handle
            Exit Function
        End If
        If recvBuf(1) = 255 Then
            SetError ERR_PROXY_AUTH_FAILED, "SOCKS5 no acceptable auth method", "SOCKS5 authentication failed.", handle
            Exit Function
        End If
        If recvBuf(1) = 2 Then
            Dim userB() As Byte
            Dim passB() As Byte
            Dim uLen As Byte
            Dim pLen As Byte
            userB = StrConv(.proxyUser, vbFromUnicode)
            passB = StrConv(.proxyPass, vbFromUnicode)
            uLen = CByte(UBound(userB) + 1)
            pLen = CByte(UBound(passB) + 1)
            ReDim sendBuf(0 To 3 + uLen + pLen)
            sendBuf(0) = 1
            sendBuf(1) = uLen
            For i = 0 To uLen - 1
                sendBuf(2 + i) = userB(i)
            Next i
            sendBuf(2 + uLen) = pLen
            For i = 0 To pLen - 1
                sendBuf(3 + uLen + i) = passB(i)
            Next i
            If sock_send(.Socket, sendBuf(0), UBound(sendBuf) + 1, 0) <= 0 Then
                wsaErr = WSAGetLastError()
                SetError ERR_PROXY_AUTH_FAILED, "SOCKS5 auth send failed: " & wsaErr, "SOCKS5 authentication failed.", handle, wsaErr
                Exit Function
            End If
            If Not WaitForDataOn(handle, 5000) Then
                SetError ERR_PROXY_AUTH_FAILED, "SOCKS5 auth timeout", "SOCKS5 authentication timed out.", handle
                Exit Function
            End If
            received = sock_recv(.Socket, recvBuf(0), 256, 0)
            If received < 2 Or recvBuf(1) <> 0 Then
                SetError ERR_PROXY_AUTH_FAILED, "SOCKS5 auth rejected", "SOCKS5 authentication failed. Check credentials.", handle
                Exit Function
            End If
        End If
        hostBytes = StrConv(.HOST, vbFromUnicode)
        If UBound(hostBytes) + 1 > 255 Then
            SetError ERR_PROXY_CONNECT_FAILED, "Hostname too long for SOCKS5: " & Len(.HOST) & " chars", "Proxy hostname exceeds SOCKS5 limit.", handle
            Exit Function
        End If
        hostLen = CByte(UBound(hostBytes) + 1)
        ReDim sendBuf(0 To 6 + hostLen + 1)
        sendBuf(0) = 5
        sendBuf(1) = 1
        sendBuf(2) = 0
        sendBuf(3) = 3
        sendBuf(4) = hostLen
        For i = 0 To hostLen - 1
            sendBuf(5 + i) = hostBytes(i)
        Next i
        sendBuf(5 + hostLen) = CByte((.port \ 256) And &HFF)
        sendBuf(6 + hostLen) = CByte(.port And &HFF)
        If sock_send(.Socket, sendBuf(0), UBound(sendBuf) + 1, 0) <= 0 Then
            wsaErr = WSAGetLastError()
            SetError ERR_PROXY_CONNECT_FAILED, "SOCKS5 CONNECT send failed: " & wsaErr, "SOCKS5 connect request failed.", handle, wsaErr
            Exit Function
        End If
        If Not WaitForDataOn(handle, 5000) Then
            SetError ERR_PROXY_CONNECT_FAILED, "SOCKS5 CONNECT timeout", "SOCKS5 server did not respond to CONNECT.", handle
            Exit Function
        End If
        received = sock_recv(.Socket, recvBuf(0), 256, 0)
        If received < 4 Then
            SetError ERR_PROXY_CONNECT_FAILED, "SOCKS5 CONNECT response too short", "SOCKS5 connect failed.", handle
            Exit Function
        End If
        If recvBuf(0) <> 5 Then
            SetError ERR_PROXY_CONNECT_FAILED, "SOCKS5 CONNECT wrong version: " & recvBuf(0), "SOCKS5 connect failed.", handle
            Exit Function
        End If
        If recvBuf(1) <> 0 Then
            SetError ERR_PROXY_CONNECT_FAILED, "SOCKS5 CONNECT rejected, code: " & recvBuf(1), "SOCKS5 server rejected connection, code: " & recvBuf(1), recvBuf(1), handle
            Exit Function
        End If
    End With
    DoProxySOCKS5 = True
End Function

Private Function GenerateNtlmToken(ByVal handle As Long, ByVal proxyAuthHeader As String, ByVal proxyHost As String) As String
    Dim hCred As SecHandle
    Dim hContext As SecHandle
    Dim tsExpiry As SECURITY_INTEGER
    Dim result As Long
    Dim outBuf As SecBuffer
    Dim outBufDesc As SecBufferDesc
    Dim inBuf(0 To 1) As SecBuffer
    Dim inBufDesc As SecBufferDesc
    Dim contextAttr As Long
    Dim targetName As String
    Dim serverToken() As Byte
    Dim b64token As String
    Dim outData() As Byte
    Dim recvLen As Long
    Dim i As Long
    targetName = "HTTP/" & proxyHost
    result = AcquireCredentialsHandle(NULL_PTR, "NTLM", SECPKG_CRED_OUTBOUND, NULL_PTR, ByVal 0&, 0, 0, hCred, tsExpiry)
    If result <> 0 Then Exit Function
    If InStr(proxyAuthHeader, "NTLM ") > 0 Then
        b64token = Mid(proxyAuthHeader, InStr(proxyAuthHeader, "NTLM ") + 5)
        serverToken = StrConv(b64token, vbFromUnicode)
    End If
    recvLen = UBound(serverToken) - LBound(serverToken) + 1
    Dim recvBuffer() As Byte
    If recvLen > 0 And Not IsEmpty(serverToken) Then
        ReDim recvBuffer(0 To recvLen - 1)
        CopyMemory recvBuffer(0), serverToken(0), recvLen
    End If
    outBufDesc.ulVersion = SECBUFFER_VERSION
    outBufDesc.cBuffers = 1
    outBufDesc.pBuffers = VarPtr(outBuf)
    outBuf.cbBuffer = 0
    outBuf.BufferType = SECBUFFER_TOKEN
    outBuf.pvBuffer = 0
    If recvLen = 0 Then
        result = InitializeSecurityContext(hCred, NULL_PTR, targetName, ISC_REQ_SEQUENCE_DETECT Or ISC_REQ_REPLAY_DETECT Or ISC_REQ_CONFIDENTIALITY Or ISC_REQ_ALLOCATE_MEMORY Or ISC_REQ_STREAM, 0, 0, NULL_PTR, 0, hContext, outBufDesc, contextAttr, tsExpiry)
    Else
        inBufDesc.ulVersion = SECBUFFER_VERSION
        inBufDesc.cBuffers = 2
        inBufDesc.pBuffers = VarPtr(inBuf(0))
        inBuf(0).cbBuffer = recvLen
        inBuf(0).BufferType = SECBUFFER_TOKEN
        inBuf(0).pvBuffer = VarPtr(recvBuffer(0))
        inBuf(1).cbBuffer = 0
        inBuf(1).BufferType = SECBUFFER_EMPTY
        inBuf(1).pvBuffer = 0
        result = InitializeSecurityContextContinue(hCred, hContext, targetName, ISC_REQ_SEQUENCE_DETECT Or ISC_REQ_REPLAY_DETECT Or ISC_REQ_CONFIDENTIALITY Or ISC_REQ_ALLOCATE_MEMORY Or ISC_REQ_STREAM, 0, 0, inBufDesc, 0, hContext, outBufDesc, contextAttr, tsExpiry)
    End If
    If outBuf.cbBuffer > 0 Then
        ReDim outData(0 To outBuf.cbBuffer - 1)
        CopyMemoryFromPtr outData(0), outBuf.pvBuffer, outBuf.cbBuffer
        GenerateNtlmToken = "NTLM " & Base64Encode(outData)
        FreeContextBuffer outBuf.pvBuffer
    End If
    DeleteSecurityContext hContext
    FreeCredentialsHandle hCred
End Function

Private Function LoadClientCert(ByVal handle As Long) As Boolean
#If VBA7 Then
    Dim outCtx As LongPtr
    Dim outStore As LongPtr
#Else
    Dim outCtx As Long
    Dim outStore As Long
#End If
    Dim fileNum As Integer
    Dim pfxBytes() As Byte
    Dim blob As CRYPT_DATA_BLOB
    Dim fileLen As Long
#If VBA7 Then
    Dim pwPtr As LongPtr
#Else
    Dim pwPtr As Long
#End If
    outCtx = 0
    outStore = 0
    With m_Connections(handle)
        If .ClientCertPfxPath <> "" Then
            If Dir(.ClientCertPfxPath) = "" Then
                SetError ERR_CERT_LOAD_FAILED, "PFX file not found: " & .ClientCertPfxPath, "Client certificate file not found.", handle
                Exit Function
            End If
            fileNum = FreeFile
            Open .ClientCertPfxPath For Binary Access Read As #fileNum
            fileLen = LOF(fileNum)
            If fileLen = 0 Then
                Close #fileNum
                SetError ERR_CERT_LOAD_FAILED, "PFX file is empty", "Client certificate file is empty.", handle
                Exit Function
            End If
            ReDim pfxBytes(0 To fileLen - 1)
            Get #fileNum, , pfxBytes
            Close #fileNum
            blob.cbData = fileLen
            blob.pbData = VarPtr(pfxBytes(0))
            pwPtr = IIf(Len(.ClientCertPfxPass) > 0, StrPtr(.ClientCertPfxPass), NULL_PTR)
            outStore = PFXImportCertStore(blob, pwPtr, CRYPT_EXPORTABLE Or PKCS12_ALLOW_OVERWRITE_KEY)
            If outStore = 0 Then
                SetError ERR_CERT_LOAD_FAILED, "PFXImportCertStore failed: 0x" & Hex(Err.LastDllError), "Failed to import client certificate PFX.", handle, Err.LastDllError
                Exit Function
            End If
            outCtx = CertFindCertificateInStore(outStore, X509_ASN_ENCODING Or PKCS_7_ASN_ENCODING, 0, CERT_FIND_ANY, ByVal NULL_PTR, 0)
            If outCtx = 0 Then
                SetError ERR_CERT_LOAD_FAILED, "CertFindCertificateInStore (ANY) failed", "No certificate found in PFX.", handle
                CertCloseStore outStore, 0
                Exit Function
            End If
        ElseIf .ClientCertThumb <> "" Then
            outStore = CertOpenStore(CERT_STORE_PROV_SYSTEM, 0, NULL_PTR, CERT_SYSTEM_STORE_CURRENT_USER, StrPtr("MY"))
            If outStore = 0 Then
                SetError ERR_CERT_LOAD_FAILED, "CertOpenStore (MY) failed: 0x" & Hex(Err.LastDllError), "Cannot open Windows certificate store.", handle, Err.LastDllError
                Exit Function
            End If
            outCtx = CertFindCertificateInStore(outStore, X509_ASN_ENCODING Or PKCS_7_ASN_ENCODING, 0, CERT_FIND_SUBJECT_STR_A, ByVal StrPtr(.ClientCertThumb), 0)
            If outCtx = 0 Then
                SetError ERR_CERT_LOAD_FAILED, "Certificate not found for subject: " & .ClientCertThumb, "Client certificate not found in store.", handle
                CertCloseStore outStore, 0
                Exit Function
            End If
        Else
            Exit Function
        End If
        .pClientCertCtx = outCtx
        .hClientCertStore = outStore
        m_ClientCertContextPtrs(handle) = outCtx
    End With
    LoadClientCert = True
End Function

Private Function ValidateServerCert(ByVal handle As Long) As Boolean
#If VBA7 Then
    Dim pRemoteCert As LongPtr
    Dim pChainCtx As LongPtr
#Else
    Dim pRemoteCert As Long
    Dim pChainCtx As Long
#End If
    Dim chainPara As CERT_CHAIN_PARA
    Dim sslExtra As SSL_EXTRA_CERT_CHAIN_POLICY_PARA
    Dim policyPara As CERT_CHAIN_POLICY_PARA
    Dim policyStatus As CERT_CHAIN_POLICY_STATUS
    Dim result As Long
    Dim chainFlags As Long
    With m_Connections(handle)
        pRemoteCert = 0
        result = QueryContextAttributes(.hContext, SECPKG_ATTR_REMOTE_CERT_CONTEXT, pRemoteCert)
        If result <> 0 Or pRemoteCert = 0 Then
            SetError ERR_CERT_VALIDATE_FAILED, "QueryContextAttributes(REMOTE_CERT) failed: 0x" & Hex(result), "Cannot retrieve server certificate.", handle, result
            Exit Function
        End If
        chainPara.cbSize = LenB(chainPara)
        pChainCtx = 0
        chainFlags = 0
        If .EnableRevocationCheck Then
            chainFlags = CERT_CHAIN_REVOCATION_CHECK_CHAIN
        End If
        result = CertGetCertificateChain(NULL_PTR, pRemoteCert, 0, 0, chainPara, chainFlags, NULL_PTR, pChainCtx)
        If result = 0 Or pChainCtx = 0 Then
            SetError ERR_CERT_VALIDATE_FAILED, "CertGetCertificateChain failed: 0x" & Hex(Err.LastDllError), "Cannot build certificate chain.", handle
            CertFreeCertificateContext pRemoteCert
            Exit Function
        End If
        sslExtra.cbSize = LenB(sslExtra)
        sslExtra.dwAuthType = AUTHTYPE_SERVER
        sslExtra.fdwChecks = 0
        sslExtra.pwszServerName = StrPtr(.HOST)
        policyPara.cbSize = LenB(policyPara)
        policyPara.dwFlags = 0
        policyPara.pvExtraPolicyPara = VarPtr(sslExtra)
        policyStatus.cbSize = LenB(policyStatus)
        result = CertVerifyCertificateChainPolicy(CERT_CHAIN_POLICY_SSL, pChainCtx, policyPara, policyStatus)
        CertFreeCertificateChain pChainCtx
        CertFreeCertificateContext pRemoteCert
        If result = 0 Then
            SetError ERR_CERT_VALIDATE_FAILED, "CertVerifyCertificateChainPolicy failed: 0x" & Hex(Err.LastDllError), "Certificate policy check failed.", handle
            Exit Function
        End If
        If policyStatus.dwError <> 0 Then
            SetError ERR_CERT_VALIDATE_FAILED, "Cert validation error 0x" & Hex(policyStatus.dwError) & " chain=" & policyStatus.lChainIndex & " elem=" & policyStatus.lElementIndex, "Server certificate is not trusted (0x" & Hex(policyStatus.dwError) & ").", handle, policyStatus.dwError
            Exit Function
        End If
    End With
    ValidateServerCert = True
End Function

Private Function DoTLSHandshake(ByVal handle As Long) As Long
    Dim outBuf As SecBuffer
    Dim outBufDesc As SecBufferDesc
    Dim inBuf(0 To 1) As SecBuffer
    Dim inBufDesc As SecBufferDesc
    Dim contextAttr As Long
    Dim tsExpiry As SECURITY_INTEGER
    Dim result As Long
    Dim contextFlags As Long
    Dim recvBuffer() As Byte
    Dim recvLen As Long
    Dim outData() As Byte
    Dim firstCall As Boolean
    Dim recv As Long
    Dim loopCount As Long
    Dim i As Long
    Dim extraData As Long
    contextFlags = ISC_REQ_SEQUENCE_DETECT Or ISC_REQ_REPLAY_DETECT Or ISC_REQ_CONFIDENTIALITY Or ISC_REQ_ALLOCATE_MEMORY Or ISC_REQ_STREAM
    ReDim recvBuffer(0 To 32767)
    recvLen = 0
    firstCall = True
    loopCount = 0
    With m_Connections(handle)
        Do
            loopCount = loopCount + 1
            If firstCall Then
                outBufDesc.ulVersion = SECBUFFER_VERSION
                outBufDesc.cBuffers = 1
                outBufDesc.pBuffers = VarPtr(outBuf)
                outBuf.cbBuffer = 0
                outBuf.BufferType = SECBUFFER_TOKEN
                outBuf.pvBuffer = 0
                result = InitializeSecurityContext(.hCred, NULL_PTR, .HOST, contextFlags, 0, 0, NULL_PTR, 0, .hContext, outBufDesc, contextAttr, tsExpiry)
                firstCall = False
            Else
                inBufDesc.ulVersion = SECBUFFER_VERSION
                inBufDesc.cBuffers = 2
                inBufDesc.pBuffers = VarPtr(inBuf(0))
                inBuf(0).cbBuffer = recvLen
                inBuf(0).BufferType = SECBUFFER_TOKEN
                inBuf(0).pvBuffer = VarPtr(recvBuffer(0))
                inBuf(1).cbBuffer = 0
                inBuf(1).BufferType = SECBUFFER_EMPTY
                inBuf(1).pvBuffer = 0
                outBufDesc.ulVersion = SECBUFFER_VERSION
                outBufDesc.cBuffers = 1
                outBufDesc.pBuffers = VarPtr(outBuf)
                outBuf.cbBuffer = 0
                outBuf.BufferType = SECBUFFER_TOKEN
                outBuf.pvBuffer = 0
                result = InitializeSecurityContextContinue(.hCred, .hContext, .HOST, contextFlags, 0, 0, inBufDesc, 0, .hContext, outBufDesc, contextAttr, tsExpiry)
                extraData = 0
                For i = 0 To 1
                    If inBuf(i).BufferType = SECBUFFER_EXTRA And inBuf(i).cbBuffer > 0 Then
                        extraData = inBuf(i).cbBuffer
                        Exit For
                    End If
                Next i
                If extraData > 0 Then
                    For i = 0 To extraData - 1
                        recvBuffer(i) = recvBuffer(recvLen - extraData + i)
                    Next i
                    recvLen = extraData
                ElseIf result <> SEC_E_INCOMPLETE_MESSAGE Then
                    recvLen = 0
                End If
            End If
            If result < 0 And result <> SEC_E_INCOMPLETE_MESSAGE Then
                DoTLSHandshake = result
                Exit Function
            End If
            If outBuf.cbBuffer > 0 And outBuf.pvBuffer <> 0 Then
                ReDim outData(0 To outBuf.cbBuffer - 1)
                CopyMemoryFromPtr outData(0), outBuf.pvBuffer, outBuf.cbBuffer
                sock_send .Socket, outData(0), outBuf.cbBuffer, 0
                FreeContextBuffer outBuf.pvBuffer
            End If
            If result = SEC_E_OK Then
                DoTLSHandshake = 0
                Exit Function
            End If
            If result = SEC_I_CONTINUE_NEEDED Or result = SEC_E_INCOMPLETE_MESSAGE Then
                If Not WaitForDataOn(handle, 10000) Then
                    DoTLSHandshake = -1
                    Exit Function
                End If
                recv = sock_recv(.Socket, recvBuffer(recvLen), 32768 - recvLen, 0)
                If recv <= 0 Then
                    DoTLSHandshake = -1
                    Exit Function
                End If
                recvLen = recvLen + recv
            End If
            If loopCount > 30 Then
                DoTLSHandshake = -1
                Exit Function
            End If
        Loop While result = SEC_I_CONTINUE_NEEDED Or result = SEC_E_INCOMPLETE_MESSAGE
    End With
    DoTLSHandshake = result
End Function

Private Function TLSSend(ByVal handle As Long, ByRef data() As Byte) As Boolean
    Dim buffers(0 To 3) As SecBuffer
    Dim bufferDesc As SecBufferDesc
    Dim sendBuf() As Byte
    Dim dataLen As Long
    Dim totalLen As Long
    Dim offset As Long
    Dim chunkSize As Long
    Dim maxChunk As Long
    Dim result As Long
    Dim toSend As Long
    Dim totalSent As Long
    Dim sent As Long
    Dim wsaErr As Long
    Dim i As Long
    With m_Connections(handle)
        dataLen = SafeArrayLen(data)
        If dataLen = 0 Then
            TLSSend = True
            Exit Function
        End If
        maxChunk = .sizes.cbMaximumMessage
        If maxChunk <= 0 Then
            maxChunk = 16384
        End If
        offset = 0
        Do While offset < dataLen
            chunkSize = maxChunk
            If offset + chunkSize > dataLen Then
                chunkSize = dataLen - offset
            End If
            totalLen = .sizes.cbHeader + chunkSize + .sizes.cbTrailer
            ReDim sendBuf(0 To totalLen - 1)
            For i = 0 To chunkSize - 1
                sendBuf(.sizes.cbHeader + i) = data(LBound(data) + offset + i)
            Next i
            buffers(0).cbBuffer = .sizes.cbHeader
            buffers(0).BufferType = SECBUFFER_STREAM_HEADER
            buffers(0).pvBuffer = VarPtr(sendBuf(0))
            buffers(1).cbBuffer = chunkSize
            buffers(1).BufferType = SECBUFFER_DATA
            buffers(1).pvBuffer = VarPtr(sendBuf(.sizes.cbHeader))
            buffers(2).cbBuffer = .sizes.cbTrailer
            buffers(2).BufferType = SECBUFFER_STREAM_TRAILER
            buffers(2).pvBuffer = VarPtr(sendBuf(.sizes.cbHeader + chunkSize))
            buffers(3).cbBuffer = 0
            buffers(3).BufferType = SECBUFFER_EMPTY
            buffers(3).pvBuffer = 0
            bufferDesc.ulVersion = SECBUFFER_VERSION
            bufferDesc.cBuffers = 4
            bufferDesc.pBuffers = VarPtr(buffers(0))
            result = EncryptMessage(.hContext, 0, bufferDesc, 0)
            If result <> 0 Then
                SetError ERR_TLS_ENCRYPT_FAILED, "EncryptMessage failed: 0x" & Hex(result), "TLS encryption error.", handle, result
                Exit Function
            End If
            toSend = buffers(0).cbBuffer + buffers(1).cbBuffer + buffers(2).cbBuffer
            totalSent = 0
            Do While totalSent < toSend
                sent = sock_send(.Socket, sendBuf(totalSent), toSend - totalSent, 0)
                If sent <= 0 Then
                    wsaErr = WSAGetLastError()
                    SetError ERR_SEND_FAILED, "send() after TLS encrypt failed: " & WSAErrDesc(wsaErr), "Failed to send encrypted data.", handle, wsaErr
                    .state = STATE_CLOSED
                    Exit Function
                End If
                totalSent = totalSent + sent
            Loop
            offset = offset + chunkSize
        Loop
    End With
    TLSSend = True
End Function

Private Sub TLSDecrypt(ByVal handle As Long)
    Dim buffers(0 To 3) As SecBuffer
    Dim bufferDesc As SecBufferDesc
    Dim result As Long
    Dim qop As Long
    Dim i As Long
    Dim dataLen As Long
    Dim extraLen As Long
    With m_Connections(handle)
        Do While .recvLen > 0
            buffers(0).cbBuffer = .recvLen
            buffers(0).BufferType = SECBUFFER_DATA
            buffers(0).pvBuffer = VarPtr(.recvBuffer(0))
            buffers(1).cbBuffer = 0
            buffers(1).BufferType = SECBUFFER_EMPTY
            buffers(1).pvBuffer = 0
            buffers(2).cbBuffer = 0
            buffers(2).BufferType = SECBUFFER_EMPTY
            buffers(2).pvBuffer = 0
            buffers(3).cbBuffer = 0
            buffers(3).BufferType = SECBUFFER_EMPTY
            buffers(3).pvBuffer = 0
            bufferDesc.ulVersion = SECBUFFER_VERSION
            bufferDesc.cBuffers = 4
            bufferDesc.pBuffers = VarPtr(buffers(0))
            result = DecryptMessage(.hContext, bufferDesc, 0, qop)
            If result = SEC_E_INCOMPLETE_MESSAGE Then Exit Sub
            If result = SEC_I_CONTEXT_EXPIRED Then
                WasabiLog handle, "TLS context expired (Server closed connection nicely)."
                .state = STATE_CLOSED
                If .AutoReconnect And .Mode = MODE_WEBSOCKET Then TryReconnect handle
                Exit Sub
            End If
            If result = SEC_I_RENEGOTIATE Then
                SetError ERR_TLS_RENEGOTIATE, "TLS renegotiation requested - closing", "Secure connection interrupted (renegotiation).", handle, SEC_I_RENEGOTIATE
                .state = STATE_CLOSED
                If .AutoReconnect Then TryReconnect handle
                Exit Sub
            End If
            If result <> SEC_E_OK Then
                SetError ERR_TLS_DECRYPT_FAILED, "DecryptMessage failed: 0x" & Hex(result), "TLS decryption error.", handle, result
                Exit Sub
            End If
            For i = 0 To 3
                If buffers(i).BufferType = SECBUFFER_DATA Then
                    dataLen = buffers(i).cbBuffer
                    If dataLen > 0 Then
                        EnsureBufferCapacity .DecryptBuffer, .DecryptLen + dataLen
                        CopyMemoryFromPtr .DecryptBuffer(.DecryptLen), buffers(i).pvBuffer, dataLen
                        .DecryptLen = .DecryptLen + dataLen
                    End If
                End If
            Next i
            extraLen = 0
            For i = 0 To 3
                If buffers(i).BufferType = SECBUFFER_EXTRA Then
                    extraLen = buffers(i).cbBuffer
                    If extraLen > 0 Then
                        CopyMemoryFromPtr .recvBuffer(0), buffers(i).pvBuffer, extraLen
                    End If
                    Exit For
                End If
            Next i
            .recvLen = extraLen
        Loop
    End With
End Sub

Private Function ReceiveHTTPResponse(ByVal handle As Long) As String
    Dim tempBuf() As Byte
    Dim received As Long
    Dim headerEnd As Long
    Dim i As Long
    Dim headerBytes() As Byte
    Dim copyLen As Long
    Dim remainingLen As Long
    With m_Connections(handle)
        Do
            If Not WaitForDataOn(handle, 5000) Then Exit Do
            ReDim tempBuf(0 To 8191)
            received = sock_recv(.Socket, tempBuf(0), 8192, 0)
            If received <= 0 Then Exit Do
            copyLen = received
            If .recvLen + copyLen > BUFFER_SIZE Then
                copyLen = BUFFER_SIZE - .recvLen
            End If
            If copyLen > 0 Then
                CopyMemory .recvBuffer(.recvLen), tempBuf(0), copyLen
                .recvLen = .recvLen + copyLen
            End If
            If .TLS Then TLSDecrypt handle
            If .DecryptLen > 0 Then
                headerEnd = -1
                For i = 0 To .DecryptLen - 4
                    If .DecryptBuffer(i) = 13 And .DecryptBuffer(i + 1) = 10 And .DecryptBuffer(i + 2) = 13 And .DecryptBuffer(i + 3) = 10 Then
                        headerEnd = i + 4
                        Exit For
                    End If
                Next i
                If headerEnd > 0 Then
                    ReDim headerBytes(0 To headerEnd - 1)
                    CopyMemory headerBytes(0), .DecryptBuffer(0), headerEnd
                    ReceiveHTTPResponse = StrConv(headerBytes, vbUnicode)
                    remainingLen = .DecryptLen - headerEnd
                    If remainingLen > 0 Then
                        CopyMemory .DecryptBuffer(0), .DecryptBuffer(headerEnd), remainingLen
                    End If
                    .DecryptLen = remainingLen
                    Exit Function
                End If
            End If
        Loop
        If .DecryptLen > 0 Then
            ReDim headerBytes(0 To .DecryptLen - 1)
            CopyMemory headerBytes(0), .DecryptBuffer(0), .DecryptLen
            ReceiveHTTPResponse = StrConv(headerBytes, vbUnicode)
            .DecryptLen = 0
        End If
    End With
End Function

Private Function SHA1(ByRef data() As Byte) As Byte()
    #If VBA7 Then
        Dim hProv As LongPtr
        Dim hHash As LongPtr
    #Else
        Dim hProv As Long
        Dim hHash As Long
    #End If
    Dim result() As Byte
    Dim bufLen As Long
    Dim i As Long

    If CryptAcquireContextW(hProv, NULL_PTR, NULL_PTR, PROV_RSA_FULL, CRYPT_VERIFYCONTEXT) = 0 Then
        Erase result
        Exit Function
    End If

    If CryptCreateHash(hProv, CALG_SHA1, NULL_PTR, 0, hHash) = 0 Then
        CryptReleaseContext hProv, 0
        Erase result
        Exit Function
    End If

    Dim dataLen As Long
    dataLen = UBound(data) - LBound(data) + 1
    If CryptHashData(hHash, data(LBound(data)), dataLen, 0) = 0 Then
        CryptDestroyHash hHash
        CryptReleaseContext hProv, 0
        Erase result
        Exit Function
    End If

    bufLen = 20
    ReDim result(0 To bufLen - 1)
    Dim tmpLen As Long
    tmpLen = bufLen
    If CryptGetHashParam(hHash, HP_HASHVAL, result(0), tmpLen, 0) = 0 Then
        Erase result
    End If

    CryptDestroyHash hHash
    CryptReleaseContext hProv, 0
    SHA1 = result
End Function

Private Function GenerateWSKey() As String
    Dim Bytes(0 To 15) As Byte
    FillRandomBytes Bytes, 16
    GenerateWSKey = Base64Encode(Bytes)
End Function

Private Sub ParseDeflateResponse(ByVal handle As Long, ByVal response As String)
    Dim extStart As Long
    Dim extLine As String
    Dim lf As Long
    Dim swbPos As Long
    Dim swbVal As Long
    Dim cwbPos As Long
    Dim cwbVal As Long
    
    extStart = InStr(LCase(response), "sec-websocket-extensions: permessage-deflate")
    If extStart = 0 Then
        m_Connections(handle).DeflateEnabled = False
        m_Connections(handle).DeflateActive = False
        Exit Sub
    End If
    
    extLine = Mid(response, extStart)
    lf = InStr(extLine, vbCrLf)
    If lf > 0 Then
        extLine = Left(extLine, lf - 1)
    End If
    
    With m_Connections(handle)
        If InStr(LCase(extLine), "client_no_context_takeover") > 0 Then
            .DeflateContextTakeover = False
        Else
            .DeflateContextTakeover = True
        End If
        
        If InStr(LCase(extLine), "server_no_context_takeover") > 0 Then
            .InflateContextTakeover = False
        Else
            .InflateContextTakeover = True
        End If
        
        swbPos = InStr(LCase(extLine), "server_max_window_bits=")
        If swbPos > 0 Then
            swbVal = val(Mid(extLine, swbPos + 22))
            If swbVal >= 8 And swbVal <= 15 Then
                .DeflateWindowBits = -swbVal
                .ServerMaxWindowBits = swbVal
            End If
        End If
        
        cwbPos = InStr(LCase(extLine), "client_max_window_bits=")
        If cwbPos > 0 Then
            cwbVal = val(Mid(extLine, cwbPos + 22))
            If cwbVal >= 8 And cwbVal <= 15 Then
                .InflateWindowBits = -cwbVal
                .ClientMaxWindowBits = cwbVal
            End If
        End If
        .DeflateActive = True
    End With
End Sub

Private Function DoWebSocketHandshake(ByVal handle As Long) As Boolean
    Dim handshake As String
    Dim sendBuf() As Byte
    Dim response As String
    Dim wsKey As String
    Dim sendResult As Long
    Dim i As Long
    Dim hostHeader As String
    Dim originHeader As String
    Dim expectedAccept As String
    Dim actualAccept As String
    Dim acceptPos As Long
    Dim acceptLineEnd As Long
    Dim wsaErr As Long
    Dim recvBuf() As Byte
    Dim received As Long
    wsKey = GenerateWSKey()
    
    With m_Connections(handle)
        hostHeader = IIf((.TLS And .port <> 443) Or (Not .TLS And .port <> 80), .HOST & ":" & .port, .HOST)
        If .TLS Then
            originHeader = "https://" & IIf(.port <> 443, .HOST & ":" & .port, .HOST)
        Else
            originHeader = "http://" & IIf(.port <> 80, .HOST & ":" & .port, .HOST)
        End If
        handshake = "GET " & .path & " HTTP/1.1" & vbCrLf
        handshake = handshake & "Host: " & hostHeader & vbCrLf
        handshake = handshake & "Upgrade: websocket" & vbCrLf
        handshake = handshake & "Connection: Upgrade" & vbCrLf
        handshake = handshake & "Sec-WebSocket-Key: " & wsKey & vbCrLf
        handshake = handshake & "Sec-WebSocket-Version: 13" & vbCrLf
        If .DeflateEnabled Then
            Dim deflateExt As String
            deflateExt = "permessage-deflate"
            If Not .DeflateContextTakeover Then
                deflateExt = deflateExt & "; client_no_context_takeover"
            End If
            If Not .InflateContextTakeover Then
                deflateExt = deflateExt & "; server_no_context_takeover"
            End If
            If .ClientMaxWindowBits <> 15 Then
                deflateExt = deflateExt & "; client_max_window_bits=" & .ClientMaxWindowBits
            End If
            handshake = handshake & "Sec-WebSocket-Extensions: " & deflateExt & vbCrLf
        End If
        If .SubProtocol <> "" Then
            handshake = handshake & "Sec-WebSocket-Protocol: " & .SubProtocol & vbCrLf
        End If
        handshake = handshake & "Origin: " & originHeader & vbCrLf
        handshake = handshake & "User-Agent: Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36" & vbCrLf
        For i = 0 To .CustomHeaderCount - 1
            handshake = handshake & .CustomHeaders(i) & vbCrLf
        Next i
        handshake = handshake & vbCrLf
        
        sendBuf = StringToUtf8(handshake)
        
        If .TLS Then
            If Not TLSSend(handle, sendBuf) Then
                SetError ERR_WEBSOCKET_HANDSHAKE_FAILED, "TLS send of WS handshake failed", "WebSocket upgrade request failed.", handle
                Exit Function
            End If
            response = ReceiveHTTPResponse(handle)
        Else
            sendResult = sock_send(.Socket, sendBuf(0), UBound(sendBuf) + 1, 0)
            If sendResult <= 0 Then
                wsaErr = WSAGetLastError()
                SetError ERR_WEBSOCKET_HANDSHAKE_FAILED, "send() WS handshake failed: " & WSAErrDesc(wsaErr), "WebSocket upgrade request failed.", handle, wsaErr
                Exit Function
            End If
            If Not WaitForDataOn(handle, 5000) Then
                SetError ERR_WEBSOCKET_HANDSHAKE_TIMEOUT, "No WS handshake response within 5s", "WebSocket handshake timed out.", handle
                Exit Function
            End If
            ReDim recvBuf(0 To 4095)
            received = sock_recv(.Socket, recvBuf(0), 4096, 0)
            If received > 0 Then
                response = Left(StrConv(recvBuf, vbUnicode), received)
            Else
                wsaErr = WSAGetLastError()
                SetError ERR_WEBSOCKET_HANDSHAKE_FAILED, "recv() WS handshake failed: " & WSAErrDesc(wsaErr), "WebSocket handshake failed.", handle, wsaErr
                Exit Function
            End If
        End If
        If InStr(response, "101") = 0 Then
            Dim lineEnd As Long
            Dim statusLine As String
            lineEnd = InStr(response, vbCrLf)
            If lineEnd > 0 Then
                statusLine = Left(response, lineEnd - 1)
            Else
                statusLine = Left(response, 50)
            End If
            SetError ERR_HANDSHAKE_REJECTED, "Server rejected WS upgrade: " & statusLine, "WebSocket connection rejected: " & statusLine, handle
            Exit Function
        End If
        If .DeflateEnabled Then
            ParseDeflateResponse handle, response
        End If
        Dim wsAcceptInput() As Byte
        wsAcceptInput = StringToUtf8(wsKey & "258EAFA5-E914-47DA-95CA-C5AB0DC85B11")
        expectedAccept = Base64Encode(SHA1(wsAcceptInput))
        acceptPos = InStr(LCase(response), "sec-websocket-accept:")
        If acceptPos > 0 Then
            acceptLineEnd = InStr(acceptPos, response, vbCrLf)
            If acceptLineEnd > 0 Then
                actualAccept = Trim(Mid(response, acceptPos + 21, acceptLineEnd - acceptPos - 21))
            End If
        End If
        If actualAccept <> expectedAccept Then
            SetError ERR_HANDSHAKE_REJECTED, "Sec-WebSocket-Accept mismatch. Expected: " & expectedAccept & " Got: " & actualAccept, "WebSocket handshake failed: invalid accept key.", handle
            Exit Function
        End If
    End With
    DoWebSocketHandshake = True
End Function

Private Sub ProcessTextFrame(ByVal handle As Long, ByRef payload() As Byte, ByVal payloadLen As Long, ByVal fin As Boolean, ByVal isCompressed As Boolean)
    Dim textMsg As String
    Dim textPayload() As Byte
    Dim textPayloadLen As Long
    
    With m_Connections(handle)
        If Not fin Then
            .Fragmenting = True
            .FragmentOpcode = WS_OPCODE_TEXT
            .FragmentIsCompressed = isCompressed
            .FragmentLen = 0
            If payloadLen > 0 Then
                CopyMemory .FragmentBuffer(0), payload(0), payloadLen
                .FragmentLen = payloadLen
            End If
        Else
            If isCompressed And .DeflateActive Then
                Dim inflTextSingle() As Byte
                Dim inflTextSingleLen As Long
                inflTextSingle = InflatePayload(handle, payload, payloadLen, inflTextSingleLen)
                If inflTextSingleLen = 0 Then
                    WebSocketSendClose 1007, "Decompression failed", handle
                    .state = STATE_CLOSED
                    Exit Sub
                End If
                textPayload = inflTextSingle
            Else
                textPayload = payload
            End If
            
            RunInboundMiddlewares handle, textPayload
            textPayloadLen = SafeArrayLen(textPayload)
            
            If textPayloadLen > 0 Then
                textMsg = Utf8ToString(textPayload, textPayloadLen)
            Else
                textMsg = ""
            End If
            
            If Not .ProtocolHandler Is Nothing Then
                .ProtocolHandler.OnTextMessage handle, textMsg
            Else
                If .MsgCount < MSG_QUEUE_SIZE Then
                    .MsgQueue(.MsgTail) = textMsg
                    .MsgTail = (.MsgTail + 1) Mod MSG_QUEUE_SIZE
                    .MsgCount = .MsgCount + 1
                    .stats.MessagesReceived = .stats.MessagesReceived + 1
                Else
                    WasabiLog handle, "Warning: message queue full, dropping message (handle=" & handle & ")"
                End If
            End If
        End If
    End With
End Sub

Private Sub ProcessBinaryFrame(ByVal handle As Long, ByRef payload() As Byte, ByVal payloadLen As Long, ByVal fin As Boolean, ByVal isCompressed As Boolean)
    Dim binaryData() As Byte
    
    With m_Connections(handle)
        If Not fin Then
            .Fragmenting = True
            .FragmentOpcode = WS_OPCODE_BINARY
            .FragmentIsCompressed = isCompressed
            .FragmentLen = 0
            If payloadLen > 0 Then
                CopyMemory .FragmentBuffer(0), payload(0), payloadLen
                .FragmentLen = payloadLen
            End If
        Else
            If isCompressed And .DeflateActive Then
                Dim inflBinSingle() As Byte
                Dim inflBinSingleLen As Long
                inflBinSingle = InflatePayload(handle, payload, payloadLen, inflBinSingleLen)
                If inflBinSingleLen = 0 Then
                    WebSocketSendClose 1007, "Decompression failed", handle
                    .state = STATE_CLOSED
                    Exit Sub
                End If
                binaryData = inflBinSingle
            Else
                binaryData = payload
            End If
            
            RunInboundMiddlewares handle, binaryData
            
            If Not .ProtocolHandler Is Nothing Then
                .ProtocolHandler.OnBinaryMessage handle, binaryData
            Else
                If .BinaryCount < MSG_QUEUE_SIZE Then
                    .BinaryQueue(.BinaryTail).data = binaryData
                    .BinaryTail = (.BinaryTail + 1) Mod MSG_QUEUE_SIZE
                    .BinaryCount = .BinaryCount + 1
                    .stats.MessagesReceived = .stats.MessagesReceived + 1
                Else
                    WasabiLog handle, "Warning: binary queue full, dropping message (handle=" & handle & ")"
                End If
            End If
        End If
    End With
End Sub

Private Sub ProcessContinuationFrame(ByVal handle As Long, ByRef payload() As Byte, ByVal payloadLen As Long, ByVal fin As Boolean)
    Dim contPayload() As Byte
    Dim contPayloadLen As Long
    Dim textMsg As String
    Dim binaryData() As Byte
    
    With m_Connections(handle)
        If Not .Fragmenting Then Exit Sub
        
        If .FragmentLen + payloadLen > UBound(.FragmentBuffer) + 1 Then
            SetError ERR_FRAGMENT_OVERFLOW, "Fragment buffer overflow on CONTINUATION frame", "Received message too large.", handle
            .state = STATE_CLOSED
            Exit Sub
        End If
        
        If payloadLen > 0 Then
            CopyMemory .FragmentBuffer(.FragmentLen), payload(0), payloadLen
            .FragmentLen = .FragmentLen + payloadLen
        End If
        
        If fin Then
            If .FragmentIsCompressed And .DeflateActive Then
                Dim inflContBytes() As Byte
                Dim inflContLen As Long
                inflContBytes = InflatePayload(handle, .FragmentBuffer, .FragmentLen, inflContLen)
                If inflContLen = 0 Then
                    WebSocketSendClose 1007, "Decompression failed", handle
                    .state = STATE_CLOSED
                    .Fragmenting = False
                    .FragmentLen = 0
                    Exit Sub
                End If
                contPayload = inflContBytes
            Else
                If .FragmentLen > 0 Then
                    ReDim contPayload(0 To .FragmentLen - 1)
                    CopyMemory contPayload(0), .FragmentBuffer(0), .FragmentLen
                End If
            End If
            
            RunInboundMiddlewares handle, contPayload
            contPayloadLen = SafeArrayLen(contPayload)
            
            If .FragmentOpcode = WS_OPCODE_TEXT Then
                If contPayloadLen > 0 Then
                    textMsg = Utf8ToString(contPayload, contPayloadLen)
                Else
                    textMsg = ""
                End If
                
                If Not .ProtocolHandler Is Nothing Then
                    .ProtocolHandler.OnTextMessage handle, textMsg
                Else
                    If .MsgCount < MSG_QUEUE_SIZE Then
                        .MsgQueue(.MsgTail) = textMsg
                        .MsgTail = (.MsgTail + 1) Mod MSG_QUEUE_SIZE
                        .MsgCount = .MsgCount + 1
                        .stats.MessagesReceived = .stats.MessagesReceived + 1
                    Else
                        WasabiLog handle, "Warning: message queue full, dropping message (handle=" & handle & ")"
                    End If
                End If
            ElseIf .FragmentOpcode = WS_OPCODE_BINARY Then
                binaryData = contPayload
                
                If Not .ProtocolHandler Is Nothing Then
                    .ProtocolHandler.OnBinaryMessage handle, binaryData
                Else
                    If .BinaryCount < MSG_QUEUE_SIZE Then
                        .BinaryQueue(.BinaryTail).data = binaryData
                        .BinaryTail = (.BinaryTail + 1) Mod MSG_QUEUE_SIZE
                        .BinaryCount = .BinaryCount + 1
                        .stats.MessagesReceived = .stats.MessagesReceived + 1
                    Else
                        WasabiLog handle, "Warning: binary queue full, dropping message (handle=" & handle & ")"
                    End If
                End If
            End If
            .Fragmenting = False
            .FragmentLen = 0
        End If
    End With
End Sub

Private Sub ProcessFrames(ByVal handle As Long)
    Dim opcode As Byte
    Dim fin As Boolean
    Dim isCompressed As Boolean
    Dim payloadLen As Long
    Dim wirePayloadLen As Long
    Dim maskFlag As Boolean
    Dim frameStart As Long
    Dim i As Long
    Dim payload() As Byte
    Dim totalFrameLen As Long
    
    With m_Connections(handle)
        Do While .DecryptLen >= 2
            fin = (.DecryptBuffer(0) And &H80) <> 0
            isCompressed = (.DecryptBuffer(0) And &H40) <> 0
            opcode = .DecryptBuffer(0) And &HF
            maskFlag = (.DecryptBuffer(1) And &H80) <> 0
            payloadLen = .DecryptBuffer(1) And &H7F
            frameStart = 2
            
            If payloadLen = 126 Then
                If .DecryptLen < 4 Then Exit Do
                payloadLen = CLng(.DecryptBuffer(2)) * 256 + CLng(.DecryptBuffer(3))
                frameStart = 4
            ElseIf payloadLen = 127 Then
                If .DecryptLen < 10 Then Exit Do
                payloadLen = 0
                For i = 2 To 9
                    payloadLen = payloadLen * 256 + CLng(.DecryptBuffer(i))
                Next i
                frameStart = 10
            End If
            
            If maskFlag Then frameStart = frameStart + 4
            If .DecryptLen < frameStart + payloadLen Then Exit Do
            wirePayloadLen = payloadLen
            
            If payloadLen > 0 Then
                ReDim payload(0 To payloadLen - 1)
                CopyMemory payload(0), .DecryptBuffer(frameStart), payloadLen
            Else
                Erase payload
            End If
            
            Select Case opcode
                Case WS_OPCODE_TEXT
                    ProcessTextFrame handle, payload, payloadLen, fin, isCompressed
                Case WS_OPCODE_BINARY
                    ProcessBinaryFrame handle, payload, payloadLen, fin, isCompressed
                Case WS_OPCODE_CONTINUATION
                    ProcessContinuationFrame handle, payload, payloadLen, fin
                Case WS_OPCODE_CLOSE
                    If isCompressed Then
                        WebSocketSendClose 1002, "RSV1 on control frame", handle
                        .state = STATE_CLOSED
                        Exit Sub
                    End If
                    ProcessCloseFrame handle, payload, payloadLen
                    Exit Sub
                Case WS_OPCODE_PING
                    If isCompressed Then
                        WebSocketSendClose 1002, "RSV1 on control frame", handle
                        .state = STATE_CLOSED
                        Exit Sub
                    End If
                    SendPongFrame handle, payload, payloadLen
                Case WS_OPCODE_PONG
                    If isCompressed Then
                        WebSocketSendClose 1002, "RSV1 on control frame", handle
                        .state = STATE_CLOSED
                        Exit Sub
                    End If
                    ProcessPongForLatency handle
                    WasabiLog handle, "PONG received (handle=" & handle & ")"
            End Select
            
            totalFrameLen = frameStart + wirePayloadLen
            If .DecryptLen > totalFrameLen Then
                CopyMemory .DecryptBuffer(0), .DecryptBuffer(totalFrameLen), .DecryptLen - totalFrameLen
            End If
            .DecryptLen = .DecryptLen - totalFrameLen
        Loop
    End With
End Sub

Private Sub ProcessCloseFrame(ByVal handle As Long, ByRef payload() As Byte, ByVal payloadLen As Long)
    Dim closeCode As Integer
    Dim closeReason As String
    Dim replyFrame(0 To 7) As Byte
    Dim mask(0 To 3) As Byte
    Dim reasonBytes() As Byte
    Dim i As Long
    With m_Connections(handle)
        closeCode = 1005
        closeReason = ""
        If payloadLen >= 2 Then
            closeCode = CInt(payload(0)) * 256 + CInt(payload(1))
            If payloadLen > 2 Then
                ReDim reasonBytes(0 To payloadLen - 3)
                For i = 0 To payloadLen - 3
                    reasonBytes(i) = payload(2 + i)
                Next i
                closeReason = Utf8ToString(reasonBytes, payloadLen - 2)
            End If
        End If
        .closeCode = closeCode
        .closeReason = closeReason
        WasabiLog handle, "CLOSE received: " & closeCode & " (" & GetCloseCodeDesc(closeCode) & ") reason=""" & closeReason & """ (handle=" & handle & ")"
        If Not .CloseInitiatedByUs Then
            FillRandomBytes mask, 4
            replyFrame(0) = &H88
            replyFrame(1) = &H82
            replyFrame(2) = mask(0)
            replyFrame(3) = mask(1)
            replyFrame(4) = mask(2)
            replyFrame(5) = mask(3)
            If payloadLen >= 2 Then
                replyFrame(6) = payload(0) Xor mask(0)
                replyFrame(7) = payload(1) Xor mask(1)
            Else
                replyFrame(6) = CByte((1000 \ 256) And &HFF) Xor mask(0)
                replyFrame(7) = CByte(1000 And &HFF) Xor mask(1)
            End If
            Dim rf() As Byte
            ReDim rf(0 To 7)
            For i = 0 To 7
                rf(i) = replyFrame(i)
            Next i
            If .TLS Then
                TLSSend handle, rf
            Else
                sock_send .Socket, rf(0), 8, 0
            End If
        End If
        .state = STATE_CLOSED
    End With
End Sub

Private Sub SendPongFrame(ByVal handle As Long, ByRef pingPayload() As Byte, ByVal pingLen As Long)
    Dim frame() As Byte
    Dim mask(0 To 3) As Byte
    Dim i As Long
    FillRandomBytes mask, 4
    If pingLen = 0 Then
        ReDim frame(0 To 5)
        frame(0) = &H8A
        frame(1) = &H80
    Else
        ReDim frame(0 To 5 + pingLen)
        frame(0) = &H8A
        frame(1) = &H80 Or CByte(pingLen)
        For i = 0 To pingLen - 1
            frame(6 + i) = pingPayload(i) Xor mask(i Mod 4)
        Next i
    End If
    frame(2) = mask(0)
    frame(3) = mask(1)
    frame(4) = mask(2)
    frame(5) = mask(3)
    With m_Connections(handle)
        If .TLS Then
            TLSSend handle, frame
        Else
            sock_send .Socket, frame(0), UBound(frame) + 1, 0
        End If
    End With
End Sub

Private Sub ProcessPongForLatency(ByVal handle As Long)
    With m_Connections(handle)
        If .LastPingTimestamp > 0 Then
            .LastRttMs = TickDiff(.LastPingTimestamp, GetTickCount())
            .LastPingTimestamp = 0
        End If
    End With
End Sub

Private Sub FeedBuffer(ByVal handle As Long)
    Dim available As Long
    Dim tempBuf() As Byte
    Dim received As Long
    Dim wsaErr As Long
    Dim copyLen As Long

    With m_Connections(handle)
        If sock_ioctlsocket(.Socket, FIONREAD, available) <> 0 Then
            wsaErr = WSAGetLastError()
            SetError ERR_CONNECTION_LOST, "ioctlsocket(FIONREAD) failed: " & WSAErrDesc(wsaErr), "Connection lost.", handle, wsaErr
            .state = STATE_CLOSED
            If .AutoReconnect And .Mode = MODE_WEBSOCKET Then TryReconnect handle
            Exit Sub
        End If

        If available <= 0 Then Exit Sub
        If available > BUFFER_SIZE \ 2 Then available = BUFFER_SIZE \ 2

        ReDim tempBuf(0 To available - 1)
        received = sock_recv(.Socket, tempBuf(0), available, 0)

        If received > 0 Then
            .stats.BytesReceived = .stats.BytesReceived + received
            .LastActivityAt = GetTickCount()

            Select Case .Mode
                Case MODE_WEBSOCKET
                    If .TLS Then
                        copyLen = received
                        If .recvLen + copyLen > BUFFER_SIZE Then copyLen = BUFFER_SIZE - .recvLen
                        If copyLen > 0 Then
                            EnsureBufferCapacity .recvBuffer, .recvLen + copyLen
                            CopyMemory .recvBuffer(.recvLen), tempBuf(0), copyLen
                            .recvLen = .recvLen + copyLen
                        End If
                        TLSDecrypt handle
                    Else
                        copyLen = received
                        If .DecryptLen + copyLen > BUFFER_SIZE Then copyLen = BUFFER_SIZE - .DecryptLen
                        If copyLen > 0 Then
                            EnsureBufferCapacity .DecryptBuffer, .DecryptLen + copyLen
                            CopyMemory .DecryptBuffer(.DecryptLen), tempBuf(0), copyLen
                            .DecryptLen = .DecryptLen + copyLen
                        End If
                    End If
                    ProcessFrames handle

                Case MODE_TCP, MODE_TCP_TLS
                    If .TLS Then
                        copyLen = received
                        If .recvLen + copyLen > BUFFER_SIZE Then copyLen = BUFFER_SIZE - .recvLen
                        If copyLen > 0 Then
                            EnsureBufferCapacity .recvBuffer, .recvLen + copyLen
                            CopyMemory .recvBuffer(.recvLen), tempBuf(0), copyLen
                            .recvLen = .recvLen + copyLen
                        End If
                        TLSDecrypt handle
                        If .DecryptLen > 0 Then
                            EnsureBufferCapacity .TcpRecvBuffer, .TcpRecvLen + .DecryptLen
                            CopyMemory .TcpRecvBuffer(.TcpRecvLen), .DecryptBuffer(0), .DecryptLen
                            .TcpRecvLen = .TcpRecvLen + .DecryptLen
                            .DecryptLen = 0
                        End If
                    Else
                        copyLen = received
                        If .TcpRecvLen + copyLen > BUFFER_SIZE Then copyLen = BUFFER_SIZE - .TcpRecvLen
                        If copyLen > 0 Then
                            EnsureBufferCapacity .TcpRecvBuffer, .TcpRecvLen + copyLen
                            CopyMemory .TcpRecvBuffer(.TcpRecvLen), tempBuf(0), copyLen
                            .TcpRecvLen = .TcpRecvLen + copyLen
                        End If
                    End If
            End Select

        ElseIf received = 0 Then
            SetError ERR_CONNECTION_LOST, "recv() returned 0 - server closed connection", "Server closed the connection.", handle
            .state = STATE_CLOSED
            If .AutoReconnect And .Mode = MODE_WEBSOCKET Then TryReconnect handle
        Else
            wsaErr = WSAGetLastError()
            If wsaErr <> 10035 Then
                SetError ERR_RECV_FAILED, "recv() failed: " & WSAErrDesc(wsaErr), "Failed to receive data.", handle, wsaErr
                .state = STATE_CLOSED
                If .AutoReconnect And .Mode = MODE_WEBSOCKET Then TryReconnect handle
            End If
        End If
    End With
End Sub

Private Sub TickMaintenance(ByVal handle As Long)
    Dim now As Long
    With m_Connections(handle)
        If .state <> STATE_OPEN Then Exit Sub
        now = GetTickCount()
        If .Mode = MODE_WEBSOCKET Then
            If .PingIntervalMs > 0 Then
                If TickDiff(.LastPingSentAt, now) >= .CurrentPingIntervalMs Then
                    WebSocketSendPing "", handle
                    CalculateNextPing handle
                End If
            End If
        End If
        If .InactivityTimeoutMs > 0 And .LastActivityAt > 0 Then
            If TickDiff(.LastActivityAt, now) >= .InactivityTimeoutMs Then
                SetError ERR_INACTIVITY_TIMEOUT, "Inactivity timeout after " & .InactivityTimeoutMs & "ms", "Connection timed out.", handle
                .state = STATE_CLOSED
                If .AutoReconnect And .Mode = MODE_WEBSOCKET Then TryReconnect handle
                Exit Sub
            End If
        End If
        If .AutoMTU And .mtu.ProbeEnabled Then
            If TickDiff(.mtu.LastProbeTime, now) >= PMTU_DISCOVERY_INTERVAL_MS Then
                probeMTU handle
            End If
        End If
    End With
End Sub

Private Sub CloseSession(ByVal handle As Long)
    If Not ValidIndex(handle) Then Exit Sub
    
    If Not m_Connections(handle).CompressionHandler Is Nothing Then
        m_Connections(handle).CompressionHandler.OnDisconnect handle
    End If
    If Not m_Connections(handle).ProtocolHandler Is Nothing Then
        m_Connections(handle).ProtocolHandler.OnDisconnect handle
    End If
    
    Dim mwDisconnect As Object
    For Each mwDisconnect In m_Connections(handle).Middlewares
        mwDisconnect.OnDisconnect handle
    Next mwDisconnect
    
    With m_Connections(handle)
        If .Socket <> INVALID_SOCKET Then
            sock_closesocket .Socket
            .Socket = INVALID_SOCKET
        End If
        
        If .recvLen > 0 Or .DecryptLen > 0 Or .TcpRecvLen > 0 Or .FragmentLen > 0 Then
            WasabiMemZero VarPtr(.recvBuffer(0)), UBound(.recvBuffer) + 1
            WasabiMemZero VarPtr(.DecryptBuffer(0)), UBound(.DecryptBuffer) + 1
            WasabiMemZero VarPtr(.TcpRecvBuffer(0)), UBound(.TcpRecvBuffer) + 1
            WasabiMemZero VarPtr(.FragmentBuffer(0)), UBound(.FragmentBuffer) + 1
        End If
        
        .recvLen = 0
        .DecryptLen = 0
        .TcpRecvLen = 0
        .FragmentLen = 0
        .Fragmenting = False
        .MsgHead = 0
        .MsgTail = 0
        .MsgCount = 0
        .BinaryHead = 0
        .BinaryTail = 0
        .BinaryCount = 0
        .MqttParserStage = 0
        .MqttBufLen = 0
        .MqttInFlightCount = 0
        .state = STATE_CLOSED
    End With
    
    FreeSecurityHandles handle
End Sub

Private Sub TryReconnect(ByVal handle As Long)
    Dim delayMs As Long
    Dim attempt As Long
    Dim i As Long
    Dim startTick As Long
    
    If Not m_Connections(handle).AutoReconnect Then Exit Sub
    
    If m_Connections(handle).ReconnectMaxAttempts > 0 And m_Connections(handle).ReconnectAttempts >= m_Connections(handle).ReconnectMaxAttempts Then
        WasabiLog handle, "Auto-reconnect exhausted after " & m_Connections(handle).ReconnectAttempts & " attempts (handle=" & handle & ")"
        m_Connections(handle).AutoReconnect = False
        Exit Sub
    End If
    
    m_Connections(handle).ReconnectAttempts = m_Connections(handle).ReconnectAttempts + 1
    attempt = m_Connections(handle).ReconnectAttempts
    delayMs = m_Connections(handle).ReconnectBaseDelayMs
    
    For i = 1 To attempt - 1
        delayMs = delayMs * 2
        If delayMs > MAX_RECONNECT_DELAY_MS Then
            delayMs = MAX_RECONNECT_DELAY_MS
            Exit For
        End If
    Next i
    
    WasabiLog handle, "Reconnect attempt " & attempt & " in " & delayMs & "ms (handle=" & handle & ")"
    
    CloseSession handle
    
    startTick = GetTickCount()
    Do
        DoEvents
        If Not m_Connections(handle).AutoReconnect Then Exit Sub
        If TickDiff(startTick, GetTickCount()) >= delayMs Then Exit Do
    Loop
    
    If Not m_WSAInitialized Then
        Dim wsa As WSADATA
        WSAStartup &H202, wsa
        m_WSAInitialized = True
    End If
    
    If Not ConnectHandle(handle, m_Connections(handle).OriginalUrl) Then
        WasabiLog handle, "Reconnect attempt " & attempt & " failed (handle=" & handle & ")"
    Else
        m_Connections(handle).ReconnectAttempts = 0
        WasabiLog handle, "Reconnect succeeded (handle=" & handle & ")"
    End If
End Sub

Private Function TcpConnectInternal(ByVal handle As Long, ByVal HOST As String, ByVal port As Long, ByVal useTLS As Boolean) As Boolean
    Dim schannelCred As SCHANNEL_CRED
    Dim tsExpiry As SECURITY_INTEGER
    Dim zeroBytes() As Byte
    Dim acquireResult As Long
    Dim tlsResult As Long
    Dim connectHost As String
    Dim connectPort As Long

    With m_Connections(handle)
        .HOST = HOST
        .port = port
        .TLS = useTLS
        .state = STATE_CONNECTING
        .LastError = ERR_NONE
        .LastErrorCode = 0
        .TechnicalDetails = ""

        connectHost = IIf(.ProxyEnabled And .proxyHost <> "", .proxyHost, HOST)
        connectPort = IIf(.ProxyEnabled And .proxyPort > 0, .proxyPort, port)

        If Not ResolveAndConnect(handle, connectHost, connectPort) Then GoTo Fail
        InitializeMTU handle
        If .AutoMTU Then probeMTU handle
        ApplySocketOptions handle

        If .ProxyEnabled Then
            If .proxyType = PROXY_TYPE_SOCKS5 Then
                If Not DoProxySOCKS5(handle) Then GoTo Fail
            Else
                If Not DoProxyHTTP(handle) Then GoTo Fail
            End If
        End If

        If useTLS Then
            ReDim zeroBytes(0 To LenB(schannelCred) - 1)
            CopyMemory schannelCred, zeroBytes(0), LenB(schannelCred)
            schannelCred.dwVersion = SCHANNEL_CRED_VERSION
            schannelCred.grbitEnabledProtocols = SP_PROT_TLS1_2_CLIENT Or SP_PROT_TLS1_3_CLIENT
            schannelCred.dwFlags = SCH_CRED_NO_DEFAULT_CREDS Or SCH_CRED_MANUAL_CRED_VALIDATION Or SCH_CRED_IGNORE_NO_REVOCATION_CHECK Or SCH_CRED_IGNORE_REVOCATION_OFFLINE

            If .ClientCertThumb <> "" Or .ClientCertPfxPath <> "" Then
                If LoadClientCert(handle) Then
                    m_ClientCertContextPtrs(handle) = .pClientCertCtx
                    schannelCred.cCreds = 1
                    schannelCred.paCred = VarPtr(m_ClientCertContextPtrs(handle))
                End If
            End If

            acquireResult = AcquireCredentialsHandle(NULL_PTR, "Microsoft Unified Security Protocol Provider", SECPKG_CRED_OUTBOUND, NULL_PTR, schannelCred, NULL_PTR, NULL_PTR, .hCred, tsExpiry)
            If acquireResult <> 0 Then
                SetError ERR_TLS_ACQUIRE_CREDS_FAILED, "AcquireCredentialsHandle failed: 0x" & Hex(acquireResult), "TLS initialization failed.", handle, acquireResult
                GoTo Fail
            End If

            tlsResult = DoTLSHandshake(handle)
            If tlsResult <> 0 Then
                If tlsResult = -1 Then
                    SetError ERR_TLS_HANDSHAKE_TIMEOUT, "TLS handshake timed out with " & HOST, "TLS handshake timed out.", handle
                Else
                    SetError ERR_TLS_HANDSHAKE_FAILED, "TLS handshake failed: 0x" & Hex(tlsResult), "TLS handshake failed.", handle, tlsResult
                End If
                GoTo Fail
            End If

            QueryContextAttributes .hContext, SECPKG_ATTR_STREAM_SIZES, .sizes
            CalculateOptimalFrameSize handle

            If .ValidateServerCert Then
                If Not ValidateServerCert(handle) Then GoTo Fail
            End If
        End If
    End With

    TcpConnectInternal = True
    Exit Function
Fail:
    CleanupHandle handle
End Function

Private Function ConnectHandle(ByVal handle As Long, ByVal url As String) As Boolean
    Dim HOST As String
    Dim port As Long
    Dim path As String
    Dim useTLS As Boolean

    With m_Connections(handle)
        .OriginalUrl = url
        If Not ParseURL(url, HOST, port, path, useTLS) Then
            SetError ERR_INVALID_URL, "Invalid URL: " & url, "Invalid WebSocket URL. Use ws:// or wss://", handle
            Exit Function
        End If
        .path = path
        .Mode = MODE_WEBSOCKET
    End With

    If Not TcpConnectInternal(handle, HOST, port, useTLS) Then Exit Function

    If Not DoWebSocketHandshake(handle) Then
        CleanupHandle handle
        Exit Function
    End If

    With m_Connections(handle)
        .state = STATE_OPEN
        .stats.ConnectedAt = GetTickCount()
        .stats.BytesSent = 0
        .stats.BytesReceived = 0
        .stats.MessagesSent = 0
        .stats.MessagesReceived = 0
        .LastPingSentAt = GetTickCount()
        .LastActivityAt = GetTickCount()
        If .OfflineQueueEnabled Then FlushOfflineQueues handle
        
        If Not .CompressionHandler Is Nothing Then
            .CompressionHandler.OnConnect handle
        End If
        
        If Not .ProtocolHandler Is Nothing Then
            .ProtocolHandler.OnConnect handle
        End If
        
        Dim mwConnect As Object
        For Each mwConnect In .Middlewares
            mwConnect.OnConnect handle
        Next mwConnect
    End With

    ConnectHandle = True
End Function

Private Function SendFrameFor(ByVal handle As Long, ByRef frame() As Byte) As Boolean
    If m_Connections(handle).TLS Then
        SendFrameFor = TLSSend(handle, frame)
    Else
        SendFrameFor = RawSendFor(handle, frame)
    End If
End Function

Private Function MqttDecodeVarInt(ByRef buf() As Byte, ByRef index As Long) As Long
    Dim multiplier As Long
    Dim value As Long
    Dim encodedByte As Byte
    multiplier = 1
    value = 0
    Do
        If index > UBound(buf) Then Exit Do
        encodedByte = buf(index)
        index = index + 1
        value = value + (encodedByte And 127) * multiplier
        multiplier = multiplier * 128
        If multiplier > 2097152 Then Exit Do
    Loop While (encodedByte And 128) <> 0
    MqttDecodeVarInt = value
End Function

Private Function MqttEncodeRemainingLength(ByVal length As Long, ByRef buf() As Byte) As Long
    Dim encodedByte As Byte
    Dim idx As Long
    idx = 0
    Do
        encodedByte = CByte(length Mod 128)
        length = length \ 128
        If length > 0 Then
            encodedByte = encodedByte Or &H80
        End If
        buf(0 + idx) = encodedByte
        idx = idx + 1
    Loop While length > 0
    MqttEncodeRemainingLength = idx
End Function

Private Function BuildMqttConnectPacket(ByVal clientId As String, Optional ByVal username As String = "", Optional ByVal password As String = "", Optional ByVal keepAlive As Integer = 60, Optional ByVal sessionExpirySec As Long = 0) As Byte()
    Dim varHeader(0 To 9) As Byte
    Dim flags As Byte
    Dim clientBytes() As Byte
    Dim userBytes() As Byte
    Dim passBytes() As Byte
    Dim payload() As Byte
    Dim payloadLen As Long
    Dim remaining As Long
    Dim rlBuf(0 To 3) As Byte
    Dim rlLen As Long
    Dim packet() As Byte
    Dim pos As Long
    Dim cLen As Long
    Dim uLen As Long
    Dim pLen As Long
    Dim propBuf() As Byte
    Dim propLen As Long
    Dim propLenVar() As Byte
    Dim propLenVarSize As Long

    varHeader(0) = 0
    varHeader(1) = 4
    varHeader(2) = 77
    varHeader(3) = 81
    varHeader(4) = 84
    varHeader(5) = 84
    varHeader(6) = 5
    
    flags = 2
    If Len(username) > 0 Then flags = flags Or 128
    If Len(password) > 0 Then flags = flags Or 64
    varHeader(7) = flags
    
    varHeader(8) = CByte((keepAlive \ 256) And 255)
    varHeader(9) = CByte(keepAlive And 255)

    If sessionExpirySec > 0 Then
        ReDim propBuf(0 To 4)
        propBuf(0) = 17
        propBuf(1) = CByte((sessionExpirySec \ 16777216) And 255)
        propBuf(2) = CByte((sessionExpirySec \ 65536) And 255)
        propBuf(3) = CByte((sessionExpirySec \ 256) And 255)
        propBuf(4) = CByte(sessionExpirySec And 255)
        propLen = 5
    Else
        propLen = 0
    End If
    
    ReDim propLenVar(0 To 3)
    propLenVarSize = MqttEncodeRemainingLength(propLen, propLenVar)

    If Len(clientId) > 0 Then
        clientBytes = StringToUtf8(clientId)
        cLen = UBound(clientBytes) + 1
    End If
    
    If Len(username) > 0 Then
        userBytes = StringToUtf8(username)
        uLen = UBound(userBytes) + 1
    End If
    
    If Len(password) > 0 Then
        passBytes = StringToUtf8(password)
        pLen = UBound(passBytes) + 1
    End If

    payloadLen = 2 + cLen
    If uLen > 0 Then payloadLen = payloadLen + 2 + uLen
    If pLen > 0 Then payloadLen = payloadLen + 2 + pLen

    ReDim payload(0 To payloadLen - 1)
    pos = 0
    
    payload(pos) = CByte((cLen \ 256) And 255)
    payload(pos + 1) = CByte(cLen And 255)
    pos = pos + 2
    If cLen > 0 Then
        CopyMemory payload(pos), clientBytes(0), cLen
        pos = pos + cLen
    End If

    If uLen > 0 Then
        payload(pos) = CByte((uLen \ 256) And 255)
        payload(pos + 1) = CByte(uLen And 255)
        pos = pos + 2
        CopyMemory payload(pos), userBytes(0), uLen
        pos = pos + uLen
    End If

    If pLen > 0 Then
        payload(pos) = CByte((pLen \ 256) And 255)
        payload(pos + 1) = CByte(pLen And 255)
        pos = pos + 2
        CopyMemory payload(pos), passBytes(0), pLen
    End If

    remaining = 10 + propLenVarSize + propLen + payloadLen
    rlLen = MqttEncodeRemainingLength(remaining, rlBuf)
    
    ReDim packet(0 To rlLen + remaining)
    packet(0) = 16
    
    CopyMemory packet(1), rlBuf(0), rlLen
    CopyMemory packet(1 + rlLen), varHeader(0), 10
    CopyMemory packet(1 + rlLen + 10), propLenVar(0), propLenVarSize
    
    pos = 1 + rlLen + 10 + propLenVarSize
    If propLen > 0 Then
        CopyMemory packet(pos), propBuf(0), propLen
        pos = pos + propLen
    End If
    
    CopyMemory packet(pos), payload(0), payloadLen
    
    BuildMqttConnectPacket = packet
End Function

Private Function MqttBuildPacket(ByVal ptype As Byte, ByVal flags As Byte, ByRef payload() As Byte, ByVal payloadLen As Long) As Byte()
    Dim remaining As Long
    Dim rlBuf(0 To 3) As Byte
    Dim rlLen As Long
    Dim packet() As Byte
    
    remaining = payloadLen
    rlLen = MqttEncodeRemainingLength(remaining, rlBuf)
    
    ReDim packet(0 To rlLen + remaining)
    
    packet(0) = ptype * 16 Or flags
    CopyMemory packet(1), rlBuf(0), rlLen
    
    If payloadLen > 0 Then
        CopyMemory packet(1 + rlLen), payload(0), payloadLen
    End If
    
    MqttBuildPacket = packet
End Function

Private Sub MqttSendAck(ByVal handle As Long, ByVal packetType As Byte, ByVal flags As Byte, ByVal packetId As Integer)
    Dim packet(0 To 3) As Byte
    packet(0) = (packetType * 16) Or flags
    packet(1) = 2
    packet(2) = CByte((packetId \ 256) And &HFF)
    packet(3) = CByte(packetId And &HFF)
    WebSocketSendBinary packet, handle
End Sub

Private Sub MqttParseByte(ByVal handle As Long, ByVal b As Byte)
    With m_Connections(handle)
        Select Case .MqttParserStage
            Case 0
                .MqttCurrentPacketType = b \ 16
                .MqttCurrentFlags = b And &HF
                .MqttParserStage = 1
                .MqttExpectedRemaining = 0
                .MqttBufLen = 0
            Case 1
                .MqttExpectedRemaining = .MqttExpectedRemaining + (b And &H7F) * (128 ^ .MqttBufLen)
                .MqttBufLen = .MqttBufLen + 1
                If (b And &H80) = 0 Then
                    .MqttParserStage = 2
                    .MqttBufLen = 0
                    If .MqttExpectedRemaining > 0 Then
                        EnsureBufferCapacity .MqttBuffer, .MqttExpectedRemaining
                    Else
                        .MqttParserStage = 3
                    End If
                End If
            Case 2
                .MqttBuffer(.MqttBufLen) = b
                .MqttBufLen = .MqttBufLen + 1
                If .MqttBufLen >= .MqttExpectedRemaining Then
                    .MqttParserStage = 3
                End If
        End Select
    End With
End Sub

Private Function MqttHasPacket(ByVal handle As Long) As Boolean
    MqttHasPacket = (m_Connections(handle).MqttParserStage = 3)
End Function

Private Sub MqttResetParser(ByVal handle As Long)
    m_Connections(handle).MqttParserStage = 0
    m_Connections(handle).MqttBufLen = 0
End Sub

Private Sub FlushOfflineQueues(ByVal handle As Long)
    Dim i As Long
    Dim tCount As Long, bCount As Long
    Dim tQueue() As String, bQueue() As BinaryMessage
    
    With m_Connections(handle)
        If Not .OfflineQueueEnabled Then Exit Sub
        tCount = .OfflineTextCount
        bCount = .OfflineBinaryCount
        
        If tCount > 0 Then
            ReDim tQueue(0 To tCount - 1)
            For i = 0 To tCount - 1: tQueue(i) = .OfflineTextQueue(i): Next i
            .OfflineTextCount = 0
        End If
        
        If bCount > 0 Then
            ReDim bQueue(0 To bCount - 1)
            For i = 0 To bCount - 1: bQueue(i) = .OfflineBinaryQueue(i): Next i
            .OfflineBinaryCount = 0
        End If
    End With
    
    If tCount > 0 Then
        For i = 0 To tCount - 1
            WebSocketSend tQueue(i), handle
        Next i
    End If
    If bCount > 0 Then
        For i = 0 To bCount - 1
            WebSocketSendBinary bQueue(i).data, handle
        Next i
    End If
End Sub

Public Function TcpConnect(ByVal HOST As String, ByVal port As Long, ByRef outHandle As Long) As Boolean
    Dim wsa As WSADATA
    Dim wsaErr As Long
    Dim handle As Long

    m_LastError = ERR_NONE
    m_LastErrorCode = 0
    m_TechnicalDetails = ""
    InitConnectionPool

    If Not m_WSAInitialized Then
        wsaErr = WSAStartup(&H202, wsa)
        If wsaErr <> 0 Then
            SetError ERR_WSA_STARTUP_FAILED, "WSAStartup failed: " & wsaErr, "Network initialization failed.", INVALID_CONN_HANDLE, wsaErr
            outHandle = INVALID_CONN_HANDLE
            Exit Function
        End If
        m_WSAInitialized = True
    End If

    handle = AllocConnection()
    If handle = INVALID_CONN_HANDLE Then
        SetError ERR_MAX_CONNECTIONS, "Max connections reached", "Too many simultaneous connections.", INVALID_CONN_HANDLE
        outHandle = INVALID_CONN_HANDLE
        Exit Function
    End If

    m_Connections(handle).Mode = MODE_TCP

    If Not TcpConnectInternal(handle, HOST, port, False) Then
        outHandle = INVALID_CONN_HANDLE
        Exit Function
    End If

    With m_Connections(handle)
        .state = STATE_OPEN
        .stats.ConnectedAt = GetTickCount()
        .stats.BytesSent = 0
        .stats.BytesReceived = 0
        .stats.MessagesSent = 0
        .stats.MessagesReceived = 0
        .LastActivityAt = GetTickCount()
    End With

    m_DefaultHandle = handle
    outHandle = handle
    TcpConnect = True
    WasabiLog handle, "TCP connected to " & HOST & ":" & port & " (handle=" & handle & ")"
End Function

Public Function TcpConnectTLS(ByVal HOST As String, ByVal port As Long, ByRef outHandle As Long) As Boolean
    Dim wsa As WSADATA
    Dim wsaErr As Long
    Dim handle As Long

    m_LastError = ERR_NONE
    m_LastErrorCode = 0
    m_TechnicalDetails = ""
    InitConnectionPool

    If Not m_WSAInitialized Then
        wsaErr = WSAStartup(&H202, wsa)
        If wsaErr <> 0 Then
            SetError ERR_WSA_STARTUP_FAILED, "WSAStartup failed: " & wsaErr, "Network initialization failed.", INVALID_CONN_HANDLE, wsaErr
            outHandle = INVALID_CONN_HANDLE
            Exit Function
        End If
        m_WSAInitialized = True
    End If

    handle = AllocConnection()
    If handle = INVALID_CONN_HANDLE Then
        SetError ERR_MAX_CONNECTIONS, "Max connections reached", "Too many simultaneous connections.", INVALID_CONN_HANDLE
        outHandle = INVALID_CONN_HANDLE
        Exit Function
    End If

    m_Connections(handle).Mode = MODE_TCP_TLS

    If Not TcpConnectInternal(handle, HOST, port, True) Then
        outHandle = INVALID_CONN_HANDLE
        Exit Function
    End If

    With m_Connections(handle)
        .state = STATE_OPEN
        .stats.ConnectedAt = GetTickCount()
        .stats.BytesSent = 0
        .stats.BytesReceived = 0
        .stats.MessagesSent = 0
        .stats.MessagesReceived = 0
        .LastActivityAt = GetTickCount()
    End With

    m_DefaultHandle = handle
    outHandle = handle
    TcpConnectTLS = True
    WasabiLog handle, "TCP+TLS connected to " & HOST & ":" & port & " (handle=" & handle & ")"
End Function

Public Function TcpSend(ByRef data() As Byte, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim dataLen As Long

    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function

    With m_Connections(h)
        If .state <> STATE_OPEN Then
            SetError ERR_NOT_CONNECTED, "TcpSend on disconnected handle=" & h, "Not connected.", h
            Exit Function
        End If
        If .Mode = MODE_WEBSOCKET Then
            SetError ERR_NOT_CONNECTED, "TcpSend called on WebSocket handle=" & h, "Use WebSocketSend for WebSocket connections.", h
            Exit Function
        End If

        RunOutboundMiddlewares h, data
        dataLen = SafeArrayLen(data)
        If dataLen = 0 Then
            TcpSend = True
            Exit Function
        End If

        If .TLS Then
            TcpSend = TLSSend(h, data)
        Else
            TcpSend = RawSendFor(h, data)
        End If

        If TcpSend Then
            .stats.BytesSent = .stats.BytesSent + dataLen
            .stats.MessagesSent = .stats.MessagesSent + 1
        End If
    End With
End Function

Public Function TcpSendText(ByVal text As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim data() As Byte

    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function

    data = StringToUtf8(text)
    TcpSendText = TcpSend(data, h)
End Function

Public Function TcpReceive(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Byte()
    Dim h As Long
    Dim result() As Byte

    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then
        TcpReceive = result
        Exit Function
    End If

    With m_Connections(h)
        If .state <> STATE_OPEN Then
            TcpReceive = result
            Exit Function
        End If
        If .Mode = MODE_WEBSOCKET Then
            TcpReceive = result
            Exit Function
        End If

        TickMaintenance h
        FeedBuffer h
        
        

        If .TcpRecvLen > 0 Then
            ReDim result(0 To .TcpRecvLen - 1)
            CopyMemory result(0), .TcpRecvBuffer(0), .TcpRecvLen
            .TcpRecvLen = 0
            .stats.MessagesReceived = .stats.MessagesReceived + 1
            
            RunInboundMiddlewares h, result
            
            TcpReceive = result
        Else
            TcpReceive = result
        End If
    End With
End Function

Public Function TcpReceiveText(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    Dim data() As Byte
    Dim dataLen As Long

    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function

    data = TcpReceive(h)
    dataLen = SafeArrayLen(data)
    If dataLen > 0 Then
        TcpReceiveText = Utf8ToString(data, dataLen)
    End If
End Function

Public Function TcpReceiveUntil(ByVal delimiter As String, Optional ByVal timeoutMs As Long = 5000, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    Dim accumulated() As Byte
    Dim accLen As Long
    Dim delimBytes() As Byte
    Dim delimLen As Long
    Dim startTick As Long
    Dim foundIndex As Long
    Dim resultBytes() As Byte
    Dim remaining As Long

    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function

    delimBytes = StringToUtf8(delimiter)
    delimLen = SafeArrayLen(delimBytes)
    ReDim accumulated(0 To BUFFER_SIZE - 1)
    accLen = 0
    startTick = GetTickCount()

    Do
        FeedBuffer h

        With m_Connections(h)
            If .TcpRecvLen > 0 Then
                EnsureBufferCapacity accumulated, accLen + .TcpRecvLen
                CopyMemory accumulated(accLen), .TcpRecvBuffer(0), .TcpRecvLen
                accLen = accLen + .TcpRecvLen
                .TcpRecvLen = 0
            End If
        End With

        If accLen >= delimLen Then
            foundIndex = WasabiMemFind(VarPtr(accumulated(0)), accLen, VarPtr(delimBytes(0)), delimLen)
            
            If foundIndex >= 0 Then
                ReDim resultBytes(0 To foundIndex + delimLen - 1)
                CopyMemory resultBytes(0), accumulated(0), foundIndex + delimLen
                TcpReceiveUntil = Utf8ToString(resultBytes, foundIndex + delimLen)
                
                remaining = accLen - (foundIndex + delimLen)
                If remaining > 0 Then
                    CopyMemory m_Connections(h).TcpRecvBuffer(0), accumulated(foundIndex + delimLen), remaining
                    m_Connections(h).TcpRecvLen = remaining
                End If
                Exit Function
            End If
        End If

        If TickDiff(startTick, GetTickCount()) >= timeoutMs Then Exit Do
        DoEvents
    Loop

    If accLen > 0 Then
        TcpReceiveUntil = Utf8ToString(accumulated, accLen)
    End If
End Function

Public Function TcpIsConnected(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    TcpIsConnected = (m_Connections(h).state = STATE_OPEN And m_Connections(h).Mode <> MODE_WEBSOCKET)
End Function

Public Function TcpGetPendingBytes(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    TcpGetPendingBytes = m_Connections(h).TcpRecvLen
End Function

Public Sub TcpFlushBuffer(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).TcpRecvLen = 0
End Sub

Public Sub TcpDisconnect(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    m_Connections(h).AutoReconnect = False
    CleanupHandle h
    WasabiLog h, "TCP disconnected (handle=" & h & ")"
End Sub

Private Sub RunOutboundMiddlewares(ByVal handle As Long, ByRef data() As Byte)
    Dim mw As Object
    For Each mw In m_Connections(handle).Middlewares
        mw.OnBeforeSend handle, data
    Next mw
End Sub

Private Sub RunInboundMiddlewares(ByVal handle As Long, ByRef data() As Byte)
    Dim mw As Object
    For Each mw In m_Connections(handle).Middlewares
        mw.OnAfterReceive handle, data
    Next mw
End Sub

Public Sub WasabiUseProtocol(ByVal extension As Object, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    
    Set m_Connections(h).ProtocolHandler = extension
    
    If Not extension Is Nothing Then
        If m_Connections(h).state = STATE_OPEN Then
            extension.OnConnect h
        End If
    End If
End Sub

Public Sub WasabiUseCompression(ByVal extension As Object, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    
    Set m_Connections(h).CompressionHandler = extension
    
    If Not extension Is Nothing Then
        If m_Connections(h).state = STATE_OPEN Then
            extension.OnConnect h
        End If
    End If
End Sub

Public Sub WasabiUseMiddleware(ByVal extension As Object, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    
    If m_Connections(h).Middlewares Is Nothing Then
        Set m_Connections(h).Middlewares = New Collection
    End If
    m_Connections(h).Middlewares.Add extension
    
    If Not extension Is Nothing Then
        If m_Connections(h).state = STATE_OPEN Then
            extension.OnConnect h
        End If
    End If
End Sub

Public Function WebSocketConnect(ByVal url As String, Optional ByRef outHandle As Long = -1, Optional ByVal DeflateEnabled As Boolean = False, Optional ByVal DeflateContextTakeover As Boolean = True, Optional ByVal SubProtocol As String = "") As Boolean
    Dim wsa As WSADATA
    Dim wsaErr As Long
    Dim handle As Long
    m_LastError = ERR_NONE
    m_LastErrorCode = 0
    m_TechnicalDetails = ""
    InitConnectionPool
    If Not m_WSAInitialized Then
        wsaErr = WSAStartup(&H202, wsa)
        If wsaErr <> 0 Then
            SetError ERR_WSA_STARTUP_FAILED, "WSAStartup failed: " & wsaErr, "Network initialization failed. Code: " & wsaErr, INVALID_CONN_HANDLE, wsaErr
            outHandle = INVALID_CONN_HANDLE
            Exit Function
        End If
        m_WSAInitialized = True
    End If
    handle = AllocConnection()
    If handle = INVALID_CONN_HANDLE Then
        SetError ERR_MAX_CONNECTIONS, "Max connections (" & MAX_CONNECTIONS & ") reached", "Too many simultaneous connections.", INVALID_CONN_HANDLE
        outHandle = INVALID_CONN_HANDLE
        Exit Function
    End If
    m_Connections(handle).DeflateEnabled = DeflateEnabled
    m_Connections(handle).DeflateContextTakeover = DeflateContextTakeover
    m_Connections(handle).InflateContextTakeover = DeflateContextTakeover
    
    m_Connections(handle).SubProtocol = SubProtocol
    
    If Not ConnectHandle(handle, url) Then
        outHandle = INVALID_CONN_HANDLE
        Exit Function
    End If
    m_DefaultHandle = handle
    outHandle = handle
    WebSocketConnect = True
    WasabiLog handle, "Connected to " & url & " (handle=" & handle & ")"
End Function

Public Sub WebSocketSetOfflineQueueing(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).OfflineQueueEnabled = enabled
End Sub

Public Sub WebSocketSetDeflate(ByVal enabled As Boolean, Optional ByVal contextTakeover As Boolean = True, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    With m_Connections(h)
        If .state = STATE_OPEN Then
            .DeflateEnabled = enabled
            .DeflateContextTakeover = contextTakeover
            .InflateContextTakeover = contextTakeover
            WasabiLog h, "DeflateEnabled set to " & enabled & " - will apply on next reconnect (handle=" & h & ")"
            Exit Sub
        End If
        .DeflateEnabled = enabled
        .DeflateContextTakeover = contextTakeover
        .InflateContextTakeover = contextTakeover
    End With
End Sub

Public Function WebSocketGetDeflateEnabled(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetDeflateEnabled = m_Connections(h).DeflateActive
End Function

Public Sub WebSocketDisconnect(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    Dim i As Long
    Dim anyActive As Boolean

    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode <> MODE_WEBSOCKET Then Exit Sub

    m_Connections(h).AutoReconnect = False
    If m_Connections(h).state = STATE_OPEN Then WebSocketSendClose 1000, "", h
    CleanupHandle h

    If h = m_DefaultHandle Then
        m_DefaultHandle = 0
        For i = 0 To MAX_CONNECTIONS - 1
            If m_Connections(i).state = STATE_OPEN Then
                m_DefaultHandle = i
                Exit For
            End If
        Next i
    End If

    anyActive = False
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state <> STATE_CLOSED Or m_Connections(i).Socket <> INVALID_SOCKET Then
            anyActive = True
            Exit For
        End If
    Next i

    If Not anyActive And m_WSAInitialized Then
        WSACleanup
        ShutdownWasabiThunks
        m_WSAInitialized = False
    End If
End Sub

Public Sub WebSocketDisconnectAll()
    Dim i As Long
    InitConnectionPool
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state <> STATE_CLOSED Or m_Connections(i).Socket <> INVALID_SOCKET Then
            Select Case m_Connections(i).Mode
                Case MODE_WEBSOCKET
                    WebSocketDisconnect i
                Case MODE_TCP, MODE_TCP_TLS
                    TcpDisconnect i
            End Select
        End If
    Next i
End Sub

Public Function WebSocketSend(ByVal message As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim msgBytes() As Byte
    Dim msgLen As Long
    Dim frame() As Byte
    Dim useDeflate As Boolean
    Dim compLen As Long
    Dim compBytes() As Byte
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then
            If .OfflineQueueEnabled Then
                If .OfflineTextCount > UBound(.OfflineTextQueue) Then
                    ReDim Preserve .OfflineTextQueue(0 To UBound(.OfflineTextQueue) + 64)
                End If
                .OfflineTextQueue(.OfflineTextCount) = message
                .OfflineTextCount = .OfflineTextCount + 1
                WebSocketSend = True
                Exit Function
            Else
                SetError ERR_NOT_CONNECTED, "Send on disconnected handle=" & h, "Not connected to WebSocket server.", h
                Exit Function
            End If
        End If
        msgBytes = StringToUtf8(message)
        RunOutboundMiddlewares h, msgBytes
        msgLen = SafeArrayLen(msgBytes)
        If msgLen = 0 Then
            WebSocketSend = True
            Exit Function
        End If
        useDeflate = .DeflateActive
        If useDeflate Then
            compBytes = DeflatePayload(h, msgBytes, msgLen, compLen)
            msgBytes = compBytes
            msgLen = compLen
        End If
        frame = BuildWSFrame(msgBytes, msgLen, WS_OPCODE_TEXT, True, useDeflate)
        If SendFrameFor(h, frame) Then
            .stats.BytesSent = .stats.BytesSent + (UBound(frame) + 1)
            .stats.MessagesSent = .stats.MessagesSent + 1
            WebSocketSend = True
        End If
    End With
End Function

Public Function WebSocketSendBinary(ByRef data() As Byte, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim dataLen As Long
    Dim frame() As Byte
    Dim useDeflate As Boolean
    Dim compLen As Long
    Dim compBytes() As Byte
    Dim sendData() As Byte
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then
            If .OfflineQueueEnabled Then
                If .OfflineBinaryCount > UBound(.OfflineBinaryQueue) Then
                    ReDim Preserve .OfflineBinaryQueue(0 To UBound(.OfflineBinaryQueue) + 64)
                End If
                .OfflineBinaryQueue(.OfflineBinaryCount).data = data
                .OfflineBinaryCount = .OfflineBinaryCount + 1
                WebSocketSendBinary = True
                Exit Function
            Else
                SetError ERR_NOT_CONNECTED, "SendBinary on disconnected handle=" & h, "Not connected to WebSocket server.", h
                Exit Function
            End If
        End If
        RunOutboundMiddlewares h, data
        dataLen = SafeArrayLen(data)
        If dataLen = 0 Then
            WebSocketSendBinary = True
            Exit Function
        End If
        useDeflate = .DeflateActive
        If useDeflate Then
            compBytes = DeflatePayload(h, data, dataLen, compLen)
            sendData = compBytes
            dataLen = compLen
        Else
            sendData = data
        End If
        frame = BuildWSFrame(sendData, dataLen, WS_OPCODE_BINARY, True, useDeflate)
        If SendFrameFor(h, frame) Then
            .stats.BytesSent = .stats.BytesSent + (UBound(frame) + 1)
            .stats.MessagesSent = .stats.MessagesSent + 1
            WebSocketSendBinary = True
        End If
    End With
End Function

Public Function WebSocketSendMTUAware(ByVal message As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim msgBytes() As Byte
    Dim msgLen As Long
    Dim offset As Long
    Dim chunkSize As Long
    Dim opcode As Byte
    Dim isLast As Boolean
    Dim chunkBytes() As Byte
    Dim frame() As Byte
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then
            SetError ERR_NOT_CONNECTED, "SendMTUAware on disconnected handle=" & h, "Not connected.", h
            Exit Function
        End If
        msgBytes = StringToUtf8(message)
        msgLen = SafeArrayLen(msgBytes)
        If msgLen = 0 Then
            WebSocketSendMTUAware = True
            Exit Function
        End If
        If Not .AutoMTU Or msgLen <= .mtu.OptimalFrameSize Then
            WebSocketSendMTUAware = WebSocketSend(message, h)
            Exit Function
        End If
        offset = 0
        opcode = WS_OPCODE_TEXT
        Do While offset < msgLen
            chunkSize = .mtu.OptimalFrameSize
            If offset + chunkSize > msgLen Then chunkSize = msgLen - offset
            isLast = (offset + chunkSize >= msgLen)
            ReDim chunkBytes(0 To chunkSize - 1)
            CopyMemory chunkBytes(0), msgBytes(offset), chunkSize
            frame = BuildWSFrame(chunkBytes, chunkSize, opcode, isLast)
            If Not SendFrameFor(h, frame) Then Exit Function
            .stats.BytesSent = .stats.BytesSent + (UBound(frame) + 1)
            offset = offset + chunkSize
            opcode = WS_OPCODE_CONTINUATION
        Loop
        .stats.MessagesSent = .stats.MessagesSent + 1
    End With
    WebSocketSendMTUAware = True
End Function

Public Function WebSocketSendBinaryMTUAware(ByRef data() As Byte, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim dataLen As Long
    Dim offset As Long
    Dim chunkSize As Long
    Dim opcode As Byte
    Dim isLast As Boolean
    Dim chunkBytes() As Byte
    Dim frame() As Byte
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then
            SetError ERR_NOT_CONNECTED, "SendBinaryMTUAware on disconnected handle=" & h, "Not connected.", h
            Exit Function
        End If
        dataLen = SafeArrayLen(data)
        If dataLen = 0 Then
            WebSocketSendBinaryMTUAware = True
            Exit Function
        End If
        If Not .AutoMTU Or dataLen <= .mtu.OptimalFrameSize Then
            WebSocketSendBinaryMTUAware = WebSocketSendBinary(data, h)
            Exit Function
        End If
        offset = 0
        opcode = WS_OPCODE_BINARY
        Do While offset < dataLen
            chunkSize = .mtu.OptimalFrameSize
            If offset + chunkSize > dataLen Then chunkSize = dataLen - offset
            isLast = (offset + chunkSize >= dataLen)
            ReDim chunkBytes(0 To chunkSize - 1)
            CopyMemory chunkBytes(0), data(offset), chunkSize
            frame = BuildWSFrame(chunkBytes, chunkSize, opcode, isLast)
            If Not SendFrameFor(h, frame) Then Exit Function
            .stats.BytesSent = .stats.BytesSent + (UBound(frame) + 1)
            offset = offset + chunkSize
            opcode = WS_OPCODE_CONTINUATION
        Loop
        .stats.MessagesSent = .stats.MessagesSent + 1
    End With
    WebSocketSendBinaryMTUAware = True
End Function

Public Function WebSocketSendBatch(ByRef messages() As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim i As Long
    Dim msgBytes() As Byte
    Dim msgLen As Long
    Dim frame() As Byte
    Dim frameSize As Long
    Dim batchBuf() As Byte
    Dim batchLen As Long
    Dim batchCount As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then Exit Function
        batchLen = 0
        batchCount = 0
        ReDim batchBuf(0 To 65535)
        For i = LBound(messages) To UBound(messages)
            msgBytes = StringToUtf8(messages(i))
            msgLen = SafeArrayLen(msgBytes)
            If msgLen = 0 Then GoTo NextMsg
            frame = BuildWSFrame(msgBytes, msgLen, WS_OPCODE_TEXT, True)
            frameSize = UBound(frame) + 1
            If batchLen + frameSize > 65536 Then
                Dim flushBuf() As Byte
                ReDim flushBuf(0 To batchLen - 1)
                CopyMemory flushBuf(0), batchBuf(0), batchLen
                If .TLS Then
                    If Not TLSSend(h, flushBuf) Then Exit Function
                Else
                    If Not RawSendFor(h, flushBuf) Then Exit Function
                End If
                .stats.BytesSent = .stats.BytesSent + batchLen
                .stats.MessagesSent = .stats.MessagesSent + batchCount
                batchLen = 0
                batchCount = 0
            End If
            CopyMemory batchBuf(batchLen), frame(0), frameSize
            batchLen = batchLen + frameSize
            batchCount = batchCount + 1
NextMsg:
        Next i
        If batchLen > 0 Then
            ReDim flushBuf(0 To batchLen - 1)
            CopyMemory flushBuf(0), batchBuf(0), batchLen
            If .TLS Then
                If Not TLSSend(h, flushBuf) Then Exit Function
            Else
                If Not RawSendFor(h, flushBuf) Then Exit Function
            End If
            .stats.BytesSent = .stats.BytesSent + batchLen
            .stats.MessagesSent = .stats.MessagesSent + batchCount
        End If
    End With
    WebSocketSendBatch = True
End Function

Public Function WebSocketSendBatchBinary(ByRef messages() As Variant, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim i As Long
    Dim bdata() As Byte
    Dim dataLen As Long
    Dim frame() As Byte
    Dim frameSize As Long
    Dim batchBuf() As Byte
    Dim batchLen As Long
    Dim batchCount As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then Exit Function
        batchLen = 0
        batchCount = 0
        ReDim batchBuf(0 To 65535)
        For i = LBound(messages) To UBound(messages)
            If IsArray(messages(i)) Then
                bdata = messages(i)
                dataLen = SafeArrayLen(bdata)
                If dataLen = 0 Then GoTo NextMsgBin
                frame = BuildWSFrame(bdata, dataLen, WS_OPCODE_BINARY, True)
                frameSize = UBound(frame) + 1
                If batchLen + frameSize > 65536 Then
                    Dim flushBuf() As Byte
                    ReDim flushBuf(0 To batchLen - 1)
                    CopyMemory flushBuf(0), batchBuf(0), batchLen
                    If .TLS Then
                        If Not TLSSend(h, flushBuf) Then Exit Function
                    Else
                        If Not RawSendFor(h, flushBuf) Then Exit Function
                    End If
                    .stats.BytesSent = .stats.BytesSent + batchLen
                    .stats.MessagesSent = .stats.MessagesSent + batchCount
                    batchLen = 0
                    batchCount = 0
                End If
                CopyMemory batchBuf(batchLen), frame(0), frameSize
                batchLen = batchLen + frameSize
                batchCount = batchCount + 1
            End If
NextMsgBin:
        Next i
        If batchLen > 0 Then
            ReDim flushBuf(0 To batchLen - 1)
            CopyMemory flushBuf(0), batchBuf(0), batchLen
            If .TLS Then
                If Not TLSSend(h, flushBuf) Then Exit Function
            Else
                If Not RawSendFor(h, flushBuf) Then Exit Function
            End If
            .stats.BytesSent = .stats.BytesSent + batchLen
            .stats.MessagesSent = .stats.MessagesSent + batchCount
        End If
    End With
    WebSocketSendBatchBinary = True
End Function

Public Function WebSocketSendClose(Optional ByVal code As Integer = 1000, Optional ByVal reason As String = "", Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim frame() As Byte
    Dim mask(0 To 3) As Byte
    Dim reasonBytes() As Byte
    Dim reasonLen As Long
    Dim payloadLen As Long
    Dim i As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then Exit Function
        .CloseInitiatedByUs = True
        .closeCode = code
        .closeReason = reason
        If Len(reason) > 0 Then
            reasonBytes = StringToUtf8(reason)
            reasonLen = SafeArrayLen(reasonBytes)
            If reasonLen > 123 Then reasonLen = 123
        End If
        payloadLen = 2 + reasonLen
        ReDim frame(0 To 5 + payloadLen)
        FillRandomBytes mask, 4
        frame(0) = &H88
        frame(1) = &H80 Or CByte(payloadLen)
        frame(2) = mask(0)
        frame(3) = mask(1)
        frame(4) = mask(2)
        frame(5) = mask(3)
        frame(6) = CByte((code \ 256) And &HFF) Xor mask(0)
        frame(7) = CByte(code And &HFF) Xor mask(1)
        For i = 0 To reasonLen - 1
            frame(8 + i) = reasonBytes(i) Xor mask((i + 2) Mod 4)
        Next i
        WasabiLog h, "Sending CLOSE: " & code & " (" & GetCloseCodeDesc(code) & ") reason=""" & reason & """ (handle=" & h & ")"
        WebSocketSendClose = SendFrameFor(h, frame)
        .state = STATE_CLOSING
    End With
End Function

Public Function WebSocketSendPing(Optional ByVal payload As String = "", Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim frame() As Byte
    Dim mask(0 To 3) As Byte
    Dim pingBytes() As Byte
    Dim pingLen As Long
    Dim i As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then Exit Function
        If Len(payload) > 0 Then
            pingBytes = StringToUtf8(payload)
            pingLen = SafeArrayLen(pingBytes)
        End If
        FillRandomBytes mask, 4
        If pingLen = 0 Then
            ReDim frame(0 To 5)
            frame(0) = &H89
            frame(1) = &H80
        Else
            ReDim frame(0 To 5 + pingLen)
            frame(0) = &H89
            frame(1) = &H80 Or CByte(pingLen)
            For i = 0 To pingLen - 1
                frame(6 + i) = pingBytes(i) Xor mask(i Mod 4)
            Next i
        End If
        frame(2) = mask(0)
        frame(3) = mask(1)
        frame(4) = mask(2)
        frame(5) = mask(3)
        WebSocketSendPing = SendFrameFor(h, frame)
        If WebSocketSendPing Then
            .LastPingSentAt = GetTickCount()
            .LastPingTimestamp = GetTickCount()
        End If
    End With
End Function

Public Function WebSocketSendPong(Optional ByVal payload As String = "", Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim frame() As Byte
    Dim mask(0 To 3) As Byte
    Dim pongBytes() As Byte
    Dim pongLen As Long
    Dim i As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then Exit Function
        If Len(payload) > 0 Then
            pongBytes = StringToUtf8(payload)
            pongLen = SafeArrayLen(pongBytes)
        End If
        FillRandomBytes mask, 4
        If pongLen = 0 Then
            ReDim frame(0 To 5)
            frame(0) = &H8A
            frame(1) = &H80
        Else
            ReDim frame(0 To 5 + pongLen)
            frame(0) = &H8A
            frame(1) = &H80 Or CByte(pongLen)
            For i = 0 To pongLen - 1
                frame(6 + i) = pongBytes(i) Xor mask(i Mod 4)
            Next i
        End If
        frame(2) = mask(0)
        frame(3) = mask(1)
        frame(4) = mask(2)
        frame(5) = mask(3)
        WebSocketSendPong = SendFrameFor(h, frame)
    End With
End Function

Public Function WebSocketBroadcast(ByVal message As String) As Long
    Dim i As Long
    Dim count As Long
    InitConnectionPool
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state = STATE_OPEN Then
            If WebSocketSend(message, i) Then count = count + 1
        End If
    Next i
    WebSocketBroadcast = count
End Function

Public Function WebSocketBroadcastBinary(ByRef data() As Byte) As Long
    Dim i As Long
    Dim count As Long
    InitConnectionPool
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state = STATE_OPEN Then
            If WebSocketSendBinary(data, i) Then count = count + 1
        End If
    Next i
    WebSocketBroadcastBinary = count
End Function

Public Function WebSocketReceive(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then
            If .AutoReconnect Then TryReconnect h
            Exit Function
        End If
        TickMaintenance h
        If .DecryptLen > 0 Then ProcessFrames h
        FeedBuffer h
        If .MsgCount > 0 Then
            WebSocketReceive = .MsgQueue(.MsgHead)
            .MsgQueue(.MsgHead) = ""
            .MsgHead = (.MsgHead + 1) Mod MSG_QUEUE_SIZE
            .MsgCount = .MsgCount - 1
        End If
    End With
End Function

Public Function WebSocketReceiveAll(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String()
    Dim h As Long
    Dim results() As String
    Dim count As Long
    Dim i As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then
        ReDim results(0)
        WebSocketReceiveAll = results
        Exit Function
    End If
    With m_Connections(h)
        If .state <> STATE_OPEN Then
            If .AutoReconnect Then TryReconnect h
            ReDim results(0)
            WebSocketReceiveAll = results
            Exit Function
        End If
        TickMaintenance h
        If .DecryptLen > 0 Then ProcessFrames h
        FeedBuffer h
        count = .MsgCount
        If count = 0 Then
            ReDim results(0)
            WebSocketReceiveAll = results
            Exit Function
        End If
        ReDim results(0 To count - 1)
        For i = 0 To count - 1
            results(i) = .MsgQueue(.MsgHead)
            .MsgQueue(.MsgHead) = ""
            .MsgHead = (.MsgHead + 1) Mod MSG_QUEUE_SIZE
            .MsgCount = .MsgCount - 1
        Next i
    End With
    WebSocketReceiveAll = results
End Function

Public Function WebSocketReceiveBinary(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Byte()
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then
        WebSocketReceiveBinary = Empty
        Exit Function
    End If
    With m_Connections(h)
        If .state <> STATE_OPEN Then
            If .AutoReconnect Then TryReconnect h
            WebSocketReceiveBinary = Empty
            Exit Function
        End If
        TickMaintenance h
        If .DecryptLen > 0 Then ProcessFrames h
        FeedBuffer h
        If .BinaryCount > 0 Then
            WebSocketReceiveBinary = .BinaryQueue(.BinaryHead).data
            Erase .BinaryQueue(.BinaryHead).data
            .BinaryHead = (.BinaryHead + 1) Mod MSG_QUEUE_SIZE
            .BinaryCount = .BinaryCount - 1
        Else
            WebSocketReceiveBinary = Empty
        End If
    End With
End Function

Public Function WebSocketReceiveBinaryCheck(ByRef outData() As Byte, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then
            If .AutoReconnect Then TryReconnect h
            Exit Function
        End If
        TickMaintenance h
        If .DecryptLen > 0 Then ProcessFrames h
        FeedBuffer h
        If .BinaryCount > 0 Then
            outData = .BinaryQueue(.BinaryHead).data
            Erase .BinaryQueue(.BinaryHead).data
            .BinaryHead = (.BinaryHead + 1) Mod MSG_QUEUE_SIZE
            .BinaryCount = .BinaryCount - 1
            WebSocketReceiveBinaryCheck = True
        End If
    End With
End Function

#If VBA7 Then
Public Function WebSocketReceiveZeroCopy(ByRef outPtr As LongPtr, ByRef outLen As Long, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
#Else
Public Function WebSocketReceiveZeroCopy(ByRef outPtr As Long, ByRef outLen As Long, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
#End If
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state <> STATE_OPEN Then
            If .AutoReconnect Then TryReconnect h
            Exit Function
        End If
        If Not .ZeroCopyEnabled Then Exit Function
        TickMaintenance h
        If .DecryptLen > 0 Then ProcessFrames h
        FeedBuffer h
        If .MsgCount > 0 Then
            m_ZeroCopyText = .MsgQueue(.MsgHead)
            outPtr = StrPtr(m_ZeroCopyText)
            outLen = Len(m_ZeroCopyText)
            .MsgQueue(.MsgHead) = ""
            .MsgHead = (.MsgHead + 1) Mod MSG_QUEUE_SIZE
            .MsgCount = .MsgCount - 1
            WebSocketReceiveZeroCopy = True
        End If
    End With
End Function

Public Function WebSocketPeek(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .MsgCount > 0 Then WebSocketPeek = .MsgQueue(.MsgHead)
    End With
End Function

Public Sub WebSocketFlushQueue(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    With m_Connections(h)
        .MsgHead = 0
        .MsgTail = 0
        .MsgCount = 0
        .BinaryHead = 0
        .BinaryTail = 0
        .BinaryCount = 0
    End With
End Sub

Public Function WebSocketIsConnected(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketIsConnected = (m_Connections(h).state = STATE_OPEN)
End Function

Public Function WebSocketGetLastError(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As WasabiError
    Dim h As Long
    h = ResolveHandle(handle)
    If ValidIndex(h) Then
        WebSocketGetLastError = m_Connections(h).LastError
    Else
        WebSocketGetLastError = m_LastError
    End If
End Function

Public Function WebSocketGetLastErrorCode(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If ValidIndex(h) Then
        WebSocketGetLastErrorCode = m_Connections(h).LastErrorCode
    Else
        WebSocketGetLastErrorCode = m_LastErrorCode
    End If
End Function

Public Function WebSocketGetTechnicalDetails(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If ValidIndex(h) Then
        WebSocketGetTechnicalDetails = m_Connections(h).TechnicalDetails
    Else
        WebSocketGetTechnicalDetails = m_TechnicalDetails
    End If
End Function

Public Function WebSocketGetErrorDescription(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    Dim errType As WasabiError
    Dim errCode As Long
    Dim tech As String
    Dim desc As String
    h = ResolveHandle(handle)
    If ValidIndex(h) Then
        errType = m_Connections(h).LastError
        errCode = m_Connections(h).LastErrorCode
        tech = m_Connections(h).TechnicalDetails
    Else
        errType = m_LastError
        errCode = m_LastErrorCode
        tech = m_TechnicalDetails
    End If
    Select Case errType
        Case ERR_NONE: desc = "No error"
        Case ERR_WSA_STARTUP_FAILED: desc = "Winsock initialization failed"
        Case ERR_SOCKET_CREATE_FAILED: desc = "Failed to create socket"
        Case ERR_DNS_RESOLVE_FAILED: desc = "DNS resolution failed"
        Case ERR_CONNECT_FAILED: desc = "TCP connection failed"
        Case ERR_TLS_ACQUIRE_CREDS_FAILED: desc = "TLS credentials initialization failed"
        Case ERR_TLS_HANDSHAKE_FAILED: desc = "TLS handshake failed"
        Case ERR_TLS_HANDSHAKE_TIMEOUT: desc = "TLS handshake timed out"
        Case ERR_WEBSOCKET_HANDSHAKE_FAILED: desc = "WebSocket upgrade rejected"
        Case ERR_WEBSOCKET_HANDSHAKE_TIMEOUT: desc = "WebSocket handshake timed out"
        Case ERR_SEND_FAILED: desc = "Send failed"
        Case ERR_RECV_FAILED: desc = "Receive failed"
        Case ERR_NOT_CONNECTED: desc = "Not connected"
        Case ERR_ALREADY_CONNECTED: desc = "Already connected"
        Case ERR_TLS_ENCRYPT_FAILED: desc = "TLS encryption failed"
        Case ERR_TLS_DECRYPT_FAILED: desc = "TLS decryption failed"
        Case ERR_INVALID_URL: desc = "Invalid URL"
        Case ERR_HANDSHAKE_REJECTED: desc = "WebSocket handshake rejected by server"
        Case ERR_CONNECTION_LOST: desc = "Connection lost"
        Case ERR_INVALID_HANDLE: desc = "Invalid connection handle"
        Case ERR_MAX_CONNECTIONS: desc = "Maximum connections reached"
        Case ERR_PROXY_CONNECT_FAILED: desc = "Proxy connection failed"
        Case ERR_PROXY_AUTH_FAILED: desc = "Proxy authentication failed"
        Case ERR_PROXY_TUNNEL_FAILED: desc = "Proxy tunnel failed"
        Case ERR_INACTIVITY_TIMEOUT: desc = "Inactivity timeout"
        Case ERR_CERT_LOAD_FAILED: desc = "Client certificate load failed"
        Case ERR_CERT_VALIDATE_FAILED: desc = "Server certificate validation failed"
        Case ERR_FRAGMENT_OVERFLOW: desc = "Fragment buffer overflow"
        Case ERR_TLS_RENEGOTIATE: desc = "TLS renegotiation not supported"
        Case Else: desc = "Unknown error (" & errType & ")"
    End Select
    If errCode <> 0 Then desc = desc & " [0x" & Hex(errCode) & "]"
    If Len(tech) > 0 Then desc = desc & " - " & tech
    WebSocketGetErrorDescription = desc
End Function

Public Function WebSocketGetPendingCount(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetPendingCount = m_Connections(h).MsgCount
End Function

Public Function WebSocketGetBinaryPendingCount(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetBinaryPendingCount = m_Connections(h).BinaryCount
End Function

Public Function WebSocketGetQueueCapacity(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetQueueCapacity = MSG_QUEUE_SIZE - m_Connections(h).MsgCount
End Function

Public Function WebSocketGetStats(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    Dim uptime As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    If m_Connections(h).Mode <> MODE_WEBSOCKET Then Exit Function
    With m_Connections(h)
        If .stats.ConnectedAt > 0 Then uptime = TickDiff(.stats.ConnectedAt, GetTickCount()) \ 1000
        WebSocketGetStats = "BytesSent=" & Format(.stats.BytesSent, "0") & _
            "|BytesReceived=" & Format(.stats.BytesReceived, "0") & _
            "|MessagesSent=" & .stats.MessagesSent & _
            "|MessagesReceived=" & .stats.MessagesReceived & _
            "|UptimeSeconds=" & uptime & _
            "|Queued=" & .MsgCount & _
            "|BinaryQueued=" & .BinaryCount & _
            "|NoDelay=" & IIf(.NoDelay, "1", "0") & _
            "|Proxy=" & IIf(.ProxyEnabled, .proxyHost & ":" & .proxyPort, "none") & _
            "|Mode=WebSocket"
    End With
End Function

Public Function TcpGetStats(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    Dim uptime As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Function
    With m_Connections(h)
        If .stats.ConnectedAt > 0 Then uptime = TickDiff(.stats.ConnectedAt, GetTickCount()) \ 1000
        TcpGetStats = "BytesSent=" & Format(.stats.BytesSent, "0") & _
            "|BytesReceived=" & Format(.stats.BytesReceived, "0") & _
            "|MessagesSent=" & .stats.MessagesSent & _
            "|MessagesReceived=" & .stats.MessagesReceived & _
            "|UptimeSeconds=" & uptime & _
            "|PendingBytes=" & .TcpRecvLen & _
            "|NoDelay=" & IIf(.NoDelay, "1", "0") & _
            "|Proxy=" & IIf(.ProxyEnabled, .proxyHost & ":" & .proxyPort, "none") & _
            "|Mode=" & IIf(.Mode = MODE_TCP_TLS, "TCP_TLS", "TCP") & _
            "|Host=" & .HOST & _
            "|Port=" & .port
    End With
End Function

Public Function TcpBroadcast(ByRef data() As Byte) As Long
    Dim i As Long
    Dim count As Long
    InitConnectionPool
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state = STATE_OPEN And m_Connections(i).Mode <> MODE_WEBSOCKET Then
            If TcpSend(data, i) Then count = count + 1
        End If
    Next i
    TcpBroadcast = count
End Function

Public Function TcpBroadcastText(ByVal text As String) As Long
    Dim i As Long
    Dim count As Long
    Dim data() As Byte
    data = StringToUtf8(text)
    InitConnectionPool
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state = STATE_OPEN And m_Connections(i).Mode <> MODE_WEBSOCKET Then
            If TcpSend(data, i) Then count = count + 1
        End If
    Next i
    TcpBroadcastText = count
End Function

Public Function TcpSetNoDelay(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim optVal As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Function
    m_Connections(h).NoDelay = enabled
    If m_Connections(h).Socket <> INVALID_SOCKET Then
        optVal = IIf(enabled, 1, 0)
        TcpSetNoDelay = (sock_setsockopt(m_Connections(h).Socket, IPPROTO_TCP_SOL, TCP_NODELAY, optVal, 4) = 0)
    Else
        TcpSetNoDelay = True
    End If
End Function

Public Sub TcpSetInactivityTimeout(ByVal timeoutMs As Long, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    m_Connections(h).InactivityTimeoutMs = timeoutMs
    m_Connections(h).LastActivityAt = GetTickCount()
End Sub

Public Sub TcpSetReceiveTimeout(ByVal timeoutMs As Long, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    m_Connections(h).ReceiveTimeoutMs = timeoutMs
End Sub

Public Sub TcpSetLogCallback(ByVal callbackName As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    m_Connections(h).LogCallback = callbackName
End Sub

Public Sub TcpSetProxy(ByVal proxyHost As String, ByVal proxyPort As Long, Optional ByVal proxyUser As String = "", Optional ByVal proxyPass As String = "", Optional ByVal proxyType As Long = PROXY_TYPE_HTTP, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    With m_Connections(h)
        .proxyHost = proxyHost
        .proxyPort = proxyPort
        .proxyUser = proxyUser
        .proxyPass = proxyPass
        .proxyType = proxyType
        .ProxyEnabled = (Len(proxyHost) > 0 And proxyPort > 0)
    End With
End Sub

Public Sub TcpClearProxy(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    With m_Connections(h)
        .proxyHost = ""
        .proxyPort = 0
        .proxyUser = ""
        .proxyPass = ""
        .ProxyEnabled = False
    End With
End Sub

Public Sub TcpSetCertValidation(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    m_Connections(h).ValidateServerCert = enabled
End Sub

Public Sub TcpSetRevocationCheck(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    m_Connections(h).EnableRevocationCheck = enabled
End Sub

Public Sub TcpSetClientCert(ByVal thumbprintOrSubject As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    m_Connections(h).ClientCertThumb = thumbprintOrSubject
    m_Connections(h).ClientCertPfxPath = ""
End Sub

Public Sub TcpSetClientCertPfx(ByVal pfxPath As String, ByVal pfxPassword As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    m_Connections(h).ClientCertPfxPath = pfxPath
    m_Connections(h).ClientCertPfxPass = pfxPassword
    m_Connections(h).ClientCertThumb = ""
End Sub

Public Sub TcpSetBufferSize(ByVal bufferSize As Long, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    With m_Connections(h)
        If .state = STATE_OPEN Then
            WasabiLog h, "Cannot change buffer size while connected (handle=" & h & ")"
            Exit Sub
        End If
        If bufferSize >= 8192 And bufferSize <= 16777216 Then
            .CustomBufferSize = bufferSize
        End If
    End With
End Sub

Public Function TcpGetHost(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Function
    TcpGetHost = m_Connections(h).HOST
End Function

Public Function TcpGetPort(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Function
    TcpGetPort = m_Connections(h).port
End Function

Public Function TcpGetMode(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As WasabiConnectionMode
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    TcpGetMode = m_Connections(h).Mode
End Function

Public Function TcpGetUptime(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Function
    With m_Connections(h)
        If .state = STATE_OPEN And .stats.ConnectedAt > 0 Then
            TcpGetUptime = TickDiff(.stats.ConnectedAt, GetTickCount()) \ 1000
        End If
    End With
End Function

Public Function TcpGetLastError(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As WasabiError
    Dim h As Long
    h = ResolveHandle(handle)
    If ValidIndex(h) Then
        TcpGetLastError = m_Connections(h).LastError
    Else
        TcpGetLastError = m_LastError
    End If
End Function

Public Function TcpGetLastErrorCode(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If ValidIndex(h) Then
        TcpGetLastErrorCode = m_Connections(h).LastErrorCode
    Else
        TcpGetLastErrorCode = m_LastErrorCode
    End If
End Function

Public Function TcpGetTechnicalDetails(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If ValidIndex(h) Then
        TcpGetTechnicalDetails = m_Connections(h).TechnicalDetails
    Else
        TcpGetTechnicalDetails = m_TechnicalDetails
    End If
End Function

Public Sub TcpResetStats(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    With m_Connections(h).stats
        .BytesSent = 0
        .BytesReceived = 0
        .MessagesSent = 0
        .MessagesReceived = 0
        .ConnectedAt = GetTickCount()
    End With
End Sub

Public Sub TcpSetPreferIPv6(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    m_Connections(h).PreferIPv6 = enabled
End Sub

Public Sub TcpSetErrorDialog(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    m_Connections(h).EnableErrorDialog = enabled
End Sub

Public Function TcpGetProxyInfo(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Function
    With m_Connections(h)
        If .ProxyEnabled Then
            TcpGetProxyInfo = "Type=" & IIf(.proxyType = PROXY_TYPE_SOCKS5, "SOCKS5", "HTTP") & _
                "|Host=" & .proxyHost & _
                "|Port=" & .proxyPort & _
                "|Auth=" & IIf(.proxyUser <> "", "Yes", "No")
        Else
            TcpGetProxyInfo = "Disabled"
        End If
    End With
End Function

Public Function TcpGetMTUInfo(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Function
    With m_Connections(h)
        TcpGetMTUInfo = "MTU=" & .mtu.CurrentMTU & _
            "|MSS=" & .mtu.MaxSegmentSize & _
            "|OptimalFrame=" & .mtu.OptimalFrameSize & _
            "|AutoMTU=" & IIf(.AutoMTU, "Yes", "No")
    End With
End Function

Public Sub TcpSetMTU(ByVal mtu As Long, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    If mtu < 576 Or mtu > 9000 Then mtu = DEFAULT_MTU
    m_Connections(h).mtu.CurrentMTU = mtu
    CalculateOptimalFrameSize h
End Sub

Public Sub TcpSetAutoMTU(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub
    m_Connections(h).AutoMTU = enabled
End Sub

Public Function TcpGetLatency(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Function
    TcpGetLatency = m_Connections(h).LastRttMs
End Function

Public Sub TcpAutoDiscoverProxy(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim proxyConfig As WINHTTP_CURRENT_USER_IE_PROXY_CONFIG
    Dim proxyRaw As String
    Dim proxyHost As String
    Dim proxyPort As Long
    Dim parts() As String
    Dim hostPort() As String
    Dim h As Long

    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).Mode = MODE_WEBSOCKET Then Exit Sub

    If WinHttpGetIEProxyConfigForCurrentUser(proxyConfig) <> 0 Then
        If proxyConfig.lpszProxy <> 0 Then
            proxyRaw = PtrToStrW(proxyConfig.lpszProxy)
            parts = Split(proxyRaw, ";")
            proxyHost = parts(0)
            If InStr(proxyHost, "=") > 0 Then proxyHost = Split(proxyHost, "=")(1)
            If InStr(proxyHost, ":") > 0 Then
                hostPort = Split(proxyHost, ":")
                proxyHost = hostPort(0)
                proxyPort = val(hostPort(1))
            Else
                proxyPort = 80
            End If
            TcpSetProxy proxyHost, proxyPort, , , PROXY_TYPE_HTTP, h
            WasabiLog h, "TCP auto-discovered proxy: " & proxyHost & ":" & proxyPort
        End If
        If proxyConfig.lpszAutoConfigUrl <> 0 Then GlobalFree proxyConfig.lpszAutoConfigUrl
        If proxyConfig.lpszProxy <> 0 Then GlobalFree proxyConfig.lpszProxy
        If proxyConfig.lpszProxyBypass <> 0 Then GlobalFree proxyConfig.lpszProxyBypass
    Else
        WasabiLog h, "TCP auto proxy: No proxy configuration found."
    End If
End Sub

Public Function WebSocketGetUptime(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .state = STATE_OPEN And .stats.ConnectedAt > 0 Then
            WebSocketGetUptime = TickDiff(.stats.ConnectedAt, GetTickCount()) \ 1000
        End If
    End With
End Function

Public Sub WebSocketResetStats(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    With m_Connections(h).stats
        .BytesSent = 0
        .BytesReceived = 0
        .MessagesSent = 0
        .MessagesReceived = 0
        .ConnectedAt = GetTickCount()
    End With
End Sub

Public Function WebSocketGetCloseCode(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Integer
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetCloseCode = m_Connections(h).closeCode
End Function

Public Function WebSocketGetCloseReason(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetCloseReason = m_Connections(h).closeReason
End Function

Public Function WebSocketGetCloseInfo(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        WebSocketGetCloseInfo = "Code=" & .closeCode & _
            "|Description=" & GetCloseCodeDesc(.closeCode) & _
            "|Reason=" & IIf(Len(.closeReason) > 0, .closeReason, "(empty)") & _
            "|InitiatedByUs=" & IIf(.CloseInitiatedByUs, "Yes", "No")
    End With
End Function

Public Function WebSocketGetConnectionCount() As Long
    Dim i As Long
    Dim count As Long
    InitConnectionPool
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state = STATE_OPEN Then count = count + 1
    Next i
    WebSocketGetConnectionCount = count
End Function

Public Function TcpGetConnectionCount() As Long
    Dim i As Long
    Dim count As Long
    InitConnectionPool
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state = STATE_OPEN And m_Connections(i).Mode <> MODE_WEBSOCKET Then
            count = count + 1
        End If
    Next i
    TcpGetConnectionCount = count
End Function

Public Function WebSocketGetAllHandles() As Long()
    Dim result() As Long
    Dim i As Long
    Dim idx As Long
    Dim count As Long
    InitConnectionPool
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state = STATE_OPEN And m_Connections(i).Mode = MODE_WEBSOCKET Then count = count + 1
    Next i
    If count = 0 Then
        WebSocketGetAllHandles = result
        Exit Function
    End If
    ReDim result(0 To count - 1)
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state = STATE_OPEN And m_Connections(i).Mode = MODE_WEBSOCKET Then
            result(idx) = i
            idx = idx + 1
        End If
    Next i
    WebSocketGetAllHandles = result
End Function

Public Function TcpGetAllHandles() As Long()
    Dim result() As Long
    Dim i As Long
    Dim idx As Long
    Dim count As Long
    InitConnectionPool
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state = STATE_OPEN And m_Connections(i).Mode <> MODE_WEBSOCKET Then count = count + 1
    Next i
    If count = 0 Then
        TcpGetAllHandles = result
        Exit Function
    End If
    ReDim result(0 To count - 1)
    For i = 0 To MAX_CONNECTIONS - 1
        If m_Connections(i).state = STATE_OPEN And m_Connections(i).Mode <> MODE_WEBSOCKET Then
            result(idx) = i
            idx = idx + 1
        End If
    Next i
    TcpGetAllHandles = result
End Function

Public Function WebSocketSetDefaultHandle(ByVal handle As Long) As Boolean
    If Not ValidIndex(handle) Then Exit Function
    If m_Connections(handle).state <> STATE_OPEN Then Exit Function
    m_DefaultHandle = handle
    WebSocketSetDefaultHandle = True
End Function

Public Function WebSocketGetDefaultHandle() As Long
    WebSocketGetDefaultHandle = m_DefaultHandle
End Function

Public Sub WebSocketSetAutoReconnect(ByVal enabled As Boolean, Optional ByVal maxAttempts As Long = DEFAULT_RECONNECT_MAX_ATTEMPTS, Optional ByVal baseDelayMs As Long = DEFAULT_RECONNECT_BASE_DELAY_MS, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    With m_Connections(h)
        .AutoReconnect = enabled
        .ReconnectMaxAttempts = maxAttempts
        .ReconnectBaseDelayMs = baseDelayMs
        .ReconnectAttempts = 0
    End With
End Sub

Public Function WebSocketGetReconnectInfo(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        WebSocketGetReconnectInfo = "AutoReconnect=" & IIf(.AutoReconnect, "1", "0") & _
            "|Attempts=" & .ReconnectAttempts & _
            "|MaxAttempts=" & .ReconnectMaxAttempts & _
            "|BaseDelayMs=" & .ReconnectBaseDelayMs
    End With
End Function

Private Sub CalculateNextPing(ByVal handle As Long)
    With m_Connections(handle)
        If .PingJitterMaxMs > 0 Then
            .CurrentPingIntervalMs = .PingIntervalMs + CLng(Rnd * .PingJitterMaxMs)
        Else
            .CurrentPingIntervalMs = .PingIntervalMs
        End If
    End With
End Sub

Public Sub WebSocketSetPingInterval(ByVal intervalMs As Long, Optional ByVal jitterMaxMs As Long = 0, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).PingIntervalMs = intervalMs
    m_Connections(h).PingJitterMaxMs = jitterMaxMs
    CalculateNextPing h
    m_Connections(h).LastPingSentAt = GetTickCount()
End Sub

Public Sub WebSocketSetReceiveTimeout(ByVal timeoutMs As Long, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).ReceiveTimeoutMs = timeoutMs
End Sub

Public Sub WebSocketSetInactivityTimeout(ByVal timeoutMs As Long, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).InactivityTimeoutMs = timeoutMs
    m_Connections(h).LastActivityAt = GetTickCount()
End Sub

Public Sub WebSocketAutoDiscoverProxy(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim proxyConfig As WINHTTP_CURRENT_USER_IE_PROXY_CONFIG
    Dim proxyRaw As String
    Dim proxyHost As String
    Dim proxyPort As Long
    Dim parts() As String
    Dim hostPort() As String
    Dim h As Long
    
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    
    If WinHttpGetIEProxyConfigForCurrentUser(proxyConfig) <> 0 Then
        If proxyConfig.lpszProxy <> 0 Then
            proxyRaw = PtrToStrW(proxyConfig.lpszProxy)
            
            parts = Split(proxyRaw, ";")
            proxyHost = parts(0)
            
            If InStr(proxyHost, "=") > 0 Then
                proxyHost = Split(proxyHost, "=")(1)
            End If
            
            If InStr(proxyHost, ":") > 0 Then
                hostPort = Split(proxyHost, ":")
                proxyHost = hostPort(0)
                proxyPort = val(hostPort(1))
            Else
                proxyPort = 80
            End If
            
            WebSocketSetProxy proxyHost, proxyPort, , , PROXY_TYPE_HTTP, h
            WasabiLog h, "Auto-discovered proxy: " & proxyHost & ":" & proxyPort
        End If
        
        If proxyConfig.lpszAutoConfigUrl <> 0 Then GlobalFree proxyConfig.lpszAutoConfigUrl
        If proxyConfig.lpszProxy <> 0 Then GlobalFree proxyConfig.lpszProxy
        If proxyConfig.lpszProxyBypass <> 0 Then GlobalFree proxyConfig.lpszProxyBypass
    Else
        WasabiLog h, "Auto proxy: No proxy configuration found."
    End If
End Sub

Public Sub WebSocketSetProxy(ByVal proxyHost As String, ByVal proxyPort As Long, Optional ByVal proxyUser As String = "", Optional ByVal proxyPass As String = "", Optional ByVal proxyType As Long = PROXY_TYPE_HTTP, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    With m_Connections(h)
        .proxyHost = proxyHost
        .proxyPort = proxyPort
        .proxyUser = proxyUser
        .proxyPass = proxyPass
        .proxyType = proxyType
        .ProxyEnabled = (Len(proxyHost) > 0 And proxyPort > 0)
    End With
End Sub

Public Sub WebSocketClearProxy(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    With m_Connections(h)
        .proxyHost = ""
        .proxyPort = 0
        .proxyUser = ""
        .proxyPass = ""
        .ProxyEnabled = False
    End With
End Sub

Public Function WebSocketGetProxyInfo(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        If .ProxyEnabled Then
            WebSocketGetProxyInfo = "Type=" & IIf(.proxyType = PROXY_TYPE_SOCKS5, "SOCKS5", "HTTP") & _
                "|Host=" & .proxyHost & _
                "|Port=" & .proxyPort & _
                "|Auth=" & IIf(.proxyUser <> "", "Yes", "No")
        Else
            WebSocketGetProxyInfo = "Disabled"
        End If
    End With
End Function

Public Sub WebSocketSetSubProtocol(ByVal protocol As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).SubProtocol = protocol
End Sub

Public Function WebSocketGetSubProtocol(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetSubProtocol = m_Connections(h).SubProtocol
End Function

Public Sub WebSocketAddHeader(ByVal headerName As String, ByVal headerValue As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    
    With m_Connections(h)
        If .CustomHeaderCount = 0 Then
            ReDim .CustomHeaders(0 To 31)
        ElseIf .CustomHeaderCount > UBound(.CustomHeaders) Then
            ReDim Preserve .CustomHeaders(0 To UBound(.CustomHeaders) + 8)
        End If
        
        .CustomHeaders(.CustomHeaderCount) = headerName & ": " & headerValue
        .CustomHeaderCount = .CustomHeaderCount + 1
    End With
End Sub

Public Sub WebSocketClearHeaders(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).CustomHeaderCount = 0
End Sub

Public Sub WebSocketSetLogCallback(ByVal callbackName As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).LogCallback = callbackName
End Sub

Public Sub WebSocketSetErrorDialog(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).EnableErrorDialog = enabled
End Sub

Public Function WebSocketSetNoDelay(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim optVal As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    m_Connections(h).NoDelay = enabled
    If m_Connections(h).Socket <> INVALID_SOCKET Then
        optVal = IIf(enabled, 1, 0)
        WebSocketSetNoDelay = (sock_setsockopt(m_Connections(h).Socket, IPPROTO_TCP_SOL, TCP_NODELAY, optVal, 4) = 0)
    Else
        WebSocketSetNoDelay = True
    End If
End Function

Public Sub WebSocketSetPreferIPv6(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).PreferIPv6 = enabled
End Sub

Public Sub WebSocketSetCertValidation(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).ValidateServerCert = enabled
End Sub

Public Sub WebSocketSetRevocationCheck(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).EnableRevocationCheck = enabled
End Sub

Public Sub WebSocketSetClientCert(ByVal thumbprintOrSubject As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).ClientCertThumb = thumbprintOrSubject
    m_Connections(h).ClientCertPfxPath = ""
End Sub

Public Sub WebSocketSetClientCertPfx(ByVal pfxPath As String, ByVal pfxPassword As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).ClientCertPfxPath = pfxPath
    m_Connections(h).ClientCertPfxPass = pfxPassword
    m_Connections(h).ClientCertThumb = ""
End Sub

Public Sub WebSocketSetBufferSizes(ByVal bufferSize As Long, ByVal fragmentSize As Long, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    With m_Connections(h)
        If .state = STATE_OPEN Then
            WasabiLog h, "Cannot change buffer sizes while connected (handle=" & h & ")"
            Exit Sub
        End If
        If bufferSize >= 8192 And bufferSize <= 16777216 Then
            .CustomBufferSize = bufferSize
        End If
        If fragmentSize >= 4096 And fragmentSize <= 16777216 Then
            .CustomFragmentSize = fragmentSize
        End If
    End With
End Sub

Public Sub WebSocketSetZeroCopy(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).ZeroCopyEnabled = enabled
End Sub

Public Sub WebSocketSetMTU(ByVal mtu As Long, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If mtu < 576 Or mtu > 9000 Then
        mtu = DEFAULT_MTU
    End If
    m_Connections(h).mtu.CurrentMTU = mtu
    CalculateOptimalFrameSize h
End Sub

Public Sub WebSocketSetAutoMTU(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).AutoMTU = enabled
End Sub

Public Function WebSocketGetMTU(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetMTU = m_Connections(h).mtu.CurrentMTU
End Function

Public Function WebSocketGetOptimalFrameSize(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetOptimalFrameSize = m_Connections(h).mtu.OptimalFrameSize
End Function

Public Function WebSocketGetMTUInfo(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    With m_Connections(h)
        WebSocketGetMTUInfo = "MTU=" & .mtu.CurrentMTU & _
            "|MSS=" & .mtu.MaxSegmentSize & _
            "|OptimalFrame=" & .mtu.OptimalFrameSize & _
            "|AutoMTU=" & IIf(.AutoMTU, "Yes", "No") & _
            "|ProbeEnabled=" & IIf(.mtu.ProbeEnabled, "Yes", "No")
    End With
End Function

Public Sub WebSocketProbeMTU(Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    If m_Connections(h).state = STATE_OPEN Then
        probeMTU h
    End If
End Sub

Public Function WebSocketGetHost(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetHost = m_Connections(h).HOST
End Function

Public Function WebSocketGetPort(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetPort = m_Connections(h).port
End Function

Public Function WebSocketGetPath(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetPath = m_Connections(h).path
End Function

Public Sub WebSocketSetHttp2(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).UseHttp2 = enabled
End Sub

Public Sub WebSocketSetProxyNtlm(ByVal enabled As Boolean, Optional ByVal handle As Long = INVALID_CONN_HANDLE)
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Sub
    m_Connections(h).ProxyUseNtlm = enabled
End Sub

Public Function WebSocketGetLatency(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Long
    Dim h As Long
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    WebSocketGetLatency = m_Connections(h).LastRttMs
End Function

Public Function MqttConnect(ByVal clientId As String, Optional ByVal username As String = "", Optional ByVal password As String = "", Optional ByVal keepAlive As Integer = 60, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim packet() As Byte
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    packet = BuildMqttConnectPacket(clientId, username, password, keepAlive)
    MqttConnect = WebSocketSendBinary(packet, h)
    If MqttConnect Then
        MqttResetParser h
    End If
End Function

Private Function MqttEncodeProperty(ByVal key As String, ByVal value As String) As Byte()
    Dim kBytes() As Byte, vBytes() As Byte
    Dim res() As Byte
    Dim kLen As Long, vLen As Long
    
    kBytes = StringToUtf8(key): kLen = SafeArrayLen(kBytes)
    vBytes = StringToUtf8(value): vLen = SafeArrayLen(vBytes)
    
    ReDim res(0 To 1 + 2 + kLen + 2 + vLen - 1)
    res(0) = &H26
    res(1) = CByte((kLen \ 256) And &HFF): res(2) = CByte(kLen And &HFF)
    If kLen > 0 Then CopyMemory res(3), kBytes(0), kLen
    res(3 + kLen) = CByte((vLen \ 256) And &HFF): res(4 + kLen) = CByte(vLen And &HFF)
    If vLen > 0 Then CopyMemory res(5 + kLen), vBytes(0), vLen
    
    MqttEncodeProperty = res
End Function

Public Function MqttPublish(ByVal topic As String, ByVal message As String, Optional ByVal qos As Byte = 0, Optional ByVal retained As Boolean = False, Optional ByVal metaKey As String = "", Optional ByVal metaValue As String = "", Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim topicBytes() As Byte, msgBytes() As Byte, payload() As Byte
    Dim propBytes() As Byte, propLen As Long, propLenVar(0 To 3) As Byte, propLenVarSize As Long
    Dim payloadLen As Long, pos As Long
    Dim flags As Byte, packet() As Byte, packetId As Integer
    Dim tLen As Long, mLen As Long
    
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    
    topicBytes = StringToUtf8(topic)
    msgBytes = StringToUtf8(message)
    tLen = SafeArrayLen(topicBytes)
    mLen = SafeArrayLen(msgBytes)
    
    If metaKey <> "" Then
        propBytes = MqttEncodeProperty(metaKey, metaValue)
        propLen = SafeArrayLen(propBytes)
    Else
        propLen = 0
    End If
    
    propLenVarSize = MqttEncodeRemainingLength(propLen, propLenVar)
    
    payloadLen = 2 + tLen + IIf(qos > 0, 2, 0) + propLenVarSize + propLen + mLen
    ReDim payload(0 To payloadLen - 1)
    
    pos = 0
    payload(pos) = CByte((tLen \ 256) And &HFF)
    payload(pos + 1) = CByte(tLen And &HFF)
    pos = pos + 2
    
    If tLen > 0 Then
        CopyMemory payload(pos), topicBytes(0), tLen
        pos = pos + tLen
    End If
    
    If qos > 0 Then
        With m_Connections(h)
            .MqttNextPacketId = .MqttNextPacketId + 1
            If .MqttNextPacketId < 0 Or .MqttNextPacketId > 65535 Then .MqttNextPacketId = 1
            packetId = .MqttNextPacketId
            
            payload(pos) = CByte((packetId \ 256) And &HFF)
            payload(pos + 1) = CByte(packetId And &HFF)
            pos = pos + 2
            
            If .MqttInFlightCount > UBound(.MqttInFlight) Then
                ReDim Preserve .MqttInFlight(0 To UBound(.MqttInFlight) + 10)
            End If
            
            .MqttInFlight(.MqttInFlightCount).packetId = packetId
            .MqttInFlight(.MqttInFlightCount).topic = topic
            .MqttInFlight(.MqttInFlightCount).qos = qos
            .MqttInFlight(.MqttInFlightCount).payload = msgBytes
            .MqttInFlight(.MqttInFlightCount).SentTick = GetTickCount()
            .MqttInFlightCount = .MqttInFlightCount + 1
        End With
    End If
    
    CopyMemory payload(pos), propLenVar(0), propLenVarSize
    pos = pos + propLenVarSize
    
    If propLen > 0 Then
        CopyMemory payload(pos), propBytes(0), propLen
        pos = pos + propLen
    End If
    
    If mLen > 0 Then
        CopyMemory payload(pos), msgBytes(0), mLen
    End If
    
    flags = IIf(retained, 1, 0) Or (qos * 2)
    packet = MqttBuildPacket(MQTT_PUBLISH, flags, payload, payloadLen)
    
    MqttPublish = WebSocketSendBinary(packet, h)
End Function

Public Function MqttSubscribe(ByVal topic As String, Optional ByVal qos As Byte = 0, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim topicBytes() As Byte
    Dim payload() As Byte
    Dim payloadLen As Long
    Dim packet() As Byte
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    topicBytes = StringToUtf8(topic)
    payloadLen = 2 + 1 + 2 + UBound(topicBytes) + 1 + 1
    ReDim payload(0 To payloadLen - 1)
    payload(0) = 0
    payload(1) = 10
    payload(2) = 0
    payload(3) = CByte(((UBound(topicBytes) + 1) \ 256) And &HFF)
    payload(4) = CByte((UBound(topicBytes) + 1) And &HFF)
    CopyMemory payload(5), topicBytes(0), UBound(topicBytes) + 1
    payload(5 + UBound(topicBytes) + 1) = qos
    packet = MqttBuildPacket(MQTT_SUBSCRIBE, 2, payload, payloadLen)
    MqttSubscribe = WebSocketSendBinary(packet, h)
End Function

Public Function MqttUnsubscribe(ByVal topic As String, Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim topicBytes() As Byte
    Dim payload() As Byte
    Dim payloadLen As Long
    Dim packet() As Byte
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    topicBytes = StringToUtf8(topic)
    payloadLen = 2 + 2 + UBound(topicBytes) + 1
    ReDim payload(0 To payloadLen - 1)
    payload(0) = 0
    payload(1) = 10
    payload(2) = CByte((Len(topic) \ 256) And &HFF)
    payload(3) = CByte(Len(topic) And &HFF)
    CopyMemory payload(4), topicBytes(0), UBound(topicBytes) + 1
    packet = MqttBuildPacket(MQTT_UNSUBSCRIBE, 2, payload, payloadLen)
    MqttUnsubscribe = WebSocketSendBinary(packet, h)
End Function

Public Function MqttDisconnect(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim packet() As Byte
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    packet = MqttBuildPacket(MQTT_DISCONNECT, 0, NullByteArray(), 0)
    MqttDisconnect = WebSocketSendBinary(packet, h)
End Function

Public Function MqttPingReq(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As Boolean
    Dim h As Long
    Dim packet() As Byte
    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then Exit Function
    packet = MqttBuildPacket(MQTT_PINGREQ, 0, NullByteArray(), 0)
    MqttPingReq = WebSocketSendBinary(packet, h)
End Function

Public Function MqttReceive(Optional ByVal handle As Long = INVALID_CONN_HANDLE) As String
    Dim h As Long
    Dim data() As Byte
    Dim i As Long
    Dim j As Long
    Dim topicLen As Long
    Dim topic As String
    Dim msgBytes() As Byte
    Dim msgLen As Long
    Dim flags As Byte
    Dim qos As Long
    Dim packetId As Long
    Dim skipLen As Long
    Dim propLen As Long
    Dim propEnd As Long
    Dim propId As Byte
    Dim metaInfo As String
    Dim tTopicB() As Byte
    Dim kB() As Byte
    Dim vB() As Byte
    Dim kL As Long
    Dim vL As Long
    Dim reasonCode As Byte

    h = ResolveHandle(handle)
    If Not ValidIndex(h) Then
        Exit Function
    End If
    
    If m_Connections(h).state <> STATE_OPEN Then
        Exit Function
    End If

    Do
        WebSocketReceive h
        If WebSocketReceiveBinaryCheck(data, h) Then
            For i = LBound(data) To UBound(data)
                MqttParseByte h, data(i)
                If MqttHasPacket(h) Then
                    With m_Connections(h)
                        Select Case .MqttCurrentPacketType
                            Case MQTT_CONNACK
                                reasonCode = .MqttBuffer(1)
                                skipLen = 2
                                propLen = MqttDecodeVarInt(.MqttBuffer, skipLen)
                                propEnd = skipLen + propLen
                                metaInfo = ""
                                
                                If reasonCode > 0 Then
                                    Do While skipLen < propEnd
                                        propId = .MqttBuffer(skipLen)
                                        skipLen = skipLen + 1
                                        If propId = 31 Then
                                            vL = CLng(.MqttBuffer(skipLen)) * 256& + .MqttBuffer(skipLen + 1)
                                            skipLen = skipLen + 2
                                            ReDim vB(0 To vL - 1)
                                            CopyMemory vB(0), .MqttBuffer(skipLen), vL
                                            metaInfo = " | Erro: " & Utf8ToString(vB, vL)
                                            Exit Do
                                        Else
                                            skipLen = propEnd
                                        End If
                                    Loop
                                    MqttReceive = "[CONNACK_ERROR] Code=" & reasonCode & metaInfo
                                Else
                                    MqttReceive = "[CONNACK]"
                                End If
                                MqttResetParser h
                                Exit Function

                            Case MQTT_PUBLISH
                                flags = .MqttCurrentFlags
                                qos = (flags And 6) \ 2
                                
                                topicLen = CLng(.MqttBuffer(0)) * 256& + CLng(.MqttBuffer(1))
                                If topicLen > 0 Then
                                    ReDim tTopicB(0 To topicLen - 1)
                                    CopyMemory tTopicB(0), .MqttBuffer(2), topicLen
                                    topic = Utf8ToString(tTopicB, topicLen)
                                Else
                                    topic = ""
                                End If
                                
                                skipLen = 2 + topicLen
                                If qos > 0 Then
                                    packetId = CLng(.MqttBuffer(skipLen)) * 256& + CLng(.MqttBuffer(skipLen + 1))
                                    skipLen = skipLen + 2
                                End If
                                
                                propLen = MqttDecodeVarInt(.MqttBuffer, skipLen)
                                propEnd = skipLen + propLen
                                metaInfo = ""
                                
                                Do While skipLen < propEnd
                                    propId = .MqttBuffer(skipLen)
                                    skipLen = skipLen + 1
                                    If propId = 38 Then
                                        kL = CLng(.MqttBuffer(skipLen)) * 256& + .MqttBuffer(skipLen + 1)
                                        skipLen = skipLen + 2
                                        ReDim kB(0 To kL - 1)
                                        CopyMemory kB(0), .MqttBuffer(skipLen), kL
                                        skipLen = skipLen + kL
                                        
                                        vL = CLng(.MqttBuffer(skipLen)) * 256& + .MqttBuffer(skipLen + 1)
                                        skipLen = skipLen + 2
                                        ReDim vB(0 To vL - 1)
                                        CopyMemory vB(0), .MqttBuffer(skipLen), vL
                                        skipLen = skipLen + vL
                                        
                                        metaInfo = metaInfo & "|" & Utf8ToString(kB, kL) & "=" & Utf8ToString(vB, vL)
                                    Else
                                        skipLen = propEnd
                                    End If
                                Loop
                                
                                If qos = 1 Then
                                    MqttSendAck h, MQTT_PUBACK, 0, CInt(packetId)
                                End If
                                
                                If qos = 2 Then
                                    MqttSendAck h, MQTT_PUBREC, 0, CInt(packetId)
                                End If
                                
                                msgLen = .MqttBufLen - skipLen
                                If msgLen > 0 Then
                                    ReDim msgBytes(0 To msgLen - 1)
                                    CopyMemory msgBytes(0), .MqttBuffer(skipLen), msgLen
                                    MqttReceive = topic & "|" & Utf8ToString(msgBytes, msgLen) & metaInfo
                                Else
                                    MqttReceive = topic & "|" & metaInfo
                                End If
                                
                                MqttResetParser h
                                Exit Function

                            Case 14
                                reasonCode = .MqttBuffer(0)
                                skipLen = 1
                                propLen = MqttDecodeVarInt(.MqttBuffer, skipLen)
                                propEnd = skipLen + propLen
                                metaInfo = ""
                                
                                Do While skipLen < propEnd
                                    propId = .MqttBuffer(skipLen)
                                    skipLen = skipLen + 1
                                    If propId = 31 Then
                                        vL = CLng(.MqttBuffer(skipLen)) * 256& + .MqttBuffer(skipLen + 1)
                                        skipLen = skipLen + 2
                                        ReDim vB(0 To vL - 1)
                                        CopyMemory vB(0), .MqttBuffer(skipLen), vL
                                        metaInfo = " | Motivo: " & Utf8ToString(vB, vL)
                                        Exit Do
                                    Else
                                        skipLen = propEnd
                                    End If
                                Loop
                                
                                MqttReceive = "[DISCONNECT] Code=" & reasonCode & metaInfo
                                MqttResetParser h
                                Exit Function

                            Case MQTT_SUBACK
                                MqttResetParser h
                                MqttReceive = "[SUBACK]"
                                Exit Function
                                
                            Case MQTT_PUBACK, MQTT_PUBCOMP
                                packetId = CLng(.MqttBuffer(0)) * 256& + CLng(.MqttBuffer(1))
                                For j = 0 To .MqttInFlightCount - 1
                                    If .MqttInFlight(j).packetId = packetId Then
                                        If j < .MqttInFlightCount - 1 Then
                                            .MqttInFlight(j) = .MqttInFlight(.MqttInFlightCount - 1)
                                        End If
                                        .MqttInFlightCount = .MqttInFlightCount - 1
                                        Exit For
                                    End If
                                Next j
                                MqttResetParser h
                                
                            Case MQTT_PUBREC
                                packetId = CLng(.MqttBuffer(0)) * 256& + CLng(.MqttBuffer(1))
                                MqttSendAck h, MQTT_PUBREL, 2, CInt(packetId)
                                MqttResetParser h
                                
                            Case MQTT_PUBREL
                                packetId = CLng(.MqttBuffer(0)) * 256& + CLng(.MqttBuffer(1))
                                MqttSendAck h, MQTT_PUBCOMP, 0, CInt(packetId)
                                MqttResetParser h
                                
                            Case Else
                                MqttResetParser h
                        End Select
                    End With
                End If
            Next i
        Else
            Exit Do
        End If
        DoEvents
    Loop
End Function

Private Function NullByteArray() As Byte()
    Dim b() As Byte
    NullByteArray = b
End Function

Private Function DeflatePayload(ByVal handle As Long, ByRef data() As Byte, ByVal dataLen As Long, ByRef outLen As Long) As Byte()
    Dim compBytes() As Byte
    Dim exactData() As Byte
    
    If dataLen = 0 Then
        outLen = 0
        DeflatePayload = exactData
        Exit Function
    End If
    
    If Not m_Connections(handle).CompressionHandler Is Nothing Then
        ReDim exactData(0 To dataLen - 1)
        CopyMemory exactData(0), data(LBound(data)), dataLen
        
        compBytes = m_Connections(handle).CompressionHandler.Deflate(exactData, m_Connections(handle).DeflateWindowBits, m_Connections(handle).DeflateContextTakeover)
        outLen = SafeArrayLen(compBytes)
        DeflatePayload = compBytes
    Else
        outLen = dataLen
        ReDim exactData(0 To dataLen - 1)
        CopyMemory exactData(0), data(LBound(data)), dataLen
        DeflatePayload = exactData
    End If
End Function

Private Function InflatePayload(ByVal handle As Long, ByRef data() As Byte, ByVal dataLen As Long, ByRef outLen As Long) As Byte()
    Dim decompBytes() As Byte
    Dim exactData() As Byte
    
    If dataLen = 0 Then
        outLen = 0
        InflatePayload = exactData
        Exit Function
    End If
    
    If Not m_Connections(handle).CompressionHandler Is Nothing Then
        ReDim exactData(0 To dataLen - 1)
        CopyMemory exactData(0), data(LBound(data)), dataLen
        
        decompBytes = m_Connections(handle).CompressionHandler.Inflate(exactData, m_Connections(handle).InflateWindowBits, m_Connections(handle).InflateContextTakeover)
        outLen = SafeArrayLen(decompBytes)
        InflatePayload = decompBytes
    Else
        outLen = dataLen
        ReDim exactData(0 To dataLen - 1)
        CopyMemory exactData(0), data(LBound(data)), dataLen
        InflatePayload = exactData
    End If
End Function
